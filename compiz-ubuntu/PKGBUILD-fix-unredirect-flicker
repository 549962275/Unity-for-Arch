# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_ubuntu_ver=0.9.8.0
_ubuntu_rel=0ubuntu1

pkgname=compiz-ubuntu-bzr
pkgver=0
pkgrel=1
pkgdesc="OpenGL window and compositing manager"
url="http://www.compiz.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('boost-libs' 'dbus' 'fuse' 'glibmm' 'gnome-control-center' 'librsvg' 'libxcomposite' 'libxdamage' 'libxinerama' 'libxrandr' 'libxslt' 'mesa' 'metacity' 'startup-notification' 'protobuf' 'pyrex' 'gsettings-desktop-schemas')
makedepends=('boost' 'cmake' 'intltool' 'libwnck')
provides=("compiz-ubuntu=0.9.8.2")
conflicts=('compiz-ubuntu')
options=('emptydirs')
install=compiz-ubuntu.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/compiz_${_ubuntu_ver}-${_ubuntu_rel}.diff.gz"
        'compiz.reset'
        '0002_Fix_cmake_install_dir.patch'
        '0003_Fix_python_install_command.patch'
        '0004_Use_python2.patch')
sha512sums=('858b9aacba030f4860d452c8a7af59fa66fa3613478f61713b418be453f8e533e556737dc52d8ada18802a2eeb8a4edc8614ff6c8bc1e0d38a0f2a969e301ac2'
            'cf0324a656e855ff26b624386d287ae4d90ede8f14d8eb7e613591de1dd90c7eb00ebfbb401802caa6e6e9aac99031e9ddd8a966ed23513b9e33b529f3b9519c'
            'ba80a6e5c3a7b488974046f8c1b906bb178691a9224b5c8f3542e0948099e2291b8888fbd4f591eaa16f9209e7288f11e122d1673731e7f1aa7c84aa6e3207c6'
            'bd7c8c0bf4cb3767c5b4b6d9c2d79e958ff9f34375a8223b509aec16b3682e219568e17378fa392f6ca4267f695835d83ef1c5db12898ea7b7cc75e1151d02b2'
            'fb0b06b8d82ffe43b54b430233c1fd53649c9c28cf85a281e3e306553eb6e568efe70e0c3500399fe31bea4aedc58f4745d604ed633b5700e9173bcdba0f978a')

_bzrtrunk="lp:~vanvugt/compiz/fix-unredirect-flicker"
_bzrmod=compiz

build() {
  cd "${srcdir}"

  if [[ -d "${_bzrmod}" ]]; then
    cd "${_bzrmod}" && bzr pull "${_bzrtrunk}" -r "${pkgver}"; cd ..
  else
    bzr branch "${_bzrtrunk}" "${_bzrmod}" -q -r "${pkgver}"
  fi

  [ -d ${_bzrmod}-build ] && rm -rv ${_bzrmod}-build
  cp -rv ${_bzrmod}{,-build} && cd ${_bzrmod}-build

  patch -Np1 -i "${srcdir}/0002_Fix_cmake_install_dir.patch"
  patch -Np1 -i "${srcdir}/0003_Fix_python_install_command.patch"
  patch -Np1 -i "${srcdir}/0004_Use_python2.patch"
  patch -Np1 -i "${srcdir}/compiz_${_ubuntu_ver}-${_ubuntu_rel}.diff" || true

  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  LDFLAGS="$(echo ${LDFLAGS} | sed 's/-B[ ]*symbolic-functions//')"
  export COMPIZ_DISABLE_RPATH=1

  [[ -d build ]] && rm -rvf build
  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_DEFAULT_PLUGINS="ccp" \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DUSE_GSETTINGS=ON \
    -DCOMPIZ_DISABLE_GS_SCHEMAS_INSTALL=OFF \
    -DCOMPIZ_BUILD_TESTING=OFF \
    -DCOMPIZ_DISABLE_PLUGIN_KDE=ON \
    -DUSE_KDE4=OFF

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_bzrmod}-build/build"
  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 make install DESTDIR="${pkgdir}"

  CMAKE_DIR=$(cmake --system-information | grep '^CMAKE_ROOT' \
    | awk -F\" '{print $2}')
  install -dm755 "${pkgdir}${CMAKE_DIR}/Modules/"
  install -m644 ../cmake/FindCompiz.cmake \
    "${pkgdir}${CMAKE_DIR}/Modules/"

  install -dm755 "${pkgdir}/usr/share/doc/compiz/"
  install ../{AUTHORS,NEWS,README} \
    "${pkgdir}/usr/share/doc/compiz/"

  install -dm755 "${pkgdir}/etc/compizconfig/upgrades/"
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -dm755 "${pkgdir}/usr/share/gnome/wm-properties/"
  install -dm755 "${pkgdir}/usr/share/gnome-control-center/keybindings/"
  install -dm755 "${pkgdir}/usr/share/man/man1/"

  install -m644 ../debian/{ccsm,compiz,gtk-window-decorator}.1 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${pkgdir}/usr/share/applications/compiz.desktop" "${pkgdir}/usr/share/gnome/wm-properties/"
  install -m755 ../debian/65compiz_profile-on-session "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m644 ../debian/unity.ini "${pkgdir}/etc/compizconfig/"
  install -m644 ../debian/config "${pkgdir}/etc/compizconfig/"
  install -m644 ../debian/compiz-gnome.gsettings-override "${pkgdir}/usr/share/glib-2.0/schemas/10_compiz-ubuntu.gschema.override"
  install "${srcdir}/compiz.reset" "${pkgdir}/usr/bin/compiz.reset"

  pushd ../debian/profile_upgrades/
  find . -type f -name '*.upgrade' -exec install -m644 {} "${pkgdir}"/etc/compizconfig/upgrades/{} \;
  popd

  pushd ../postinst/convert-files/
  find . -type f -name '*.convert' -exec install -m644 {} "${pkgdir}"/usr/lib/compiz/migration/{} \;
  popd

  find ../*/gtk/gnome/ -name *.xml -exec install {} "${pkgdir}/usr/share/gnome-control-center/keybindings/" \;
}

# vim:set ts=2 sw=2 et:
