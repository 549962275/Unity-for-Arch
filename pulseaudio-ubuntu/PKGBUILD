# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

# This packages is currently required by the gnome-control-center-ubuntu
# package because of the git patches that it requires. When PulseAudio 2.0
# is out, this package should be able to be removed.

pkgbase=pulseaudio-ubuntu
pkgname=("pulseaudio-ubuntu" "libpulse-ubuntu")
pkgdesc="A featureful, general-purpose sound server"
_ubuntu_rel=0ubuntu15
pkgver=1.1.${_ubuntu_rel}
pkgrel=101
arch=('i686' 'x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
makedepends=('libasyncns' 'libcap' 'attr' 'libxtst' 'libsm' 'libsamplerate' 'libtool' 'rtkit' 'speex' 'tdb' 'udev' 'dbus-core' 'avahi' 'bluez' 'gconf' 'intltool' 'jack' 'lirc-utils' 'openssl' 'fftw' 'orc' 'json-c' 'gtk2')
options=('!emptydirs' '!libtool')
source=("http://freedesktop.org/software/${pkgbase%-*}/releases/${pkgbase%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        "${pkgbase%-*}.xinit"
        '0001-Remove-usage-of-deprecated-udev_get_-_path.patch')
sha512sums=('6cd24259ad27f272c9069f4474c63208e431b802858f8485e601a7335a92787701a4a51b3f6b11d3bf2c5c04b4d9ec8c3b33f8dd3c0dda674c15da7c0c78cbba'
            'ffd4f2786d4e134387b9abd41d4cad264f3b70d4e16b7b60668e2b41fcb8c463f29f0f1b2a438d10c2578f5f47c99b6e36e383fbd59fbe7761aa94e2bbecf49c'
            'd6d2fae73a80f7e470dd858c7d5f917348d536e146f551776abf1fbea671febeeb74e44daeb1a03e2626d3fc946ce659eec78c22728f95ed4be73be9e7829f4a'
            'e8e9b3ae1a505a5cf10f525505f0086ee8c1c504a435dafb0cfdd95d8ea17368002fd266271a2240e74fdb6c36bdfde54703d5a62a89aa6d42fa108df1c353f5')

build() {
  cd "${srcdir}/${pkgbase%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase%-*}_${pkgver%.*}-${_ubuntu_rel}.diff"

  # Disable patches
    # Disable loading of Ubuntu specific configuration files
      sed -i '/0003-esd-honour-system-pulseaudio.patch/d' debian/patches/series

  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  patch -Np1 -i "${srcdir}/0001-Remove-usage-of-deprecated-udev_get_-_path.patch"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-database=tdb \
    --disable-hal \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package_pulseaudio-ubuntu() {
  depends=("libpulse-ubuntu=${pkgver}-${pkgrel}" 'rtkit' 'libtool' 'speex' 'tdb' 'udev' 'fftw' 'orc' 'libsamplerate')
  optdepends=('avahi: zeroconf support'
              'bluez: bluetooth support'
              'gconf: configuration through gconf (paprefs)'
              'jack: jack support'
              'lirc-utils: infra-red support'
              'openssl: RAOP support'
              'python2-pyqt: Equalizer GUI (qpaeq)')
  provides=("pulseaudio=${pkgver%.*}")
  conflicts=('pulseaudio')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install

  cd "${srcdir}/${pkgbase%-*}-${pkgver%.*}"
  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}" install

  # Lower resample quality, saves CPU
  sed -e '/resample-method/iresample-method=speex-float-0' \
      -i "${pkgdir}/etc/pulse/daemon.conf"

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "${pkgdir}/usr/bin/start-pulseaudio-x11"

  # Make ConsoleKit optional
  sed -e $'/load-module module-console-kit/{i.nofail\n;a.fail\n;}' \
      -i "${pkgdir}/etc/pulse/default.pa"

  # Python fix
  sed -i '1s:python$:&2:' "${pkgdir}/usr/bin/qpaeq"

  install -Dm755 "${srcdir}/pulseaudio.xinit" "${pkgdir}/etc/X11/xinit/xinitrc.d/pulseaudio"

  rm "${pkgdir}/etc/dbus-1/system.d/pulseaudio-system.conf"

### Split libpulse

  mkdir -p "${srcdir}"/libpulse/{etc/pulse,usr/{lib,share/man/man5}}

  mv {"${pkgdir}","${srcdir}/libpulse"}/etc/pulse/client.conf

  mv "${pkgdir}"/usr/lib/libpulse{,dsp,-simple,-mainloop-glib}.so* \
     "${pkgdir}"/usr/lib/libpulsecommon-*.so \
     "${srcdir}/libpulse/usr/lib"

  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/lib/pkgconfig
  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/include

  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/share/man/man5/pulse-client.conf.5
  mv {"${pkgdir}","${srcdir}/libpulse"}/usr/share/vala

### Ubuntu stuff

  # man page symlinks
  ln -s /usr/share/man/man1/pacat.1.gz "${pkgdir}/usr/share/man/man1/pamon.1.gz"
  ln -s /usr/share/man/man1/pacat.1.gz "${pkgdir}/usr/share/man/man1/parec.1.gz"
  ln -s /usr/share/man/man1/pacat.1.gz "${pkgdir}/usr/share/man/man1/parecord.1.gz"

  # esound compat binary symlink
  ln -s /usr/bin/esdcompat "${pkgdir}/usr/bin/esd"

  # pm-utils script
  install -dm755 "${pkgdir}/usr/lib/pm-utils/sleep.d/"
  install -m755 debian/01PulseAudio "${pkgdir}/usr/lib/pm-utils/sleep.d/"
}

package_libpulse-ubuntu() {
  pkgdesc="${pkgdesc} (client library)"
  depends=('dbus-core' 'libasyncns' 'libcap' 'libxtst' 'libsm' 'libsndfile' 'json-c')
  optdepends=('alsa-plugins: ALSA support'
              'avahi: zeroconf support')
  provides=("libpulse=${pkgver%.*}")
  conflicts=('libpulse')
  backup=('etc/pulse/client.conf')

  mv "${srcdir}"/libpulse/* "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
