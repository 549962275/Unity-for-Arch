# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=ido
pkgname=('libido' 'libido3')
pkgver=0.3.1
pkgrel=100
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/0.3/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('65773e4acec5a536f7fcc74bd69f00f0368dc4ff4d16db731859e48a32905d1c65036a0afcb8cca77d449d69bef433c853cb5c05fbfb70e3313e3c3680fb698e')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --disable-static --with-gtk=3
  make
  popd
}

package_libido3() {
  pkgdesc="Widgets and other objects used for indicators - GTK 3 library"
  depends=('gtk3-ubuntu')
  provides=("ido=${pkgver}")
  conflicts=('ido')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_libido() {
  pkgdesc="Widgets and other objects used for indicators - GTK 2 library"
  depends=('gtk2-ubuntu')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
}
