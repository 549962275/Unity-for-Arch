# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=ido
pkgname=('libido' 'libido3')
_ubuntu_rel=0ubuntu2
pkgver=0.3.3.${_ubuntu_rel}
pkgrel=100
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/0.3/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('1683fce2d88e77079c978fa79adb8259ec2ba1e5ee1e39fcd01f499fa67b6032c90ce24ad4199e4497e4360407b7018afd070190f4ca0b910ab93aa20c20d1e6'
            '8948c01e81c27d9fd00ce50038af17bfcd93355a67ba8174b6e3659cf674bed43106c31059d155d68ee3e399c47d50c806d70230d517d60cef37c3e2241920fe')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/series/${i}"
  done

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --disable-static --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --disable-static --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_libido3() {
  pkgdesc="Widgets and other objects used for indicators - GTK 3 library"
  depends=('gtk3-ubuntu')
  provides=("ido=${pkgver%.*}")
  conflicts=('ido')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_libido() {
  pkgdesc="Widgets and other objects used for indicators - GTK 2 library"
  depends=('gtk2-ubuntu')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
}
