# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=ido
_actual_ver=13.10.0
_extra_ver=+14.04.20140328
pkgver=${_actual_ver}daily14.03.28
pkgrel=1
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu' 'glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'vala' 'xorg-util-macros')
groups=('unity')
provides=('ido3')
conflicts=('ido3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('655ed64453137f40fdcc770a9cd6799988240517937eab1bbed9dce899f729e16c9dc7626a6e56d9c3118ce489145e85b8bd82592a8614a1ede2dff3459aeb01')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
