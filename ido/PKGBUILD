# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=ido
_actual_ver=13.10.0
_extra_ver=+14.04.20140407
pkgver=${_actual_ver}daily14.04.07
pkgrel=1
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu' 'glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'vala' 'xorg-util-macros')
groups=('unity')
provides=('ido3')
conflicts=('ido3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('10bd936fe0bf11b3fa330fe453fd4a4f2bad30bb1acee8dff7ec760954b9e2151e9a38bef5cc80cecca7aa5b9f34859c727cdd737d2da8b8114354fc1a89781e')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
