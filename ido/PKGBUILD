# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=ido
_actual_ver=13.10.0
_extra_ver=+15.04.20150130
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Widgets and other objects used for indicators"
arch=(i686 x86_64)
url="https://launchpad.net/ido"
license=(LGPL)
depends=(gtk3-ubuntu)
makedepends=(gobject-introspection gtk-doc vala xorg-util-macros)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Temporarily-disable-previous-button-drop-shadow-draw.patch)
sha512sums=('7bd75d90b6946d66169569fe9473f37dbf2be62640977cdae845c28e5f9eb1ab2236c303d68e36fb21db42fcc10fbd938ee3729f4756e5410d07f8a6983b73cd'
            '91d19642868a3859a860fc1a32541141472b8af46d9d2fa6983dd41ec6e0497ca6397a836a0921ba000e26be2be5b8d52ae74b0c5c29b4b73e4f6bce67f9b60d')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Temporarily-disable-previous-button-drop-shadow-draw.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
