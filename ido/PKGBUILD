# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=ido
_actual_ver=13.10.0
_extra_ver=+14.04.20140321
pkgver=${_actual_ver}daily14.03.21
pkgrel=1
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu' 'glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'vala' 'xorg-util-macros')
groups=('unity')
options=('!libtool')
provides=('ido3')
conflicts=('ido3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('4d996988e0bce7d2686bb4c115cfe6f1c519bf637dde2fb3232347bbf5e16cccb16a313d255d58f9b206709350a51c962b987e2f58543e6fbed4919613a9bb06')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --disable-introspection
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
