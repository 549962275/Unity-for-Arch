# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=compizconfig-python-ubuntu
_ubuntu_ver=0ubuntu2
pkgver=0.9.5.94.${_ubuntu_ver}
pkgrel=1
pkgdesc="Compizconfig bindings for python - Ubuntu version"
arch=('i686' 'x86_64')
url="http://compiz.org"
license=('GPL')
depends=('compiz-core-ubuntu' 'libcompizconfig-ubuntu' 'glib2' 'python2' 'pyrex')
makedepends=('intltool' 'pkgconfig' 'cython')
provides=('compizconfig-python')
conflicts=('compizconfig-python')
groups=('unity')
source=("http://archive.ubuntu.com/ubuntu/pool/universe/c/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}.orig.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/universe/c/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}-${_ubuntu_ver}.diff.gz")
sha512sums=('88bb4e9f481351209c2b05d0f921f86ab4c73ed1dc3db47f9f341b408d6327fbc80adc3901c3e8dc0e5272c0b45cead09c50986b264c01eac94067da2ee6b536'
            'a43758a3cf1c0f96080e8aa973fef205eaa4fa1dfe21030c215f63367df5e6bfae8a3d79769025e534755ea84b39cc862c5868baad5998ea95ea0785ffd3496a')

build() {
  cd "${srcdir}"
  cd "${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/compizconfig-python_${pkgver%.*}-${_ubuntu_ver}.diff"
  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  python2 setup.py install --prefix="/usr" --root="${pkgdir}"
}
