# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-2d
_ubuntu_rel=0ubuntu1
pkgver=5.10.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="Unity interface for non-accelerated graphics cards"
arch=('i686' 'x86_64')
url="http://launchpad.net/unity-2d"
license=('GPL')
depends=('qt-ubuntu>=4.8.0.1ubuntu11-101' 'libdbusmenu-qt' 'libindicator' 'libqtbamf' 'libqtdee' 'libqtgconf' 'metacity-ubuntu' 'unity-lens-files' 'unity-lens-applications' 'unity-asset-pool' 'nux' 'unity-core' 'nautilus-ubuntu' 'gnome-session-ubuntu' 'gnome-themes-standard' 'gconf-ubuntu' 'xorg-server>=1.12.0-100' 'dconf-qt')
makedepends=('cmake')
conflicts=('unity2d-bzr')
replaces=('unity2d-bzr')
groups=('unity')
optdepends=('appmenu-gtk: Export GTK menus over DBus'
            'appmenu-qt: appmenu support for Qt')
options=('!emptydirs')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        "launchpad-export-2d.tar.gz::http://ubuntuone.com/0SVDsqeV12zqz4FfcIVwMZ" # Snapshot of translations from Launchpad as of 2012-03-03
        'gcc_4.7.0_build_fix.patch')
sha512sums=('90c82aa1a35238988fc23053022c68b81dc450af98d2f114a4fb5d42b87c49caade22f1579b9e334231c6bc7f7c5e35658aba033c0d3f0a8b41fda7c18c81376'
            '444f5d24d6ee7c260a171f0585c4ac98ee8350063171438e4ae9a01755a859e441a9d33a9e4fcb7714b67a2c3c6321693c5bd7f8a972897d1a43e2bb6d08cad1'
            '8cfd6d7f852b7ccf759f48b788a8239e3df732a10d81ede29fc6caaf95bc106f0ba6105c192b619a08945d6286f3e4ee59b5c690837f7a74a5fe7a705c70dcbc'
            '4905a05fb519e3f18a200d634e72dbf571e451090629c7d3e320d170b97f9397cd50579978f6a751bce43635f17a5d574bd640e629d7e7821789bbec359ecfdf')

# https://bugs.launchpad.net/unity-2d/+bug/809205
export NO_PNG_PKG_MANGLE=1

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Install translations from Launchpad
  pushd po
  rm -f LINGUAS && touch LINGUAS # Recreate LINGUAS (list of languages) file
  for i in "${srcdir}"/po/*.po; do
    _NEWFILE=${i##*/} # Strip path
    _NEWFILE=${_NEWFILE##*-} # Strip 'unity-2d-'
    cp "${i}" "${_NEWFILE}" # Copy new translation
    echo "${_NEWFILE%.*}" >> LINGUAS # Add translation
  done
  cp "${srcdir}/po/unity-2d.pot" unity-2d.pot
  popd

  # Fix Qt4 imports directory
  sed -i 's|qt4/imports|qt/imports|' libunity-2d-private/Unity2d/CMakeLists.txt

  # GCC 4.7.0 build fix
  patch -Np1 -i "${srcdir}/gcc_4.7.0_build_fix.patch"

  mkdir build
  cd build
  cmake .. \
    `#-DCMAKE_BUILD_TYPE=Release` \
    -DCMAKE_INSTALL_PREFIX=/usr \
    `#-DCMAKE_VERBOSE_MAKEFILE=ON` \
    -DCMAKE_INSTALL_LIBDIR=lib

  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  pushd build
  make DESTDIR="${pkgdir}/" install
  popd

  # Install GConf stuff
  install -dm755 "${pkgdir}/usr/share/gconf/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"
  install -m644 debian/gconf/ubuntu-2d.default.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/gconf/ubuntu-2d.mandatory.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/20_ubuntu-2d-gconf-mandatory "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -m644 debian/20_ubuntu-2d-gconf-default "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"

  # Install manuals
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  for i in debian/manpages/*; do
    install -m644 "${i}" "${pkgdir}/usr/share/man/man1/"
  done
}

# vim:set ts=2 sw=2 et:
