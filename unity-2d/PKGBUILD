# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-2d
_ubuntu_rel=0ubuntu1.1
pkgver=5.12.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="Unity interface for non-accelerated graphics cards"
arch=('i686' 'x86_64')
url="http://launchpad.net/unity-2d"
license=('GPL')
depends=('qt-ubuntu>=4.8.0.1ubuntu11-101' 'libdbusmenu-qt' 'libindicator' 'libqtbamf' 'libqtdee' 'libqtgconf' 'metacity-ubuntu' 'unity-lens-files' 'unity-lens-applications' 'unity-asset-pool' 'nux' 'unity-core' 'nautilus-ubuntu' 'gnome-session-ubuntu' 'gnome-themes-standard' 'gconf-ubuntu' 'xorg-server>=1.12.0-100' 'dconf-qt')
makedepends=('cmake')
conflicts=('unity2d-bzr')
replaces=('unity2d-bzr')
groups=('unity')
optdepends=('appmenu-gtk: Export GTK menus over DBus'
            'appmenu-qt: appmenu support for Qt')
options=('!emptydirs')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*.*}-${_ubuntu_rel}.diff.gz"
        "launchpad-export-2d.tar.gz::http://ubuntuone.com/0Zbt9BBlGbHMah8y1tCjGA" # Snapshot of translations from Launchpad as of 2012-04-26
        'gcc_4.7.0_build_fix.patch')
sha512sums=('fbc080d30fc1dcdb130c3227f862729452301fcbb5bdce89495f54612b0efbbac7109307455da13c168dcc576cf433f888fbfb43fb366486ce79cb7a018975aa'
            '712c0c126e1db83820d8db41c94cab12188a09698f7f1944482e96520dfaeac32bee3e7a0ac7e2184c6cf72aa2815df97f90d37289093439e502c0d36cf23d29'
            '1912a66eb9e3f4fca8d821ecd2e077291620346b3e603201c50d9694fcad3377606434d3928a25463ece275ce9a814ba6e46fc8ccb2e94ca561fccf56b871d5a'
            '4905a05fb519e3f18a200d634e72dbf571e451090629c7d3e320d170b97f9397cd50579978f6a751bce43635f17a5d574bd640e629d7e7821789bbec359ecfdf')

# https://bugs.launchpad.net/unity-2d/+bug/809205
export NO_PNG_PKG_MANGLE=1

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Install translations from Launchpad
  pushd po
  rm -f LINGUAS && touch LINGUAS # Recreate LINGUAS (list of languages) file
  for i in "${srcdir}"/po/*.po; do
    _NEWFILE=${i##*/} # Strip path
    _NEWFILE=${_NEWFILE##*-} # Strip 'unity-2d-'
    cp "${i}" "${_NEWFILE}" # Copy new translation
    echo "${_NEWFILE%.*}" >> LINGUAS # Add translation
  done
  cp "${srcdir}/po/unity-2d.pot" unity-2d.pot
  popd

  # Fix Qt4 imports directory
  sed -i 's|qt4/imports|qt/imports|' libunity-2d-private/Unity2d/CMakeLists.txt

  # GCC 4.7.0 build fix
  patch -Np1 -i "${srcdir}/gcc_4.7.0_build_fix.patch"

  mkdir build
  cd build
  cmake .. \
    `#-DCMAKE_BUILD_TYPE=Release` \
    -DCMAKE_INSTALL_PREFIX=/usr \
    `#-DCMAKE_VERBOSE_MAKEFILE=ON` \
    -DCMAKE_INSTALL_LIBDIR=lib

  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver%.*.*}"
  pushd build
  make DESTDIR="${pkgdir}/" install
  popd

  # Install GConf stuff
  install -dm755 "${pkgdir}/usr/share/gconf/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"
  install -m644 debian/gconf/ubuntu-2d.default.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/gconf/ubuntu-2d.mandatory.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/20_ubuntu-2d-gconf-mandatory "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -m644 debian/20_ubuntu-2d-gconf-default "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"

  # Install manuals
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  for i in debian/manpages/*; do
    install -m644 "${i}" "${pkgdir}/usr/share/man/man1/"
  done
}

# vim:set ts=2 sw=2 et:
