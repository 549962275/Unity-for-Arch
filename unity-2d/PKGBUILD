# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-2d
_ubuntu_rel=0ubuntu1
pkgver=5.4.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="unity-2d project is a 2d implementation of Unity"
arch=('i686' 'x86_64')
url="http://launchpad.net/unity-2d"
license=('GPL')
depends=('qt' 'libwnck' 'libdbusmenu-qt' 'libindicator' 'libqtbamf' 'libqtdee' 'utouch-geis' 'libqtgconf' 'metacity-ubuntu' 'unity-lens-files' 'unity-lens-applications' 'unity-asset-pool' 'nux' 'unity-core' 'nautilus-ubuntu' 'gnome-session-ubuntu' 'gnome-themes-standard')
makedepends=('cmake')
conflicts=('unity2d-bzr')
replaces=('unity2d-bzr')
groups=('unity')
optdepends=('appmenu-gtk: Export GTK menus over DBus'
            'appmenu-qt: appmenu support for Qt')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        "launchpad-export-2d.tar.gz::http://ubuntuone.com/0SVDsqeV12zqz4FfcIVwMZ" # Snapshot of translations from Launchpad as of 2012-03-03
        'disable_utouch.patch')
sha512sums=('d7b59042cf486caf69ef11262201bcd83484784dddb57641704a7ab40819cedd7082b95d2e2183c528b6dc7077ce24524c8e9f1798065b326f2bf666e4749a1e'
            '3244e292779f1151f29013fb441bcc522e57143b2903b23983b65140fdd77b2f76ba6ee215d83524ca89d12ef96a29b72711ed232fcc60b8c2c78307edf86e82'
            '8cfd6d7f852b7ccf759f48b788a8239e3df732a10d81ede29fc6caaf95bc106f0ba6105c192b619a08945d6286f3e4ee59b5c690837f7a74a5fe7a705c70dcbc'
            '28607fac4dd648b3779abe035e2306b29f8428c663d3b1fb8739b4a1c528393eee6bffdaa03af94e8442d811243a2acfc59407bed432b03d85a91a97707da058')

# https://bugs.launchpad.net/unity-2d/+bug/809205
export NO_PNG_PKG_MANGLE=1

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Uncomment to disable uTouch (if xorg-*-ubuntu ans utouch-* wasn't
  # installed). Patch was made by City-Busz.
  #patch -Np1 -i "${srcdir}/disable_utouch.patch"

  # Install translations from Launchpad
  pushd po
  rm -f LINGUAS && touch LINGUAS # Recreate LINGUAS (list of languages) file
  for i in "${srcdir}"/po/*.po; do
    _NEWFILE=${i##*/} # Strip path
    _NEWFILE=${_NEWFILE##*-} # Strip 'unity-2d-'
    cp "${i}" "${_NEWFILE}" # Copy new translation
    echo "${_NEWFILE%.*}" >> LINGUAS # Add translation
  done
  cp "${srcdir}/po/unity-2d.pot" unity-2d.pot
  popd

  # Fix Qt4 imports directory
  sed -i 's|qt4/imports|qt/imports|' libunity-2d-private/Unity2d/CMakeLists.txt

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  pushd build
  make DESTDIR="${pkgdir}/" install
  popd

  # Install GConf stuff
  install -dm755 "${pkgdir}/usr/share/gconf/"
  install -dm755 "${pkgdir}/usr/share/gconf/defaults/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"
  install -m644 debian/gconf/ubuntu-2d.default.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/gconf/ubuntu-2d.mandatory.path "${pkgdir}/usr/share/gconf/"
  install -m644 debian/unity-2d.gconf-defaults "${pkgdir}/usr/share/gconf/defaults/10_${pkgname}"
  install -m644 debian/20_ubuntu-2d-gconf-mandatory "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -m644 debian/20_ubuntu-2d-gconf-default "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"

  # Install manuals
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  for i in debian/manpages/*; do
    install -m644 "${i}" "${pkgdir}/usr/share/man/man1/"
  done
}
