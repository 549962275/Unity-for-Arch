# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-2d
_ubuntu_rel=0ubuntu1
pkgver=5.2.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="unity-2d project is a 2d implementation of Unity"
arch=('i686' 'x86_64')
url="http://launchpad.net/unity-2d"
license=('GPL')
depends=('qt' 'libwnck' 'libdbusmenu-qt' 'libindicator' 'libqtbamf' 'libqtdee' 'utouch-geis' 'libqtgconf' 'metacity-ubuntu' 'unity-lens-files' 'unity-lens-applications' 'unity-asset-pool' 'nux' 'unity-core' 'nautilus-ubuntu' 'gnome-session-ubuntu' 'gnome-themes-standard')
makedepends=('cmake')
conflicts=('unity2d-bzr')
replaces=('unity2d-bzr')
groups=('unity')
optdepends=('appmenu-gtk: Export GTK menus over DBus'
            'appmenu-qt: appmenu support for Qt')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'disable_utouch.patch')
sha512sums=('ef3fcbab0836ee231dc6dbf9ca94690d0ffdc0056aa6d1271a18d72dbb02430d0477b4534796436ded135040ce03e2bb0444b84209be29ad25312ddbbdda4f83'
            'b50693649b615e5c8735f862853faa75bd04a180317148370f9bdf93329da6318bffd48339c34714419254ef2d909d19dedf7195b9479d0b190861155b3bfbc5'
            '28607fac4dd648b3779abe035e2306b29f8428c663d3b1fb8739b4a1c528393eee6bffdaa03af94e8442d811243a2acfc59407bed432b03d85a91a97707da058')

# https://bugs.launchpad.net/unity-2d/+bug/809205
export NO_PNG_PKG_MANGLE=1

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Uncomment to disable uTouch (if xorg-*-ubuntu ans utouch-* wasn't
  # installed). Patch was made by City-Busz.
  #patch -Np1 -i "${srcdir}/disable_utouch.patch"

  # Fix Qt4 imports directory
  sed -i 's|qt4/imports|qt/imports|' libunity-2d-private/Unity2d/CMakeLists.txt

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  cd 'build'
  make DESTDIR="${pkgdir}/" install

  # Install GConf stuff
  install -dm755 "${pkgdir}/usr/share/gconf/"
  install -dm755 "${pkgdir}/usr/share/gconf/defaults/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -dm755 "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"
  install -m644 "${srcdir}/debian/gconf/ubuntu-2d.default.path" "${pkgdir}/usr/share/gconf/"
  install -m644 "${srcdir}/debian/gconf/ubuntu-2d.mandatory.path" "${pkgdir}/usr/share/gconf/"
  install -m644 "${srcdir}/debian/unity-2d.gconf-defaults" "${pkgdir}/usr/share/gconf/defaults/10_${pkgname}"
  install -m644 "${srcdir}/debian/20_ubuntu-2d-gconf-mandatory" "${pkgdir}/usr/share/gconf/ubuntu-2d/mandatory/"
  install -m644 "${srcdir}/debian/20_ubuntu-2d-gconf-default" "${pkgdir}/usr/share/gconf/ubuntu-2d/default/"

  # Install manuals
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  for i in "${srcdir}"/debian/manpages/*; do
    install -m644 "${i}" "${pkgdir}/usr/share/man/man1/"
  done
}
