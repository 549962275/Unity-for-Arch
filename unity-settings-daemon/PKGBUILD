# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic

pkgname=unity-settings-daemon
_actual_ver=14.04.0
_extra_ver=+14.10.20140605
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
pkgdesc="Unity Settings Daemon"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-settings-daemon"
license=('GPL')
groups=('unity')
depends=('gnome-desktop-compat' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'ibus' 'libappindicator3' 'libcanberra-pulse' 'libnotify' 'librsvg' 'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower-compat')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl')
install=unity-settings-daemon.install
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Use-gnome-desktop-compat.patch'
        '0002-Remove-accountsservice-dependency.patch'
        '0003-Use-upower-compat.patch'
        '0004-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch'
        '0002-Remove-lid-close.patch')
sha512sums=('3e3a30effd8b006174bedc6e04e9552f15108f6aac18aa915b59f1958b9d3e86bffc42cebe2ee54b5055529cda92b0308e86500efaebbe4a1d727938e9fee1a3'
            'a6b95df3ac8bb05c217ecb5cca6da4a7c411d153064efa9a596f931d9bb878e6489ca2d88d566cf621c79c195c7104ad18b3309900b1f8d5e321ac9ac95076ae'
            'a81ed0404fce6381a13b546775eba90b5aef950c2ce32ebf70d49438841646cad54a82460cd77a4b42b5ad98d46c3e5cfbe298e6283b2d5a7e076eedc59ce59b'
            '38403438559dc7a00c0bf4d0ffecd019f1942e91cd9080b991ec5d9c2d192b13dd0f82d08edac19531e0366902d75dab6e1fd11e3a479289868c8376c8986189'
            'ecf114ce359893eb9b134ae901313f380d9243c9eb5cd4573bb29c0dc1c9c4681eb3c7b3d363d718220428f7760e21bcf514a3429af5c00afe91b368bb9dd7dc'
            '820f7ff0f073d61b8b5c26d17dd3df458f1c967e36fbd2fa27c959d1be4d2d0652027ac731f3ff2644a8407ec07566f337fcee502a540a3bcfb1d29696201c48')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Use-gnome-desktop-compat.patch"
  patch -p1 -i "${srcdir}/0002-Remove-accountsservice-dependency.patch"
  patch -p1 -i "${srcdir}/0003-Use-upower-compat.patch"
  patch -p1 -i "${srcdir}/0004-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch"
#  patch -p1 -i "${srcdir}/0002-Remove-lid-close.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/unity-settings-daemon \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
    "${pkgdir}/usr/bin/unity-settings-daemon"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
