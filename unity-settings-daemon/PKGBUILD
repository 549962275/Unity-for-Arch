# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=vivid

pkgname=unity-settings-daemon
_actual_ver=15.04.1
_extra_ver=+15.04.20150408
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Unity Settings Daemon"
arch=(i686 x86_64)
url="https://launchpad.net/unity-settings-daemon"
license=(GPL)
groups=(unity)
depends=(gnome-settings-daemon-ubuntu gsettings-desktop-schemas-ubuntu fcitx
         gperf hicolor-icon-theme ibus libappindicator3 libcanberra-pulse
         libnotify librsvg libsystemd libwacom libxkbfile mesa pulseaudio
         pulseaudio-alsa upower)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl python2)
conflicts=(gnome-desktop-compat upower-compat)
install=unity-settings-daemon.install
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Remove-accountsservice-dependency.patch
        0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch)
sha512sums=('a2cb4fe5c779d9490f3bda8de32878219df34b4feffe307f2babd487831c615abbaf79176793c0f62b13465294b52fc8b8c656bcbb218c9153f3158b083704e9'
            '4b8e984ac7284239a8e8b42c30b801c40709b107d9e651b9e82d46a6e33d45e2642b02d6027699ba72cb58e67a4f74e94544451f39d537111438354d2b4db082'
            'bc4c8af6cafbe170e95f9d73ca7848eb13b7a1fe2d4017a50d969258919a52463faac9adb195896d3d38ca47c596f90529be1d03b73cba0eecd7820c4003f70b')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Remove-accountsservice-dependency.patch
  patch -p1 -i ../0002-Add-gnome-settings-daemon-3.12-rfkill-plugin.patch

  # Temporarily disable since it fails to link to glib
  sed -i '/noinst_PROGRAMS += test-wm-button-layout-translations/d' plugins/xsettings/Makefile.am
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/unity-settings-daemon \
    --disable-static \
    --enable-systemd \
    --enable-fcitx

  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
    "${pkgdir}/usr/bin/unity-settings-daemon"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
