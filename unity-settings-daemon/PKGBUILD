# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-settings-daemon
_actual_ver=14.04.0
_extra_ver=+14.04.20140402
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Unity Settings Daemon"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-settings-daemon"
license=('GPL')
groups=('unity')
depends=('gnome-desktop' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'ibus' 'libappindicator3' 'libcanberra-pulse' 'libnotify' 'librsvg' 'libsystemd' 'libwacom' 'packagekit' 'pulseaudio' 'pulseaudio-alsa' 'upower')
install=unity-settings-daemon.install
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "http://ftp.gnome.org/pub/GNOME/sources/gnome-desktop/3.8/gnome-desktop-3.8.4.tar.xz"
        '0001-Remove-accountsservice-deps.patch'
        '0002-Remove-lid-close.patch'
        '0001-power-Use-up_client_get_critical_action.patch'
        '0002-power-Update-for-libupower-glib-API-changes.patch')
sha512sums=('97f7c0d9611bd58a7c868cdf6e4c27ec7a9553048e6af7b08555088d7f43fd31d78af3a006d4f9a36cb08e58685cd361fbe5a31e87baccdca6fe0a473ed20e04'
            'e5932bd73f0be85b79dc63af1766633c56044078313ea764a7faabaaabb52af8ffcc59361a0d65509626fa83c6afcc2ac1fb4e64b49ad06d220994dcfc10c31d'
            '3b1a7a01af22163687f4a9b99ef63783b8412a99141a8d0efd575503c1e7f98fc89e9a4fc5b9480e4f007958e6c518dd94093882d7e4e2feadcf4572243b0d7e'
            '820f7ff0f073d61b8b5c26d17dd3df458f1c967e36fbd2fa27c959d1be4d2d0652027ac731f3ff2644a8407ec07566f337fcee502a540a3bcfb1d29696201c48'
            '0e547535a824918ae77c7c3fb725ae0b0eab12770bae4cd6d0b8ef7eb97eadace0029d93909bb3bba64a23951f7b44c9dac4d171400da89f4bba7fde6b84a43e'
            '0481c5606d4253fb07e475d81957d7ec1e74a3d206ba08e412d055e285e0a837b56e4fd63f3f3a899fe97299833f4787800e2ddfd873872f07549a97ad962368')

build_gnome_desktop() {
  cd "${srcdir}/gnome-desktop-3.8.4"

  CFLAGS="-fPIC" \
  ./configure \
    --prefix=/usr \
    --enable-static=yes \
    --enable-shared=no \
    --disable-desktop-docs \
    --enable-gtk-doc=no

  make
  make install DESTDIR="${srcdir}/gdinst"

  sed -i "s,^prefix=.*$,prefix=${srcdir}/gdinst/usr,g" \
    "${srcdir}/gdinst/usr/lib/pkgconfig/gnome-desktop-3.0.pc"
}

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Remove-accountsservice-deps.patch"
  patch -p1 -i "${srcdir}/0002-Remove-lid-close.patch"
  patch -p1 -i "${srcdir}/0001-power-Use-up_client_get_critical_action.patch"
  patch -p1 -i "${srcdir}/0002-power-Update-for-libupower-glib-API-changes.patch"
}

build() {
  build_gnome_desktop

  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  CFLAGS="-fPIC" PKG_CONFIG_PATH="${srcdir}/gdinst/usr/lib/pkgconfig" \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/unity-settings-daemon \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
    "${pkgdir}/usr/bin/unity-settings-daemon"
}
