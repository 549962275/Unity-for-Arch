# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-settings-daemon
_actual_ver=14.04.0
_extra_ver=+14.10.20140605
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Unity Settings Daemon"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-settings-daemon"
license=('GPL')
groups=('unity')
depends=('gnome-desktop-compat' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'ibus' 'libappindicator3' 'libcanberra-pulse' 'libnotify' 'librsvg' 'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower-compat')
makedepends=('intltool')
install=unity-settings-daemon.install
#options=('!emptydirs')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-settings-daemon_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Use-gnome-desktop-compat.patch'
        '0002-Remove-accountsservice-dependency.patch'
        '0003-Use-upower-compat.patch'
        '0002-Remove-lid-close.patch')
sha512sums=('3e3a30effd8b006174bedc6e04e9552f15108f6aac18aa915b59f1958b9d3e86bffc42cebe2ee54b5055529cda92b0308e86500efaebbe4a1d727938e9fee1a3'
            '6e59b54a662be578de05db2c5627c36dd193d0320cc5b1fc9f5aa3438a6870f9dabba6906b9c2227ab078d813e4ea6cb6c785045d8e5b4a91660cb4e4b95892c'
            '407d6303796b24fdcc7e40ecf16e68c3588245c2c9970e30d79217fc33bf597b6d975176e4333ad34a2297fe10952785c518eb14f3ff113770606b3ac68445d7'
            '4f4ee627f6084a13dadff68c11ca504c98395af8de1aea812790647439382a143c430e4b3eef96de87e5fc221670bbc0ab34ed23564f541b4a74adae3f6bf5e4'
            '820f7ff0f073d61b8b5c26d17dd3df458f1c967e36fbd2fa27c959d1be4d2d0652027ac731f3ff2644a8407ec07566f337fcee502a540a3bcfb1d29696201c48')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Use-gnome-desktop-compat.patch"
  patch -p1 -i "${srcdir}/0002-Remove-accountsservice-dependency.patch"
  patch -p1 -i "${srcdir}/0003-Use-upower-compat.patch"
#  patch -p1 -i "${srcdir}/0002-Remove-lid-close.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/unity-settings-daemon \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s /usr/lib/unity-settings-daemon/unity-settings-daemon \
    "${pkgdir}/usr/bin/unity-settings-daemon"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
