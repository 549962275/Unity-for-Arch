# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=compiz-plugins-extra-ubuntu

_ubuntu_rel=0ubuntu1
_ubuntu_ver='~bzr9'
_actual_ver=0.9.7.0

pkgver=${_actual_ver}.bzr9.${_ubuntu_rel}
pkgrel=100
pkgdesc="Compiz extra plugins - Ubuntu version"
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
provides=('compiz-plugins-extra')
conflicts=('compiz-plugins-extra')
groups=('unity')
depends=('compiz-core-ubuntu' 'compiz-plugins-main-ubuntu' 'librsvg' 'cairo' 'libsm' 'dbus' 'libjpeg-turbo' 'libxslt' 'gconf-ubuntu' 'libnotify')
makedepends=('intltool' 'cmake' 'pkgconfig')
install=compiz-plugins-extra-ubuntu.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}${_ubuntu_ver}.orig.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('5681447344359af7b5bd803cf986d3e27e79a4a55d338d7b793c2f5611b0c2d08ccdd8cbe0d60f41f88fda1146e91f524f40209c071d3bbb0b63c4dfef549714'
            '591d6f48931ec765d7572747c7db355c9d26af908e3a7a8e51181fb95e7d3cda81f6bbcb3d000ffde226170aaee343b078cb92735fa5810c6edc81ee9595f4bc')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  [[ -d build ]] || mkdir build
  cd build
  cmake .. \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE="compiz" \
    -DCMAKE_INSTALL_PREFIX=/usr

  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}/build"
  make DESTDIR="${pkgdir}" install
}

