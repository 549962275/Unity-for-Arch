# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-music
_actual_ver=6.9.0
_extra_ver=+14.10.20141008
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1
pkgdesc="Music, in the dash"
arch=(i686 x86_64)
url="https://launchpad.net/unity-place-music"
license=(GPL)
depends=(gstreamer gst-plugins-base libgee06 libnotify liboauth libsoup libunity
         tdb)
makedepends=(vala intltool json-glib)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Remove-Ubuntu-One-support-service-is-dead.patch)
sha512sums=('fadfafaf1dde12d20972ce33afa56e0da211b7412a7c7f1125195908bc7d13842eae698a7ad8c69eb1c7ad4f697bd49239c2c854d4634b7b0862eafa1fbb7332'
            'a79f0c3891dce08536f9cf66fa024cc2b920685f4a1fb783ae081da81a0178bc0d6f87c61f4b187ad76d28437e5732fcdff519bff364bf857fee6206d0339d26')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Remove-Ubuntu-One-support-service-is-dead.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}
