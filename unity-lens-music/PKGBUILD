# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-music
_actual_ver=6.8.1
_extra_ver=daily12.12.05
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-music"
license=('GPL')
depends=('libunity' 'gstreamer0.10' 'gstreamer0.10-base')
makedepends=('vala' 'intltool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('83586a4036b8efbc005fc0237508a52904eb9e5cad1305460b249ec7263e299e0d07a0089f07313e49aaf2ed8efa8093af0e260ed51d67c7e073e0a5db69834c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
