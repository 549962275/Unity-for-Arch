# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-music
_actual_ver=6.8.1
_extra_ver=daily13.02.26
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=100
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-music"
license=('GPL')
depends=('libunity' 'gstreamer' 'gst-plugins-base' 'liboauth' 'libsoup' 'tdb')
makedepends=('vala' 'intltool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('becb87b8f21c97716a01b104c9246efe1da9f6e27cc2b4618c746131b27929ef4abaa96bf05d0732fa614b274a0b4d1017b893bc50a6b03130715572d053178f')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
