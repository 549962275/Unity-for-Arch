# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicate
pkgname=('libindicate' 'libindicate-gtk2' 'libindicate-gtk3')
_ubuntu_rel=0ubuntu1
pkgver=0.6.90.${_ubuntu_rel}
pkgrel=100
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=('libdbusmenu' 'dbus-glib' 'gtk2-ubuntu' 'gtk3-ubuntu' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'gnome-doc-utils')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/0.7/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('914d652309b31fc772daa9864f680f870a83be0fdabbb3bb447a9d7f457c3383859a7b894f06f8f9188fe495d0a1e46eb5ad14e8c65b41e545e59625a5a3a96b'
            '24d7269ce96f4c901e855be08992a8f99d26883d4a753c45ba425cf85c30ef8f1a33d61deb1bd9e9ecdd66f9ff5aeb73af7edbb6bd79054a9b48009235cbc455')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Python 2 fix
  sed -i "s|^\(#!.*python\)$|\12|" examples/{im-client,listen-and-print}.py
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  # libindicate-gtk2 won't install if libindicate isn't installed yet
  sed -i 's|$(LIBINDICATEGTK_CFLAGS)|$(LIBINDICATEGTK_CFLAGS) -L../libindicate/.libs|' libindicate-gtk/Makefile.{am,in}

  # Cannot find gmodule-2.0
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  autoreconf -vfi

  export MAKEFLAGS="-j1"
  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=2 --disable-static PYTHON=python2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=3 --disable-static PYTHON=python2
  make ${MAKEFLAGS}
  popd
}

package_libindicate() {
  depends=('libdbusmenu' 'dbus-glib')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  MAKEFLAGS="-j1"

  make -C libindicate DESTDIR="${pkgdir}" install
  make -C bindings/mono/indicate DESTDIR="${pkgdir}" install
  make -C tests DESTDIR="${pkgdir}" install
  make -C docs DESTDIR="${pkgdir}" install
}

package_libindicate-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("libindicate=${pkgver}" 'gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  MAKEFLAGS="-j1"

  make -C libindicate-gtk DESTDIR="${pkgdir}" install
  make -C bindings/python DESTDIR="${pkgdir}" install
  make -C bindings/mono/indicate-gtk DESTDIR="${pkgdir}" install
  make -C examples DESTDIR="${pkgdir}" install
}

package_libindicate-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=("libindicate=${pkgver}" 'gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  MAKEFLAGS="-j1"

  make -C libindicate-gtk DESTDIR="${pkgdir}" install
}
