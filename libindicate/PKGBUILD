# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicate
pkgname=('libindicate' 'libindicate-gtk2' 'libindicate-gtk3')
pkgver=0.6.92
pkgrel=100
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=('libdbusmenu' 'dbus-glib' 'gtk2-ubuntu' 'gtk3-ubuntu' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'gnome-doc-utils' 'perl-xml-libxml')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/0.7/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz"
        '0001_No_gmessages.h.patch')
sha512sums=('2ed7a3e8be3041e2a7663b5717ae136b5674d121467bfd08a694571ebc998300a4af7e9356ed30c44163b3d6faae5cca07e9940d424a753b2acc631a32a3bf03'
            '5b630fb9106e9f01bdf31b3241964f90657578e6f480e6602d56b7fbb3e611ed8ebc4bdb50d084e6ec77dc8be2ac879df4ca1ace591f2a1222a375e502c4759c')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Do not include glib/gmessages.h
  patch -Np1 -i "${srcdir}/0001_No_gmessages.h.patch"

  # Python 2 fix
  sed -i "s|^\(#!.*python\)$|\12|" examples/{im-client,listen-and-print}.py
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  # libindicate-gtk2 won't install if libindicate isn't installed yet
  sed -i 's|$(LIBINDICATEGTK_CFLAGS)|$(LIBINDICATEGTK_CFLAGS) -L../libindicate/.libs|' libindicate-gtk/Makefile.{am,in}

  # Cannot find gmodule-2.0
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  autoreconf -vfi

  export MAKEFLAGS="-j1"
  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=2 --disable-static PYTHON=python2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --with-gtk=3 --disable-static PYTHON=python2
  make ${MAKEFLAGS}
  popd
}

package_libindicate() {
  depends=('libdbusmenu' 'dbus-glib')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  MAKEFLAGS="-j1"

  make -C libindicate DESTDIR="${pkgdir}" install
  make -C bindings/mono/indicate DESTDIR="${pkgdir}" install
  make -C tests DESTDIR="${pkgdir}" install
  make -C docs DESTDIR="${pkgdir}" install
}

package_libindicate-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("libindicate=${pkgver}" 'gtk2-ubuntu' 'python2-gobject2')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  MAKEFLAGS="-j1"

  make -C libindicate-gtk DESTDIR="${pkgdir}" install
  make -C bindings/python DESTDIR="${pkgdir}" install
  make -C bindings/mono/indicate-gtk DESTDIR="${pkgdir}" install
  make -C examples DESTDIR="${pkgdir}" install
}

package_libindicate-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=("libindicate=${pkgver}" 'gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  MAKEFLAGS="-j1"

  make -C libindicate-gtk DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
