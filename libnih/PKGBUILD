# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libnih
_ubuntu_rel=4ubuntu16
pkgver=1.0.3
pkgrel=1
pkgdesc="Canonical's not-in-house library"
arch=('i686' 'x86_64')
url="https://launchpad.net/libnih"
license=('GPL')
groups=('unity')
depends=('dbus' 'expat')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libnih_${pkgver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libnih_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('fce40d2445b28c27b8838631681ca3206a4f053b2dd4fc488fc9ef98bbd3d933e3d62b82cf346be2ef1677f6457f692cf5544cd915a6bb1e5c618f98ffa101b4'
            '7765c64ca01a30fe21170e8661886b8a5dd9babdd2e052da2b21018cf766bc77fe46a233e207f3558fcfe7cf026e2d69f6dd0f66735f453531e5a5db5ca98aff')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/libnih_${pkgver}-${_ubuntu_rel}.diff"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
