# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_ubuntu_rel=0ubuntu3
pkgver=4.0.6.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee' 'dee' 'libdbusmenu' 'dbus-glib')
makedepends=('vala' 'python2' 'gobject-introspection')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgname}/4.0.0/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/libu/${pkgname}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('dbb7718204542db81722c7484bba647a99f808e23de2d09c782527feddc9674ba64ea6bdfa6da027cb53f91106f3d0f971a43f7ec8452036f0bf6664ef0b73c9'
            '660e8e2a007a12081764d96ad9526d9824c46a4d09219c181c60023a838d790e27ddd737abb4ed1edc1d6aaa0f3a8c4aba6d0dc811501ba19da5a1c66e80c52d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  #Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -fi
  ./configure --prefix=/usr --disable-static PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}
