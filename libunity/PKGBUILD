# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_actual_ver=6.90.2
_extra_ver=daily13.03.06.1
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver}.${_ubuntu_rel}
pkgrel=103
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee1' 'dee' 'libdbusmenu' 'gtk3')
makedepends=('intltool' 'vala' 'python2' 'gobject-introspection' 'vala0.18')
checkdepends=('python2-gobject' 'xorg-server-xvfb' 'xorg-xauth')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch')
sha512sums=('2119174ad437d6e123a04925e79e2c1de70569434543dd1d52ddc8ed83e9a7184185df49734f4d3672cfdea3b33b84fbef399fe5ae9bdcabe40974d14d57bdad'
            '7d25d5fa5e769097614b5cb3ce7fe4c74d27845e48ed19bdac11bf374263058ffb7e7686405f4fd60398769302ed7e5dda465b801bcf5fafdccd2924ebef4cb0'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  #export VALAC=valac-0.18
  mkdir bin/
  ln -s /usr/bin/valac-0.18 ./bin/valac
  export PATH="$(pwd)/bin:${PATH}"

  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
  export MAKEFLAGS="-j1"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  #export XDG_DATA_DIRS="$(pwd)/test/data:${XDG_DATA_DIRS}"

  # test-appinfo-manager is failing: something really bad is probably going to
  # happen
  sed -i -e '/\/Unit\/AppInfoManager\/SyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_sync_lookup_ok/d' \
         -e '/\/Unit\/AppInfoManager\/AsyncLookupOk/d' \
         -e '/AppInfoManagerSuite\.test_async_lookup_ok/d' \
         test/vala/test-appinfo-manager.vala

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
