# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_ubuntu_rel=0ubuntu1
pkgver=5.94.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee' 'dee' 'libdbusmenu')
makedepends=('vala' 'python2' 'gobject-introspection')
groups=('unity')
options=('!libtool')
source=("http://launchpad.net/${pkgname}/6.0/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('5a346552c59bb17e57350d6e1f11a09cb71098d0bb21bbc8b6f12006d550bf889e4b43cbd2e493caa7719eb9569f274d0d1954918d476e15b36b096f61989077'
            '14bade5ce96256bcf678ae9c66fe55fa0edfbb8f526f0346c86647c1741f87db1cbd556e6d3664bece546db4760e2d26e643a31d53ca55146f74e0e3c3034eef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
