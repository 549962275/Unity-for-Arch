# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jan@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-menus2
pkgver=3.0.1
pkgrel=100
pkgdesc="GNOME menu specifications"
url="http://www.gnome.org"
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
depends=('glib2' 'python2')
makedepends=('intltool' 'gobject-introspection')
groups=('unity')
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname/2/}/${pkgver%.*}/${pkgname/2/}-${pkgver}.tar.bz2"
        'menus.patch')
sha512sums=('05eda56b741d7236b17276b3b208b8370ec8ff0db82e32a2c0adb84334c4d9365fe61dea08183858fb7c0093cf04a02ba1311483e3318dbe816a02c28f4198eb'
            'd6f5f61b34bfd6adbc446b2d6d28f19bcafa02f238e779f4d4a9daf4d78d7cc37fe17e79df8eda46c834ec325b222b76afb7808b5869807e1f401d251efc5203')

build() {
  cd "${srcdir}/${pkgname/2/}-${pkgver}"

  patch -Np0 -i "${srcdir}/menus.patch"

  # Cannot find 'gmodule-2.0'
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  PYTHON=python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package(){
  cd "${srcdir}/${pkgname/2/}-${pkgver}"

  make -C libmenu DESTDIR="${pkgdir}/" install
  make -C python DESTDIR="${pkgdir}/" install
}
