# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=gnome-activity-journal
pkgver=0.8.0
pkgrel=1
pkgdesc="A tool for easily browsing and finding files on your computer"
arch=('any')
url="https://launchpad.net/gnome-activity-journal"
license=('GPL')
depends=('python2-libgnome' 'python2-gconf' 'zeitgeist' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python-distutils-extra')
optdepends=('gstreamer0.10-python: add multimedia tooltip to multimedia files'
            'python2-pygments: get a thumbnail of a text file')
groups=('unity')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5d92bb032328c9eb7c230015675c379b9cb9f3c31362128b8b25ede070df11120beadbf1766ba5d250006f1da523fc8a45f760119706a977de6e0124321375a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|^\(#!.*python\)$|\12|g' src/config.py gnome-activity-journal

  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
