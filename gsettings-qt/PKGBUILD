# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gsettings-qt
_actual_ver=0.1
_extra_ver=+14.04.20140408
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Library to access GSettings from Qt"
arch=('i686' 'x86_64')
url="https://launchpad.net/gsettings-qt"
license=('LGPL')
groups=('unity')
depends=('qt5-base' 'qt5-declarative')
makedepends=('qtchooser')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-qt_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('1c77694de5257d249af7426d64639d10cc23a732d85dd8aa1a826fac6489fc04d9d264d09a91c92a79e443de0fc39d0e6e3759c312d5e78b3be1c381ad3bd2c9')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  qmake-qt5 gsettings-qt.pro
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make INSTALL_ROOT="${pkgdir}/" install

  rm -r "${pkgdir}/usr/tests/"
}
