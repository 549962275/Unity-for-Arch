# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic

pkgname=gsettings-qt
_actual_ver=0.1
_extra_ver=+14.10.20140606
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Library to access GSettings from Qt"
arch=('i686' 'x86_64')
url="https://launchpad.net/gsettings-qt"
license=('LGPL')
groups=('unity')
depends=('qt5-base' 'qt5-declarative')
makedepends=('qtchooser')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-qt_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('88a6fab4d07eb89d726837621727074dde9bd5edadeb441416d8edc7763ef27b51fa746903453e630fd32e47f3bd07ae48c19d9f57278ed1609a66715fdfe25f')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  qmake-qt5 gsettings-qt.pro
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make INSTALL_ROOT="${pkgdir}/" install

  rm -r "${pkgdir}/usr/tests/"
}
