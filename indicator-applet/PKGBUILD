# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# THIS PACKAGE WAS DELETED FROM THE UBUNTU 11.10 REPOSITORY
#   Source: http://www.ubuntuupdates.org/packages/show/365107
# This package is probably not required anymore

pkgname=indicator-applet
pkgver=0.4.13
pkgrel=100
pkgdesc="A small applet to display information from various applications consistently in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-applet"
license=('GPL')
depends=('libindicator3' 'xdg-utils' 'gnome-panel')
makedepends=('intltool')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/~jconti/+archive/gnome3/+files/${pkgname}_${pkgver}.orig.tar.gz")
sha512sums=('490cdef9c8cff5a3b5296cf2a9b8001e85754200a214bb680392d80d0e5b8d9b78dca32eeba9d24e2117433612c090afbb14d7b78bd4c27ee3aacb7ddad5f175')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -fi
  export MAKEFLAGS="-j1"
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
