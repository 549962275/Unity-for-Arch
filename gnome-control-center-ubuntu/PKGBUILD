# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ppa_rel=0ubuntu1~trusty1
pkgver=3.12.0
pkgrel=2
pkgdesc="The Control Center for GNOME and Unity"
arch=('i686' 'x86_64')
groups=('gnome' 'unity')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'gnome-color-manager' 'smbclient' 'libmm-glib' 'libgnomekbd' 'grilo' 'webkitgtk3')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'modemmanager' 'vala' 'desktop-file-utils')
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login'
            'ntp: network time sync')
provides=("gnome-control-center=${pkgver}")
conflicts=('gnome-control-center')
options=('!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-control-center/gnome-control-center_${pkgver}-${_ppa_rel}.debian.tar.gz"
        '0001-Clear-GTK-MODULES-Before-Starting.patch'
        '0002-Reimplement-support-for-loading-external-panels.patch')
sha512sums=('d69125f31bb3d5cf51161836617327b221eb5b9992e1f9db84121602723295496091ea5e4c1e7017ecd2f9d943197843c7c195b58738b3527220e38e52525987'
            '44c56821b55746df8bfb6ca0aa303f63353f0350d23617d5f245d777c39c3e5989db41006dfffbaf131bb1b43a6c474075acff14f9ea5c10e34c4af9fb89bd97'
            'e4af362d67fda91b3f03dbe8f18781e31b60448aa7b9947d81acf37f823a2c5e5c8330c68af0aa6155573811be20e5dadac0f4e51576dd1be4e35599394a6900'
            '961f524727204f45470de8e5bc4cf7a8b18d138602aef8ddd88257652bda382dd308088abdaa7ef773a8abd1354f26c15c9db2f63b361662febdb4945f097165')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-Clear-GTK-MODULES-Before-Starting.patch"
  patch -p1 -i "${srcdir}/0002-Reimplement-support-for-loading-external-panels.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Why?
        sed -i '/revert_git_new_bluetooth_panel.patch/d' "${srcdir}/debian/patches/series"
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/52_ubuntu_language_list_mods.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/55_user_accounts_hide_controls.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/56_use_ubuntu_info_branding.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/59_install_gcm_components_on_demand.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/61_workaround_online_account.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/91_unity_no_printing_panel.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/92_ubuntu_system_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/revert_git_info_packagekit_api.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_region_packagekit.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/zz_add_fallback_panels_dir.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_external_panels.patch/d' "${srcdir}/debian/patches/series"
      # There are currently no packages compatible with this library (assuming it even works)
        sed -i '/revert_git_drop_library.patch/d' "${srcdir}/debian/patches/series"
      # We have gnome-bluetooth 3.12
        sed -i '/revert_new_bluetooth.patch/d' "${srcdir}/debian/patches/series"
      # We also have BlueZ 5
        sed -i '/revert-bluetooth-BlueZ5-interface.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --disable-silent-rules

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Install legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"

  # Make the Region panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-region-panel.desktop"

  # Make the Background panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-background-panel.desktop"

  # Make the Date & Time panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-datetime-panel.desktop"
}

# vim:set ts=2 sw=2 et:
