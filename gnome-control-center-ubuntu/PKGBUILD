# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ubuntu_rel=0ubuntu23
_actual_ver=3.6.3
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'libsocialweb' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'libsystemd' 'archlinux-artwork' 'webkitgtk3' 'ibus' 'system-config-printer')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'patchutils')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'gnome-color-manager: for color management tasks')
provides=("gnome-control-center=${_actual_ver}" 'FEATURE-libgnome-control-center')
conflicts=('gnome-control-center')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        'pulse3.0.patch')
sha512sums=('77cd68349dc7f58714fbd121a34ec73a5c3e67c7a42a08c20a8dfdcfc9cefdec47af0f09130b7e3f0407cb7a786234b2619fdb76462c3bdb2bbc548294bfd444'
            'a5e343561d40515a8c1f04e909f2640504bed64a4412817ea9d0e91f3b86f77d3eab7aee6f75264da08325c1a179fddc7402c68e4314313f7c1bfdcb60a8f841'
            '5f10412037bf9c81dec9e14259fee50955c355183c72623b399b92766e57c5411198db2b0fe5e007a6ca167da33f3fd72a73281f72b41e524eb65909f5231192')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Arch Linux's patches
  #  patch -p1 -i "${srcdir}/pulse3.0.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu is too lazy to port their patches to the latest version of IBus
        sed -i '/revert_new_ibus_keyboard_use.patch/d' "${srcdir}/debian/patches/series"
      # The gnome-settings-daemon-ubuntu patch for this conflicts with Arch
      # Linux's patches
        sed -i '/99_add_lock-on-suspend.patch/d' "${srcdir}/debian/patches/series"
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/10_keyboard_layout_on_unity.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/50_ubuntu_systemwide_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/56_use_ubuntu_info_branding.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    if [ "x${i}" = "xunity_notice_info.patch" ]; then
      cat "${srcdir}/debian/patches/${i}" | \
        filterdiff --exclude gnome-control-center-3.6.3/configure.ac | patch -p1
      # Base 64 because the tabs will screw up bash's parser
      cat << EOF | base64 -d | patch -p1
LS0tIGEvY29uZmlndXJlLmFjCisrKyBiL2NvbmZpZ3VyZS5hYwpAQCAtMTM3LDcgKzEzNyw3IEBA
CiAgICAgICAgICAgICAgICAgICBnZGstcGl4YnVmLTIuMCA+PSAkR0RLUElYQlVGX1JFUVVJUkVE
X1ZFUlNJT04pCiBQS0dfQ0hFQ0tfTU9EVUxFUyhESVNQTEFZX1BBTkVMLCAkQ09NTU9OX01PRFVM
RVMgZ25vbWUtZGVza3RvcC0zLjAgPj0gMy4xLjApCiBQS0dfQ0hFQ0tfTU9EVUxFUyhJTkZPX1BB
TkVMLCAkQ09NTU9OX01PRFVMRVMgbGliZ3RvcC0yLjAgZ2wgeDExCi0JCSAgcG9sa2l0LWdvYmpl
Y3QtMSA+PSAkUE9MS0lUX1JFUVVJUkVEX1ZFUlNJT04pCisJCSAgcG9sa2l0LWdvYmplY3QtMSA+
PSAkUE9MS0lUX1JFUVVJUkVEX1ZFUlNJT04gd2Via2l0Z3RrLTMuMCkKIFBLR19DSEVDS19NT0RV
TEVTKEtFWUJPQVJEX1BBTkVMLCAkQ09NTU9OX01PRFVMRVMKICAgICAgICAgICAgICAgICAgIGdu
b21lLWRlc2t0b3AtMy4wID49ICRHTk9NRV9ERVNLVE9QX1JFUVVJUkVEX1ZFUlNJT04KICAgICAg
ICAgICAgICAgICAgIHgxMSkK
EOF
    else
      patch -p1 -i "${srcdir}/debian/patches/${i}"
    fi
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd \
    --with-libsocialweb \
    --disable-update-mimedb \
    --without-cheese

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"
}

# vim:set ts=2 sw=2 et:
