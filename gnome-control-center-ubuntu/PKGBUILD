# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ubuntu_rel=0ubuntu13
_actual_ver=3.4.2
# Add 'z' to force upgrade from badly named previous version
pkgver=${_actual_ver}.z${_ubuntu_rel}
pkgrel=100
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop'
         'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu'
         'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'libsocialweb'
         'network-manager-applet' 'sound-theme-freedesktop' 'upower'
         'libsystemd' 'cheese' 'libpulse' 'archlinux-artwork')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts'
            'gnome-color-manager: for color management tasks')
provides=("gnome-control-center=${_actual_ver}")
conflicts=('gnome-control-center')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        'systemd-fallback.patch')
sha512sums=('78927f56f1e1af4043f0d96bae174513916e752aa3e987b545e78213f34527a25ba69ce61da43dcd21ed51aa9d1ee713cabed507853f20bb6c0b0bea82f272e6'
            'd5147beefca1c444d8ef52c8e0450088c3f93f0a31eee83ea3449d7f37541ff5a259717b97db6ea9151e011acd9888541a46b2f44b4696dac735536646c10a7a'
            '9e58b6f46fc58dd4f03ba569aea735e1ce5976995ad0357b9b87ed9b5773af1b26a9cf8ab4d29984286d879fdf50948b12b19ce75824260480320e73eb9a2ff3')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu patches

  # Disable patches
    # Disable using Ubuntu's update manager
      sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
    # Do not use Ubuntu help files
      sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
    # Arch Linux does not use Ubuntu's language selector
      sed -i '/52_ubuntu_language_list_mods.patch/d' "${srcdir}/debian/patches/series"
    # Arch Linux does not have ubuntu-system-service
      sed -i '/50_ubuntu_systemwide_prefs.patch/d' "${srcdir}/debian/patches/series"
    # No GNOME 3.6 yet
      sed -i '/git_gnome_desktop_update.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_new_bluetooth_api.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/00git_online_accounts_gtkgrid.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Use Arch Linux logo
    sed -i 's/UbuntuLogo.png/archlinux-vert-dark-grad2.svg/' "${srcdir}/debian/patches/56_use_ubuntu_info_branding.patch"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  patch -Np1 -i "${srcdir}/systemd-fallback.patch"

  # Builds just fine with the stable version of clutter-gtk
  sed -i 's/1\.11\.10/1.10.6/g' configure.ac

  autoreconf -vfi

  CFLAGS="${CFLAGS} -D_IPP_PRIVATE_STRUCTURES"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd \
    --with-libsocialweb \
    --disable-update-mimedb \
    --with-clutter

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  make DESTDIR="${pkgdir}" install

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  install -dm755 "${pkgdir}/usr/share/menu/"
  #install -m644 "${srcdir}/debian/UbuntuLogo.png" "${pkgdir}/usr/share/gnome-control-center/ui/"
  # Use Arch Linux branding
  ln -s /usr/share/archlinux/logos/archlinux-vert-dark-grad2.svg "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -m644 "${srcdir}/debian/gnome-control-center.xpm" "${pkgdir}/usr/share/pixmaps/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"
  install -m644 "${srcdir}/debian/gnome-control-center.menu" "${pkgdir}/usr/share/menu/"
}

# vim:set ts=2 sw=2 et:
