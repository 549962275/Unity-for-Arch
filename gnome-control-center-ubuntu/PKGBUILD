# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=3.4.1.${_ubuntu_rel}
pkgrel=101
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop'
         'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu'
         'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'libsocialweb'
         'network-manager-applet' 'sound-theme-freedesktop' 'upower'
         'libsystemd' 'cheese' 'libpulse-ubuntu>=1.1.0ubuntu7-100'
         'archlinux-artwork')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts'
            'gnome-color-manager: for color management tasks')
provides=("gnome-control-center=${pkgver}")
conflicts=('gnome-control-center')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'systemd-fallback.patch')
sha512sums=('09d0547dd0d76c071b14364b47f01b4a20b63a4ecec8f35b1da267c8abf73e5b1b495f74aeb3bff980d6e3c6f96e36377712cc920cd1831e22e6f98ae128f18b'
            '2a651208a5c4e3cb755175b26c414716c358462b46bc5a94cce38a7b6bcdbcea61374b6afa92065b00d753147d0619c7da1276d4f6dc8481d5a964a1ba586a07'
            '9e58b6f46fc58dd4f03ba569aea735e1ce5976995ad0357b9b87ed9b5773af1b26a9cf8ab4d29984286d879fdf50948b12b19ce75824260480320e73eb9a2ff3')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches

  # Disable patches
    # Disable using Ubuntu's update manager
      sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
    # Do not use Ubuntu help files
      sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
    # Arch Linux does not use Ubuntu's language selector
      sed -i '/52_ubuntu_language_list_mods.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Use Arch Linux logo
    sed -i 's/UbuntuLogo.png/archlinux-vert-dark-grad2.svg/' "${srcdir}/debian/patches/56_use_ubuntu_info_branding.patch"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  patch -Np1 -i "${srcdir}/systemd-fallback.patch"

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd \
    --with-libsocialweb \
    --disable-update-mimedb

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  install -dm755 "${pkgdir}/usr/share/menu/"
  #install -m644 "${srcdir}/debian/UbuntuLogo.png" "${pkgdir}/usr/share/gnome-control-center/ui/"
  # Use Arch Linux branding
  ln -s /usr/share/archlinux/logos/archlinux-vert-dark-grad2.svg "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -m644 "${srcdir}/debian/gnome-control-center.xpm" "${pkgdir}/usr/share/pixmaps/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"
  install -m644 "${srcdir}/debian/gnome-control-center.menu" "${pkgdir}/usr/share/menu/"
}

# vim:set ts=2 sw=2 et:
