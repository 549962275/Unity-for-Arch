# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=3.3.92.${_ubuntu_rel}
pkgrel=100
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
depends=('gtk3-ubuntu' 'gsettings-desktop-schemas' 'gnome-menus' 'gnome-desktop' 'gnome-settings-daemon-ubuntu' 'upower' 'libgtop' 'cups-pk-helper' 'accountsservice' 'sound-theme-freedesktop' 'gnome-online-accounts' 'network-manager-applet' 'libpulse-ubuntu>=1.1.0ubuntu7-100')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts'
            'gnome-color-manager: for color management tasks')
makedepends=('gnome-doc-utils' 'intltool')
groups=('gnome')
url="http://www.gnome.org"
provides=("gnome-control-center=${pkgver}")
conflicts=('gnome-control-center')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'disable_wacom_panel.patch')
sha512sums=('e40dae34995e8171427ebb52868abfb791f42bf2c6c88ae0873f1abcdc9ba1fd084cad74a4207a5eecd1db97786a25a74d89e73a3c65d05df4993308b493a936'
            '350ef8339324341e7937f351fa107b486ee95bbdccdb55cf0b07fa198fba73a4cb3f60b48b68ed11f97e65dc362c7f8a9c62d1597394bbc27f8a63e004365e07'
            'a2ee352a4f30fa3d5d551c4bcc060c535ac7a10e1bcf39dd1365502797f906470abc938c84418b1c993a98523eb4032baf1372c00d0fd6c3dd0a529a83fe3821')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches

  # Disable patches
    # Disable using Ubuntu's update manager
      sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
    # Do not use Ubuntu help files
      sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Use Arch Linux logo
    sed -i 's/UbuntuLogo.png/archlinux-vert-dark-grad2.svg/' "${srcdir}/debian/patches/56_use_ubuntu_info_branding.patch"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # disable wacom panel - too much work to package
  patch -Np1 -i "${srcdir}/disable_wacom_panel.patch" || true

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-scrollkeeper \
    --disable-update-mimedb

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  install -dm755 "${pkgdir}/usr/share/menu/"
  #install -m644 "${srcdir}/debian/UbuntuLogo.png" "${pkgdir}/usr/share/gnome-control-center/ui/"
  # Use Arch Linux branding
  ln -s /usr/share/archlinux/logos/archlinux-vert-dark-grad2.svg "${pkgdir}/usr/share/gnome-control-center/ui/"
  install -m644 "${srcdir}/debian/gnome-control-center.xpm" "${pkgdir}/usr/share/pixmaps/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"
  install -m644 "${srcdir}/debian/gnome-control-center.menu" "${pkgdir}/usr/share/menu/"
}
