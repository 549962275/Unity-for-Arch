# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=utopic
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.12
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=gnome-settings-daemon-ubuntu
_ppa_rel=0ubuntu1~utopic1
_ppa_ver=3.12.1
pkgver=3.12.2
pkgrel=3
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'libcanberra-pulse' 'libnotify' 'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'libibus' 'librsvg' 'libgweather' 'geocode-glib' 'geoclue2' 'nss')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl' 'python2')
groups=('gnome' 'unity')
provides=("gnome-settings-daemon=${pkgver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs')
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/3.12/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-settings-daemon/gnome-settings-daemon_${_ppa_ver}-${_ppa_rel}.debian.tar.xz"
        '0001-Do-not-hide-cursor.patch'
        '0001-Revert-power-Remove-unused-policy-GSettings.patch')
sha512sums=('d8063933ace4dfc9943991b5e6d29811546dec362cd3e26ec3c1c355ee2bbb854fb6fcc37ebaee78d70342ea2f6ae46325712dc6e20e6404ed54b0ee81c4bb8c'
            'd81f3cf811c4049051c6428ec7e6a24bfc94bed4bfa0b5a5912bbf167bcad7c17005e4cc93ad19e7f561065bcc45089e8f8ceab07e1d8fc1fe14236ae40001fb'
            'f0e4971759ea7553afb998d55124dff7450288d7079057911a61da1b9625272ce19b3dc8f0dbce9b3418e86aaf666954269ed42512e2d1ed37b8b4bf123725e2'
            'ca8db79e946cbd293ce8d647c37c00fb3e5802e18fd47789f3926bd7ec99306a6016128399102eb99c586a3c6e8a5757d74e659891ecfe2a4fc30c393da1b971')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-Do-not-hide-cursor.patch"

  # Needed for unity-settings-daemon
  patch -p1 -i "${srcdir}/0001-Revert-power-Remove-unused-policy-GSettings.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu specific
        sed -i '/43_disable_locale_settings.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_sync_input_sources_to_accountsservice.patch/d' "${srcdir}/debian/patches/series"
      # Fail to apply
        sed -i '/ubuntu-lid-open-reset-ideletime.patch/d' "${srcdir}/debian/patches/series"
      # Merged upstream
        sed -i '/git_power_fix_sleep_notifacations.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-settings-daemon \
    --disable-static
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
