# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ubuntu_rel=0ubuntu1.1
pkgver=3.4.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('colord' 'dconf' 'gnome-desktop' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libcanberra-pulse' 'libgnomekbd' 'libnotify' 'libsystemd' 'libwacom' 'nss' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'libappindicator3' 'packagekit')
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
groups=('unity')
provides=("gnome-settings-daemon=${pkgver%.*.*}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*.*}/${pkgname%-*}-${pkgver%.*.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*.*}-${_ubuntu_rel}.debian.tar.gz"
        'systemd-fallback.patch'
        'arguments.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=00681573ad6ebeb127ec72ac61d30d69be6459f8'
        'mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=053f48b3b1b9a3b2ab0714d470da4354735d0c4b'
        'glist-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=214a832820f6448f07d438f6686267eaf8301dd1'
        'mouse-mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=09c215b42312ca5bb63b215c2f95231613b3103d')
sha512sums=('530aa9f3a79c9b89093885c57abaf863c437aef12b77873aa6b8eac4a5f3daaf86fadeb38a5190096e400f70d322cea175025bde2b5d8c2d123601690555fe15'
            'dd3b48db1cee72dd20b9ad8646f77c35e8f9e88a96e0cb3829a6f22b5a1cfd89733667be9e0ebdafd56e74017d68bcdd0256f04b59f95d671bf742038b904356'
            '6afbad62dd69e0c779ae991e9afa46c3f2c0db07f0f7d77d607198f939498a8dfa0cd46099f46ce25d6d993425d7914bb3b8098044f007485e7fe501c900fe63'
            'b4390f57871b362ff641d948f243c34a6a21f5e6eb94aad8d2d6dbef7263de806d238cc96c872aef98e299d5dbf6ecf794b9270fbcc7f280b5a3daf5b3f35d9c'
            '35e4ea828a3813af316c7a5477231cb2901d9df64deb6996ab121fe17845e3a12c614d6a0eb8218d1fc72c867672bbafa902b8271c115f647120bda75cd391d9'
            '456191859a91f754995b7aff14e4fc1a34edc53836d494d366b0bbeae320cf49faac1d1b6c5153b26d50c3f8696c90bc674712f293e2daedbdb1fc88874196f0'
            'c19b4419d8fea8662a214dfe8524404daf7cd4ef6e2abd4905bdbb8c2e9e0bdd7380c7126958106072b755b6d2f9a231bfb9d9d993bb7c41085a52c20d277033')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*.*}"

  patch -Np1 -i "${srcdir}/systemd-fallback.patch"

  # Extra patches that Arch Linux applies
  patch -Np1 -i "${srcdir}/arguments.patch"
  patch -Np1 -i "${srcdir}/mem-leak.patch"
  patch -Np1 -i "${srcdir}/glist-leak.patch"
  patch -Np1 -i "${srcdir}/mouse-mem-leak.patch"

  # Apply Ubuntu patches
  # gsd-media-keys-manager.c fails to find libnotify. Adding libnotify to Makefile.am
  #   will solve the problem.
  # Add ${LIBNOTIFY_LIBS} to plugins/media-keys/Makefile.am
  sed -i '/test_media_keys_LDADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am
  sed -i '/libmedia_keys_la_LIBADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --disable-static \
    --enable-systemd
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}

  # Compile gnome-update-wallpaper-cache.c
  gcc -o gnome-settings-daemon/gnome-update-wallpaper-cache "${srcdir}/debian/gnome-update-wallpaper-cache.c" $(pkg-config --cflags --libs glib-2.0 gdk-3.0 gdk-x11-3.0 gio-2.0 gnome-desktop-3.0)
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*.*}"
  make DESTDIR="${pkgdir}" install

  # Install gnome-update-wallpaper-cache
  install -c gnome-settings-daemon/gnome-update-wallpaper-cache "${pkgdir}/usr/lib/${pkgname%-*}"
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${srcdir}/debian/${pkgname%-*}.1" "${pkgdir}/usr/share/man/man1/"

  # Symlinks
  install -dm755 "${pkgdir}/usr/bin/"
  ln -s '/usr/lib/gnome-settings-daemon/gnome-settings-daemon' "${pkgdir}/usr/bin/"

  # Gsetings override
  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/debian/gnome-settings-daemon.gsettings-override" "${pkgdir}/usr/share/glib-2.0/schemas/10_${pkgname%-*}.gschema.override"
}

# vim:set ts=2 sw=2 et:
