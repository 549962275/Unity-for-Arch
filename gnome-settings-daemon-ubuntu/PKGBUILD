# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ubuntu_rel=0ubuntu4
_actual_ver=3.6.3
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libcanberra-pulse' 'libnotify' 'libsystemd' 'libwacom' 'nss' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'libappindicator3')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl')
groups=('unity')
provides=("gnome-settings-daemon=${_actual_ver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gnome-settings-daemon_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        '0001-power-and-media-keys-Use-logind-for-suspending-and-r.patch')
sha512sums=('45b7a0a2162d8419ba9c9d248b87374f9d2012b43b9667f34d3a125567b45e2c2dde5535091cf7cd170e982b07ec7594803cba2628774aefae9ce9f3e672a49a'
            '61dabdb0ed6d9b5a1a22410b8ab1d7793936c9c16f0d2d7d4dad915da24269e03a07d386d18b6a8b9d821bb479a451b0eae85a2597428f392902ddf6cc74fcec'
            '8dfff4992ad21cf5325e411d526cb0b19ff74f393685ff82f28a8f31c229245d03730fd9057c114f3b6cfce18891c4433b48ebe57916f07cb64d68f7b7477fe8')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # logind key handling FS#31801, patch from Fedora
  patch -Np1 -i ../0001-power-and-media-keys-Use-logind-for-suspending-and-r.patch

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu specific
        sed -i '/sync_keyboard_layout_to_accountsservice.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/61_revert_libgnomekbd_drop/d' "${srcdir}/debian/patches/series"
        sed -i '/43_disable_locale_settings.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu is too lazy to port their patches to the latest IBus release
        sed -i '/revert_new_ibus_use.patch/d' "${srcdir}/debian/patches/series"
      # systemd should make this obsolete
        sed -i '/revert_git_datetime_dropping.patch/d' "${srcdir}/debian/patches/series"
      # We do use PackageKit in Arch Linux
        sed -i '/53_update_schemas_warning.patch/d' "${srcdir}/debian/patches/series"
      # Merged upstream. How does Debian/Ubuntu's quilt not error out, I have no
      # idea. https://launchpadlibrarian.net/124034045/buildlog_ubuntu-raring-amd64.gnome-settings-daemon_3.6.3-0ubuntu2_BUILDING.txt.gz
        sed -i '/48_register_client_before_idle_callbacks.patch/d' "${srcdir}/debian/patches/series"
      # Conflicts with Fedora's patches
        sed -i '/51_lock_screen_on_suspend.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/bugzilla_segfault_dpms.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --disable-static \
    --enable-systemd \
    --disable-ibus
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"
  make DESTDIR="${pkgdir}" install

  # Plugins that aren't installed still have schema references
  # and cause gsettings errors - remove the references we're not using
  sed -i '/org\.gnome\.settings-daemon\.plugins\.updates/d' \
    "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.settings-daemon.plugins.gschema.xml"
}

# vim:set ts=2 sw=2 et:
