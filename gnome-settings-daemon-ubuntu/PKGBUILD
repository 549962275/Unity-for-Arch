# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ubuntu_ver=0ubuntu5
pkgver=3.2.0.${_ubuntu_ver}
pkgrel=2
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 'libcanberra-pulse' 'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'colord')
makedepends=('intltool' 'gtk-doc' 'gnome-desktop')
groups=('unity')
provides=("gnome-settings-daemon=${pkgver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}-${_ubuntu_ver}.debian.tar.gz"
        'fix_color_crash.patch')
sha512sums=('e6f57ebb8d93d4a97ee5ffa2df8dc1f6554a403abe66b4eae8f7e5006add9bb0875e9771092794dd29a99c6029cdb53bde5195b752e683e0d3dc6e278741691b'
            '8af54e657b0438e0bd035c7890c3d94b6d53724a2911410166292af1c16cb1b6e3fdd905a3855d7ffa8e7046962140a2061faf97ad30a0269fa330b8f3484ec7'
            'b1775cd433d5a7b0a0ae40d25be8315dbe6766adf295e952f772a44dc0eb1662b78932e96e7dd16d28903f88ee0478aab5a39887fd30e0a06742e7c11e05c6f0')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  #Apply Ubuntu patches

  #gsd-media-keys-manager.c fails to find libnotify. Adding libnotify to Makefile.am
  #  will solve the problem.
  #Add ${LIBNOTIFY_LIBS} to plugins/media-keys/Makefile.am
  sed -i '/test_media_keys_LDADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am
  sed -i '/libmedia_keys_la_LIBADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  patch -Np1 -i "${srcdir}/fix_color_crash.patch"

  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --disable-static \
    --enable-pulse \
    --with-pnpids=/usr/share/libgnome-desktop-3.0/pnp.ids \
    --enable-gconf-bridge #from debian/rules
      
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  #Compile gnome-update-wallpaper-cache.c
  gcc -o gnome-settings-daemon/gnome-update-wallpaper-cache "${srcdir}/debian/gnome-update-wallpaper-cache.c" $(pkg-config --cflags --libs glib-2.0 gdk-3.0 gdk-x11-3.0 gio-2.0 gnome-desktop-3.0)
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  #Install gnome-update-wallpaper-cache
  install -c gnome-settings-daemon/gnome-update-wallpaper-cache "${pkgdir}/usr/lib/${pkgname%-*}"
}
