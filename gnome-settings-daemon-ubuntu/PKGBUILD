# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=3.3.92.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 'libcanberra-pulse' 'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'colord')
makedepends=('intltool' 'gtk-doc' 'gnome-desktop')
groups=('unity')
provides=("gnome-settings-daemon=${pkgver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'disable_wacom_plugin.patch')
sha512sums=('dbcf9db025228c3265ec429de2ec5d8ff4c333f9e5973936e53aa01b66977e27f22676279c753ce8e74d37ff29fc66ac338a127f0aa0c77ab115e6157ec3025e'
            '20ff2083dd69d43be8a45b37d66d4ed8448c337ce68acb9d89a72816c54ff313b7bcaa34b868d6e661d034a7fb92f38d4751414bea34e885c98ee2dec9b3ed0c'
            '8b04751faf5f4bcdc77e8a0a7a0d84e26b6e44042ce2ee389a8cee9465794d0bbef5aaf9593a0c4d60c9277027490738c6e22660d249a7434cc91fd2a7856030')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  # gsd-media-keys-manager.c fails to find libnotify. Adding libnotify to Makefile.am
  #   will solve the problem.
  # Add ${LIBNOTIFY_LIBS} to plugins/media-keys/Makefile.am
  sed -i '/test_media_keys_LDADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am
  sed -i '/libmedia_keys_la_LIBADD.*=.*\\/ a ${LIBNOTIFY_LIBS} \\' plugins/media-keys/Makefile.am

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Disable wacom plugin
  patch -Np1 -i "${srcdir}/disable_wacom_plugin.patch" || true

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --disable-static \
    --enable-pulse \
    --with-pnpids=/usr/share/libgnome-desktop-3.0/pnp.ids \
    --enable-gconf-bridge #from debian/rules
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}

  # Compile gnome-update-wallpaper-cache.c
  gcc -o gnome-settings-daemon/gnome-update-wallpaper-cache "${srcdir}/debian/gnome-update-wallpaper-cache.c" $(pkg-config --cflags --libs glib-2.0 gdk-3.0 gdk-x11-3.0 gio-2.0 gnome-desktop-3.0)
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  # Install gnome-update-wallpaper-cache
  install -c gnome-settings-daemon/gnome-update-wallpaper-cache "${pkgdir}/usr/lib/${pkgname%-*}"
  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${srcdir}/debian/${pkgname%-*}.1" "${pkgdir}/usr/share/man/man1/"

  # Symlinks
  install -dm755 "${pkgdir}/usr/bin/"
  ln -s '/usr/lib/gnome-settings-daemon/gnome-settings-daemon' "${pkgdir}/usr/bin/"

  # Gsetings override
  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/debian/gnome-settings-daemon.gsettings-override" "${pkgdir}/usr/share/glib-2.0/schemas/10_${pkgname%-*}.gschema.override"
}
