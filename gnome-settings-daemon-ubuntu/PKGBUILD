# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=yakkety
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.20
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=gnome-settings-daemon-ubuntu
_use_ppa=true
_ppa_rel=0ubuntu1~xenial1
_ppa_ver=3.20.1
#_ubuntu_rel=0ubuntu3
#_ubuntu_ver=3.14.2
pkgver=3.20.1+8+g40bf4fd
pkgrel=1
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL)
depends=(dconf gnome-desktop gsettings-desktop-schemas-ubuntu libcanberra-pulse
         libnotify libsystemd libwacom pulseaudio pulseaudio-alsa upower
         librsvg libgweather geocode-glib geoclue2 nss libgudev)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl python git gnome-common)
groups=(gnome unity)
provides=("gnome-settings-daemon=${pkgver}")
conflicts=(gnome-settings-daemon)
options=(!emptydirs)
source=('git://git.gnome.org/gnome-settings-daemon#commit=40bf4fdce0001c662094f47e8edc1b0855ec9632')

if [[ "${_use_ppa}" == "true" ]]; then
    source+=("http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-settings-daemon/gnome-settings-daemon_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-settings-daemon_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('SKIP'
            'e0a75c2bb655a84613008a6f7fbe3d7e33b6ab547b11dbd81e222ea47586134b1578fc92e9148716c466863a83d251a82278d24512bc89564a8c8e414aa7b1c0')

pkgver() {
    cd "${pkgname%-*}"
    git describe --long | sed -e 's/-/+/g' -e 's/^GNOME_SETTINGS_DAEMON_//' -e 's/_/./g'
}

prepare() {
    cd "${pkgname%-*}"

    # Apply Ubuntu's patches
    patch -p1 -i ../debian/patches/64_restore_terminal_keyboard_shortcut_schema.patch
    patch -p1 -i ../debian/patches/ubuntu-lid-close-suspend.patch
    patch -p1 -i ../debian/patches/revert_background_dropping.patch
    patch -p1 -i ../debian/patches/revert-gsettings-removals.patch

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${pkgname%-*}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnome-settings-daemon \
        --disable-static
      
    # https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname%-*}"
    make DESTDIR="${pkgdir}" install
}
