# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-settings-daemon-ubuntu
_ppa_rel=0ubuntu1~saucy4
_translations=20130418
pkgver=3.8.4
pkgrel=101
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas-ubuntu' 'hicolor-icon-theme' 'ibus' 'libcanberra-pulse' 'libnotify' 'libsystemd' 'libwacom' 'nss' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'librsvg' 'libappindicator3' 'packagekit')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl' 'desktop-file-utils')
groups=('unity')
provides=("gnome-settings-daemon=${pkgver}")
conflicts=('gnome-settings-daemon')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3/ubuntu/pool/main/g/gnome-settings-daemon/gnome-settings-daemon_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-settings-daemon.tar.gz"
        '0001_Do_not_hide_IBus_icon.patch')
sha512sums=('cf6905afb275f9eea10e2c5729316572b7ad665fb4c78edf53b54335a113413b725a20c07fba2505d2279110460ea6632d3a2bec68e7b5af37f44597a5e80860'
            '00a1d1582ae03bf49979f29e823aec2281bd5d931474933177c3d14d9c5b18c0afedc4e797f1c2e8a1a089769306776c1d1eed6004f5deb3f8234993b5ac3542'
            '8282c8c9233b3dd2199ae3bcedd910ff68a69896ae295300fb59c3d03119ac5e889a2088c2021c2fc2afd452d167b3ee22c92ad9c6375a1588f1f8dff6b172ff'
            '2a5a4fdc030ad7499c4551ba33480a7c4b609f018c9bebcb21f49deecf54a73faf1f6078b93895fdefb5738ee8a4aad9fe4ae7e95243242d6d82ecbeb5b9754f')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Do not hide ibus icon. This will cause a duplicate icon to show up in GNOME
  # Shell, but whatever.
  patch -p1 -i "${srcdir}/0001_Do_not_hide_IBus_icon.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu specific
        sed -i '/43_disable_locale_settings.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname%-*}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-settings-daemon \
    --disable-static \
    --enable-systemd
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Part of gnome-settings-daemon-compat
  rm "${pkgdir}/usr/lib/gnome-settings-daemon/gnome-fallback-mount-helper"

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/debian/gnome-settings-daemon.gsettings-override" \
    "${pkgdir}/usr/share/glib-2.0/schemas/10_${pkgname}.gschema.override"
}

# vim:set ts=2 sw=2 et:
