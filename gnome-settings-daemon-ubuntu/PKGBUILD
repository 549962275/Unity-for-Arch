# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=utopic
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.14
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=gnome-settings-daemon-ubuntu
_ppa_rel=1ubuntu1~utopic1
_ppa_ver=3.14.0
pkgver=3.14.1
pkgrel=2
pkgdesc="The GNOME Settings daemon with Ubuntu's patches"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL)
depends=(dconf gnome-desktop gsettings-desktop-schemas-ubuntu hicolor-icon-theme
         libcanberra-pulse libnotify libsystemd libwacom pulseaudio
         pulseaudio-alsa upower librsvg libgweather geocode-glib geoclue2 nss)
makedepends=(intltool xf86-input-wacom libxslt docbook-xsl python2)
groups=(gnome unity)
provides=("gnome-settings-daemon=${pkgver}")
conflicts=(gnome-settings-daemon)
options=(!emptydirs)
install=gnome-settings-daemon.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-settings-daemon/gnome-settings-daemon_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz"
        0001-64_restore_terminal_keyboard_shortcut_schema.patch.patch)
sha512sums=('be89d27cb2be2557f7381c0d11c3cc48346973543b412ba95abdb05d7e60f27074a29a4d7c8083b963400de7769e9025b815b086a832d904ee3a17a43e9d992f'
            '9299c77626307d20f8d896a7cfee8872ea69e233baf6025f98f2e4251dda03ec8703178638071d715a5dfe0a21e0e7168570713e2a954766109ea54a81b345af')
sha512sums=('be89d27cb2be2557f7381c0d11c3cc48346973543b412ba95abdb05d7e60f27074a29a4d7c8083b963400de7769e9025b815b086a832d904ee3a17a43e9d992f'
            '9299c77626307d20f8d896a7cfee8872ea69e233baf6025f98f2e4251dda03ec8703178638071d715a5dfe0a21e0e7168570713e2a954766109ea54a81b345af'
            '448242b333fd113b5cc19199514ce3d90ed96ef0cc00213ead46751d2bda34f09706c31114d0c36dea1728b70e990c8ab897181035e7029d2265c0780a310161')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
  patch -p1 -i ../0001-64_restore_terminal_keyboard_shortcut_schema.patch.patch
  #patch -p1 -i ../debian/patches/64_restore_terminal_keyboard_shortcut_schema.patch
  patch -p1 -i ../debian/patches/ubuntu-lid-close-suspend.patch
  patch -p1 -i ../debian/patches/revert_background_dropping.patch
  patch -p1 -i ../debian/patches/revert-gsettings-removals.patch
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-settings-daemon \
    --disable-static
      
  # https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
