# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-gtk-module
_actual_ver=0.0.0
_extra_ver=+14.04.20140311
pkgver=${_actual_ver}daily14.03.11
pkgrel=1

# Override AUR version
epoch=1

pkgdesc="Unity GTK+ Module"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-gtk-module"
license=('GPL')
groups=('unity')
depends=('libx11' 'gtk2-ubuntu' 'gtk3')
makedepends=('gtk-doc')
provides=('appmenu-gtk2' 'appmenu-gtk3')
replaces=('appmenu-gtk2' 'appmenu-gtk3')
conflicts=('appmenu-gtk2' 'appmenu-gtk3')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-gtk-module_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '80unity-gtk-module'
        'g_ptr_array_insert.patch')
sha512sums=('f4b0f0c9d2a34420a4b54d16110c251a57ba62901712b26d2cd1fc35c31e04b7a625fa62980c92ba0ac23f8849979f58c2089b797a847ad0929d550b85a0621b'
            'cbf13533c128c870dc4b9bb3436a95c3a38ec3803cf99f1221416c0d78b971a66b0b279d765aab8a6942148a89638c569031050012bfb5c53bb7d5daae14189c'
            '426279a6c5067a78e635fc832c7b0a4ef078a9223101675330aaf3d60a7d754e921eb4e77d7df419ad543e56d5aa4f1a844106b784a96567383281e44b615c86')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -R -i "${srcdir}/g_ptr_array_insert.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [ -d build-gtk2 ] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --enable-gtk-doc \
               --disable-static
  make
  popd

  [ -d build-gtk3 ] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --enable-gtk-doc \
               --disable-static
  make
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-gtk3
  make DESTDIR="${pkgdir}/" install
  popd

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755  "${srcdir}/80unity-gtk-module" \
                 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  rm -rvf "${pkgdir}/usr/share/upstart/"
}
