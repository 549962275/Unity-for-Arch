# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-gtk-module
_actual_ver=0.0.0
_extra_ver=+14.04.20140403
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=3

# Override AUR version
epoch=2

pkgdesc="Unity GTK+ Module"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-gtk-module"
license=('GPL')
groups=('unity')
depends=('libx11' 'gtk2-ubuntu' 'gtk3')
makedepends=('gtk-doc')
provides=('appmenu-gtk2' 'appmenu-gtk3')
replaces=('appmenu-gtk2' 'appmenu-gtk3')
conflicts=('appmenu-gtk2' 'appmenu-gtk3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-gtk-module_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '80unity-gtk-module')
sha512sums=('dca50543f7259f5d1b00a0886d53108f8fd939c7066df8c948154e615bbc29ba2879daca25f3fe6e1dba5e166773a98e2e3072c57bf58713088f75923790eb5e'
            'e30db7e3e1aba0abf100e619c667bac16d0a25ee3d9c67f65af532ab1a512dce5966e3c94f5d938c378dc30d9ed766fc2c9fa62e96235f29d923be5bad7f3a45')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [ -d build-gtk2 ] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --enable-gtk-doc \
               --disable-static
  make
  popd

  [ -d build-gtk3 ] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --enable-gtk-doc \
               --disable-static
  make
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-gtk3
  make DESTDIR="${pkgdir}/" install
  popd

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755  "${srcdir}/80unity-gtk-module" \
                 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  rm -rvf "${pkgdir}/usr/share/upstart/"
}
