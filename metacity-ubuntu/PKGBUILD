# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity-ubuntu
pkgver=3.20.3
pkgrel=1
pkgdesc="Window manager of GNOME Flashback"
arch=(i686 x86_64)
license=(GPL)
depends=(dconf libcanberra libgtop libsm gsettings-desktop-schemas gtk3
         startup-notification zenity)
makedepends=(intltool itstool)
url="https://wiki.gnome.org/Projects/Metacity"
source=("https://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz")
sha512sums=('7070193f5f8f43cb90a655073b3d296e9bdf0060c2c6d9a63155736e6d37361a951e684b34e9fa7f226ef0aef230cf686f71c06a34075bf37876ba2a848b5259')

build() {
    cd "${pkgname%-*}-${pkgver}"

    ./configure \
        --prefix=/usr/unity \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-schemas-compile
    make
}

package() {
    cd "${pkgname%-*}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
