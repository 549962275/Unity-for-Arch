# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=metacity-ubuntu
_ubuntu_rel=1ubuntu11
pkgver=2.34.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="A window manager for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('startup-notification' 'gconf' 'zenity' 'libcanberra' 'libgtop'
         'libwnck' 'libsm')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
groups=('gnome')
provides=("metacity=${pkgver}")
conflicts=('metacity')
options=('!libtool' '!emptydirs')
install=metacity.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('44e4879d0b5cac8a50fbd0c33c29fa64b6485f80eca5bf6154b368cae64492402fb880eb6783ab4f1d2258079e96f53bf3b7300d4c2fbb36e4ebc7baaf8ee952'
            'e6c7f49089ade57cdabeb722e36aa2375a0ae0a117228f5c98303091b74affa27d04fd0da677bd093db37383cd32501352b3157d5b8ed43b87cf4eb980c4a551')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/metacity \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notifications=yes # from debian/rules

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain metacity "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas

  # Install Ubuntu stuff
  install -dm755 "${pkgdir}/usr/share/sgml/metacity-common/"
  install -dm755 "${pkgdir}/usr/share/gconf/defaults/"
  install -m644 "doc/metacity-theme.dtd" "${pkgdir}/usr/share/sgml/metacity-common/"
  install -m644 "${srcdir}/debian/metacity-common.catalog" "${pkgdir}/usr/share/sgml/metacity-common/"
  install -m644 "${srcdir}/debian/metacity-common.gconf-defaults" "${pkgdir}/usr/share/gconf/defaults/10_metacity-common"
}

# vim:set ts=2 sw=2 et:
