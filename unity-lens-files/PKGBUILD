# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-files
_actual_ver=7.0
_extra_ver=~daily13.02.28
pkgver=${_actual_ver}${_extra_ver/\~/.}
pkgrel=100
pkgdesc="A daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-files"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'libxdg-basedir' 'xdg-user-dirs' 'pyxdg' 'dconf')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-files_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('a54ff834d951aee2b5509a53e6dccaff46e707cc519c5232e40c42e721f1ff2ca27fa85024b783b00bc1f06ecad56cc18baaf83bd8b04b6cc0fd5e0d36038498')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
