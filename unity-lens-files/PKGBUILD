# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-files
_actual_ver=6.6.0
_extra_ver=daily12.12.05
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-files"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'libxdg-basedir' 'xdg-user-dirs' 'pyxdg' 'dconf')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-files_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('fdf1f6d9830ee8b597468e14eab09b2e0937eac9efa13b6d3cb2960c04c0adb4ef14d614d08fbb12d4a47f6e27611ee557ff0893e6db1166831810493d0a1020')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
