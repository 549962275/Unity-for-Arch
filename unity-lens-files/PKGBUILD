# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-files
pkgver=5.10.0
pkgrel=100
pkgdesc="A daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-files"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'zeitgeist-extensions' 'libxdg-basedir' 'xdg-user-dirs' 'pyxdg' 'dconf')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/${pkgver%%.*}.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        'vala-0.16_fix.patch')
sha512sums=('7474afb7462db3c03b992ae2f1fe092c244733d9bab74399651542cab11f46cc190ce75af742a1a6e4e006489d04df1c9d2bd1d45d2eb9729a103f4984db727b'
            '40beb4f7e355040005c3648c0ab5fdfaeed93abe2ddfea6aea0e6a7cf6b33c633614d39f6a404e02e876fbf0c6b9ad4ed1364662147c827090fd61f85d5a79b4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix build with vala 0.16
  patch -Np1 -i "${srcdir}/vala-0.16_fix.patch"

  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
