# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf3')
_actual_ver=0.4.0
_extra_ver=daily13.01.11
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('gobject-introspection' 'libwnck3' 'libgtop' 'gnome-common' 'libunity-webapps' 'gtk-doc')
provides=('FEATURE-bamf-webapps')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('13b43cc5c01a8bcd47cbc9b48ebe1e589bbb6a2d3eed646bfc73cadbed51e0d6a85c98cedd6891189f35ba58c1c3da05ff48a3f486f102610f9c0acc9d9e3c71'
            '4806a0e236ea03a2d63e07d563c564956a041e743b8d1e73e5f858fea3b685c1b59ef94e72dfccbdecc92f2b1ca96d9ba5f8bd5ef56f37ebe60f3f711255855a')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  # Apply Ubuntu's patches
  #patch -p1 -i "${srcdir}/bamf_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  #for i in $(grep -v '#' debian/patches/series); do
  #  patch -p1 -i "debian/patches/${i}"
  #done

  gtkdocize
  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libwnck3' 'libgtop' 'libunity-webapps')
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon' 'gtk3-ubuntu')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rv "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rv "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
