# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf3')
_actual_ver=0.4.0
_extra_ver=daily13.02.06
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('dbus-glib' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'libgtop' 'libunity-webapps' 'libwnck3')
provides=('FEATURE-bamf-webapps')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('24275dbaac6d4f9d1bdc4b04e169b8cc6e7b4ad883508f67fce5a263f34bb24be0be962a33bd98c7ba886954970f21d90dc49f0299d2d35e4a0e6f4d1404b269')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libgtop' 'libunity-webapps' 'libwnck3')
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rv "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rv "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
