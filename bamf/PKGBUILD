# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf3')
_actual_ver=0.4.0
_extra_ver=daily12.12.05
_ubuntu_rel=0ubuntu2
pkgver=${_actual_ver}${_extra_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('gobject-introspection' 'libwnck3' 'libgtop' 'gnome-common' 'libunity-webapps' 'gtk-doc')
provides=('FEATURE-bamf-webapps')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('10472205e06e641e916b984590367623984e6cb8bc30f7ee8791d69245b860e4cf3a8c8f7248a25ac42e269ecfd7df87874aecc4f350301f96a66887cc53d414'
            'bd2ae82e2c7463385ccbd7550019bb00924e07765cf51662f5e308565ef30aa3046d1b82139cf8b7d60ef7fc584a0f9c2ddfea0447d1a56badc4efd881ccecb8')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  # Apply Ubuntu's patches
  #patch -p1 -i "${srcdir}/bamf_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  #for i in $(grep -v '#' debian/patches/series); do
  #  patch -p1 -i "debian/patches/${i}"
  #done

  gtkdocize
  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libwnck3' 'libgtop' 'libunity-webapps')
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon' 'gtk3-ubuntu')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rv "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rv "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
