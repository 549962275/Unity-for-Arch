# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf' 'libbamf3')
_ubuntu_rel=0ubuntu1
pkgver=0.2.106.${_ubuntu_rel}
pkgrel=101
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('gobject-introspection' 'libwnck3' 'libwnck' 'libgtop')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}.orig.tar.gz")
sha512sums=('331361b14464ff482b1890bbe72ce71d20cdb52d805f0599ad6141e17a40bc31e74d264e2b02f1ef605039ee8bc111f1743218db227a5068ef164971bcb626a7')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  #Documentation will go in the GTK 2 package
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libwnck3' 'libgtop')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3" #Doesn't matter which one

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf() {
  pkgdesc="Window matching library for GTK 2"
  depends=('bamfdaemon' 'gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make DESTDIR="${pkgdir}/" install

  #Remove bamfdaemon from this package
  rm -rvf "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rvf "${pkgdir}/usr/share/dbus-1/"
  rm -rvf "${pkgdir}/usr/share/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon' 'gtk3-ubuntu')
  provides=("bamf=${pkgver%.*}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install

  #Remove bamfdaemon from this package
  rm -rvf "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rvf "${pkgdir}/usr/share/dbus-1/"
}
