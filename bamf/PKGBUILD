# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=bamf
_actual_ver=0.5.1
_extra_ver=+14.04.20140310
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=101
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('libgtop' 'libunity-webapps' 'libwnck3' 'glib2')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'libxml2' 'libxslt' 'vala')
provides=('FEATURE-bamf-webapps' "libbamf3=${pkgver}" "bamfdaemon=${pkgver}")
replaces=('libbamf3' 'bamfdaemon')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        'bamfwatcher.c'
        'update-bamf-index.pl'
        'bamfwatcher.service')
sha512sums=('f298bac969f0f1db6097a7dd73b66af18dc38cea5fb4c48830f43461d94b384f2b83097cb9665a68de135635d3375cc5a81a36c4d17b0831183f10bf47c2997f'
            'fd8623f185529e268c8622405da72d22b0d6863a37defb499e27a00297390b9419774f236fb9d86bd08e7f279209c58f4e111540b86a6b85b3eb578bed504010'
            '7db84774b72fb330abe2116f3d1d842c007820c200344d76ff87abab5226488fcc21d2e82cbe5381fb3e7ce0eacc09232f17d64c4e2a5ee8eef0be06808f21ff'
            'b2e899e2916efc6e04af34c3bffc14f6254caaf42d2e3cee88277dde4c191dd93cab14246091beb5d4cfb8a242559b8361a7c4c1dc70c31034ade52689322b83')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  sed -i 's/-Werror/-Wno-error/g' configure.ac
}

build() {
  cd "${srcdir}"

  gcc bamfwatcher.c -lprocps -o bamfwatcher

  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi

  export PYTHON=/usr/bin/python2
  
  if ! pacman -Q xorg-server-xvfb &>/dev/null; then
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  else
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-headless-tests
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/bamfwatcher" "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/update-bamf-index.pl" "${pkgdir}/usr/lib/bamf/"

  # Install systemd service
  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/bamfwatcher.service" "${pkgdir}/usr/lib/systemd/system/"
}
