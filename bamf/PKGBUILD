# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf3')
_actual_ver=0.4.0
_extra_ver=daily13.02.01
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('gobject-introspection' 'libwnck3' 'libgtop' 'gnome-common' 'libunity-webapps' 'gtk-doc')
provides=('FEATURE-bamf-webapps')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('48ee1d5c885305aef9ab50d455bad25035c55b00c22e876e21db4f1559e337042fefe54af5845010360cf387d19dd50cbb282546660978e7e1acd02869f760ba')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libwnck3' 'libgtop' 'libunity-webapps')
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon' 'gtk3-ubuntu')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rv "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rv "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
