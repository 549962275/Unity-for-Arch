# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+16.04.20160105
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="An indicator to show the power status of your devices"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-power"
license=(GPL)
depends=(dconf libnotify upower unity-control-center)
makedepends=(cmake intltool python2)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch)
sha512sums=('90f6b08d3bc4f86ad429f8dfa6c1cf5a250ab1747d5cab83158b9540810304bf5666d79f51c99f35a2039fcaf8f71b7743b644001a10258174403639db77fd8b'
            'd524f0061554d9a06262bad48b8c650229af1cda5189188a5a48ed8cc61c2079e9e00f060d83500020e484e68a8c0b7a47162c64f99615ccdecabb59e79c2489')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0001-Not-a-phone.patch

    sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    export CFLAGS+=" -Wno-error"

    rm -rf build; mkdir build; cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

    make DESTDIR="${pkgdir}/" install

    find "${pkgdir}" -type f -name '*.a' -delete
}
