# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=daily13.01.25
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('dbus-glib' 'libindicator3' 'dconf' 'gnome-settings-daemon' 'upower')
makedepends=('intltool')
checkdepends=('gtest-ubuntu' 'xorg-gtest')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Link_pthread.patch'
        'revert_r161.patch')
sha512sums=('b1363b0e8ea40fd8797ec11205902c8126c822546766353efd15dca0cead903aa2758e18c8427c99c924d8d10b4350fcd3673358e744f5de4c2cbcfda115e32e'
            '2dc44f44e9e4a6f97a46823addf708fa9e1cdb05f5a2f385ec8d06d2b952c44162bb071076ff0531fd5045b08578db6d0005201568515cf98876256acc62319b'
            '10181e8ef9495cd646e9207cf406398de38942977f2228f16693cc024c94499c35e00028134724e6444075a7bb3b55949710594179cf38fa8c02d1d3c9e0e0e7')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Link_pthread.patch"
  patch -p0 -i "${srcdir}/revert_r161.patch"

  autoreconf -vfi
  intltoolize -f
  # Static libraries are needed for the tests
  ./configure --prefix=/usr --libexecdir=/usr/share/${pkgname} #--disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -name '*.a' -delete
}

# vim:set ts=2 sw=2 et:
