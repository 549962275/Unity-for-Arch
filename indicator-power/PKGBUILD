# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+14.04.20140328
pkgver=${_actual_ver}daily14.03.28
pkgrel=3
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('dconf')
makedepends=('intltool' 'python2')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Not_a_phone.patch')
sha512sums=('ef36aa53969861636fe512c6e8d1df0dda19dc4e6c99aada954ef1ecfe2d170bff00eafb52fa1fdd84cf3cd7cfeeb0e5a33fd8b1bfe199b9cde970de00a943a5'
            'e7ce61788b59995f950b24b75899e517749319a209fd3b857cde26f8610ae0ddbefb82bf32e445b306caa65fac32871c1e7b6fd5c72e6debf88553ca9e12dcfc')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Not_a_phone.patch"

  sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi
  intltoolize -f
  # Static libraries are needed for the tests
  ./configure --prefix=/usr --libexecdir=/usr/lib #--disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  find "${pkgdir}" -type f -name '*.a' -delete

  rm -r "${pkgdir}/usr/share/upstart/"
}

# vim:set ts=2 sw=2 et:
