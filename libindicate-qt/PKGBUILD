# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libindicate-qt
_ubuntu_rel=1ubuntu3
pkgver=0.2.5.91.${_ubuntu_rel}
pkgrel=100
pkgdesc="This project provides a set of Qt bindings for libindicate, the indicator system developed by Canonical Desktop Experience team"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate-qt"
license=('GPL')
depends=('qt' 'libindicate')
makedepends=('cmake')
groups=('unity')
source=("http://launchpad.net/${pkgname}/${pkgname%-*}-0.5/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('62b6ee0a758729f8007d0dfe6c7b9f55264f0bc110cebc8ef9f782c345fb81e45d9b652de45db06105510a232449b2427fb9a8344f86fb911d3fac1d5c552a23'
            '12edd6e44bd25fe906a925f5239896f82d9a4c8ff07bea967897590b82654357793ab1ea7f61a7cf34961ecd915e7fab3347a381c0c6b487a37f957c0c281870')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install

  # Not present in Ubuntu packages
  rm -rvf "${pkgdir}/usr/include/libindicate-qt/CMakeFiles"
}
