# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-datetime
pkgname=('indicator-datetime' 'indicator-datetime-gtk2')
pkgver=0.3.0
pkgrel=1
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
makedepends=('intltool')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/0.3/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('28ca8969d16eb3250f7ae736be8476e5d5e36d4c435dd45cf4e7e8f45068aca3280db664d64c3f5060931c4b8b30aac645e9af5a521c92d8efe3a0f1f621f59d')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make
  popd
}

package_indicator-datetime() {
  pkgdesc="A very, very simple clock - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'gtk3-ubuntu' 'libido3' 'geoclue' 'evolution-data-server' 'libunique3' 'gnome-control-center-ubuntu')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-datetime-gtk2() {
  pkgdesc="A very, very simple clock - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'gtk2-ubuntu' 'libido' 'geoclue' 'indicator-datetime')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  install -d -m755 "${pkgdir}/usr/lib/indicators/6/"
  install -m755 'src/.libs/libdatetime.so' "${pkgdir}/usr/lib/indicators/6/"
}
