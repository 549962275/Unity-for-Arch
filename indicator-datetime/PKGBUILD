# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-datetime
_actual_ver=12.10.3
_extra_ver=daily13.02.06
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'ido' 'evolution-data-server' 'gnome-control-center')
makedepends=('intltool')
checkdepends=('gtest-ubuntu' 'xorg-gtest' 'xorg-util-macros')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Port_to_systemd_timedated.patch'
        '0002_Remove_timezone_functionality.patch'
        '0003_Link_pthread.patch'
        'revert_r201.patch')
sha512sums=('ff5df92f5d681ab6df3eaf5ec308f50b60171182b7f690f881f01eb7547f0449c6d246ed3dc89f27351aa709cd8913ebad9751af7e454b7f54c1dec38de886ab'
            '57c0b1846c946bcc2fffd45b933c83fa7ef14e1e19e4431238be4a406bc6565aba5c80dabaed5fc8b46d83377c71d5d2c55331599813827fa70290a9f02dd51b'
            '79b563e7d31e1f0c56c9b592fd370e1b5f3efd82f84b52fcee937e35306cdf74966de5f2c3997cddd9078ac9cfd3e60233832be8efac27bb80e043fa13964838'
            '5c10fda83393b33b45c1fd0701e464ac1e9cc3180c3d1ebb47f5e666e57065b64f34047c913f90db5d047ffef094fe0b4ef625fe04ebeefc74b341a8f6612cf2'
            'eb3d061dbaa9451373f25a977eb3280b903fd9b046fa26246a08bae0a214afc2aa54cc474e9c135aa1f5f2488751fdb29e1fa5239c69a48f5b8907a8c7d4826c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated
  patch -p1 -i "${srcdir}/0001_Port_to_systemd_timedated.patch"

  # Removes all timezone functionality from the indicator.
  # - Removes indicator-datetime's own timezone settings. They do not work with
  #   systemd and requires much work to be ported when GNOME's own timezone
  #   settings can be used instead.
  # - Removes dependencies on libtimezonemap and geoclue
  # - Removes ability to disable multiple timezones
  # - Removes ability to changes timezone based on physical location
  patch -p1 -i "${srcdir}/0002_Remove_timezone_functionality.patch"

  # Tests need to link against pthread
  patch -p1 -i "${srcdir}/0003_Link_pthread.patch"

  patch -p0 -i "${srcdir}/revert_r201.patch"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
