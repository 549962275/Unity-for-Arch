# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+15.04.20150213.1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gstreamer libtimezonemap properties-cpp
         unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Get-timezone-from-systemd-s-timedate1-interface.patch
        0002-Not-a-phone.patch)
sha512sums=('6bdaab7702ee27e043f205b71fda014a81f71bc2a5cacc7bdb0dbcf3fd092e2e2e50738a271c5ef31d29ab797a96191f7e12ef256a8d408b009deae4e03e97db'
            'a1068fb5632d8b713d3487d2e36ce91824c989bb8143878e9ec4d27e8fdd5f515c255fb10c89dd78b3e7e25b4f3a44cb2f958d6bb12ac521172d76e22ff39dd5'
            '6218303028d7033724461960fe1a2746f5e00206c2b01fa50d55c0b9545ab23b2822c2a40cfa6e82986928bf7f15916aa601359b67189b4b4e7fc3c38f85ae41')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i ../0001-Get-timezone-from-systemd-s-timedate1-interface.patch
  patch -p1 -i ../0002-Not-a-phone.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
