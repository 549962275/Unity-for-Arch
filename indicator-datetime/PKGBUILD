# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=15.10
_extra_ver=+16.10.20160820.1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
# ubuntu-app-launch, uuid
depends=(evolution-data-server gsettings-ubuntu-schemas gstreamer
         indicator-messages libaccounts-glib libtimezonemap properties-cpp
         unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch)
sha512sums=('e8fda06ed98cb6045443728ccaac2f4e91d860c55ca30d5957b943cbe5f45da4703ce670fdf0b6853988a26454b9ccfb314c770fc08d6ce6e4f7b016ffae448d'
            '9f98bfbf5424550e23e7ca4b72e1abaea68f5fddd98cc90265a23162efcbd2adb4293aab2e3bc0c98c5ec7ab5708bb93e1e01176300e77f65088a6575f5a0cd7')

prepare() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i 0001-Not-a-phone.patch
}

build() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    export CFLAGS+=" -Wno-error"

    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF

    make
}

package() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"
    cd build

    make DESTDIR="${pkgdir}/" install

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
