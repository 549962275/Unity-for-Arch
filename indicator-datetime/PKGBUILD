# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-datetime
pkgname=('indicator-datetime' 'indicator-datetime-gtk2')
_ubuntu_rel=0ubuntu5
pkgver=0.3.1.${_ubuntu_rel}
pkgrel=102
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
makedepends=('intltool')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        'fix_build_with_dbusmenu.patch')
sha512sums=('24a2cbcf77477497cf0aed13a48f081d0532874d40db982b071ba43f61cd407dfd6dc4595bbb26de0c8a10f179b08fab33cecb83b646c69ff6bd06cb4c63b175'
            '0444532a75a937412f1a52a511b9151fb8b8063ab118827557fd7b8ef66874ae25d358b5c3f1f42caa8dcb7931afb0d5df1dd535323b3bc6e57bf4c1933bfd0c'
            '3ba72e7c2a539a638fd7d44ba833bbdf6765f2a5fc823a2bf33f76cba5e04c198139ce45e6bfb2c226a401df507e4de9dac871da7c6c94dda8ec3b968b76d4ec')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Build fix with new libdbusmenu
  patch -Np1 -i "${srcdir}/fix_build_with_dbusmenu.patch"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-datetime() {
  pkgdesc="A very, very simple clock - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'gtk3-ubuntu' 'libido3' 'geoclue' 'evolution-data-server' 'gnome-control-center-ubuntu')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-datetime-gtk2() {
  pkgdesc="A very, very simple clock - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'gtk2-ubuntu' 'libido' 'geoclue' 'indicator-datetime')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  INDICATOR_VERSION=$(ls /usr/lib | grep libindicator | grep 'libindicator.so.[0-9]$' | awk -F'.' '{print $NF}')

  install -d -m755 "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
  install -m755 'src/.libs/libdatetime.so' "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
}
