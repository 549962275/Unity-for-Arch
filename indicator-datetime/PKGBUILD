# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-datetime
pkgname=('indicator-datetime' 'indicator-datetime-gtk2')
_ubuntu_rel=0ubuntu1
pkgver=0.3.90.${_ubuntu_rel}
pkgrel=100
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
makedepends=('intltool' 'libtimezonemap')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/0.4/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('1573d6bdb28379913b0a8dc7025d48079571514cb04f081c0fa95195b9c8628db7f9ac05367af42ed3afa84019a29f1732d0c38b842e667cf9e213e4672aeaca'
            'e9186dce6de558cd1c9f2e614e4d7c1eaf20ce664ca73c46fc0bdee50f965efbc831c39f14ed253260ad521641f436b40621ee904eeaa006eb06b01217cbb2bb')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-datetime() {
  pkgdesc="A very, very simple clock - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'gtk3-ubuntu' 'libido3' 'geoclue' 'evolution-data-server' 'gnome-control-center-ubuntu' 'libtimezonemap')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-datetime-gtk2() {
  pkgdesc="A very, very simple clock - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'gtk2-ubuntu' 'libido' 'geoclue' 'indicator-datetime')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  INDICATOR_VERSION=$(ls /usr/lib | grep libindicator | grep 'libindicator.so.[0-9]$' | awk -F'.' '{print $NF}')

  install -dm755 "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
  install -m755 'src/.libs/libdatetime.so' "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
}
