# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-datetime
pkgname=('indicator-datetime' 'indicator-datetime-gtk2')
_ubuntu_rel=0ubuntu2
pkgver=0.3.94.${_ubuntu_rel}
pkgrel=101
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
makedepends=('intltool' 'libtimezonemap')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/0.4/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('44214a834fba00d4c810b9029bc01ca1a25dde14abfa295f5b87ee8b4aa49e37e41aa08da1e751c925020c8e93d1bbb7e9683ead29f4e504def9afd6bce94692'
            '8a019da66a1457a581629a39e11e5478895934a97cba3d0b30cd9c18e82688499c6a0f8fec0606e9208b2bef94f0f65239b7933172e8066c85cb1b4a89fe803f')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-datetime() {
  pkgdesc="A very, very simple clock - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'libido3' 'geoclue' 'evolution-data-server' 'gnome-control-center' 'libtimezonemap')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-datetime-gtk2() {
  pkgdesc="A very, very simple clock - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'libido' 'geoclue' 'indicator-datetime')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  INDICATOR_VERSION=$(ls /usr/lib | grep libindicator | grep 'libindicator.so.[0-9]$' | awk -F'.' '{print $NF}')

  install -dm755 "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
  install -m755 'src/.libs/libdatetime.so' "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
}

# vim:set ts=2 sw=2 et:
