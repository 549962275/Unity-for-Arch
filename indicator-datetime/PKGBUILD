# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+15.04.20141103
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gstreamer libtimezonemap properties-cpp
         unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Get-timezone-from-systemd-s-timedate1-interface.patch
        0002-Not-a-phone.patch)
sha512sums=('991b34e4d1c0384265e465bd4a8cbfd55ba20d3064dc1052ff702ffaf6aea9758c9717b1a7a0de211859a4eb1216aaaf4490a5fac0102dd6e5187a7a6376138a'
            'd592e60106abedb73d4c82b8346c1d47ae2dc1350a792da4ce4cdf190db7dacda7a77fdd40bf5b28ca39d5c4759a2241956dcc1168907d210f69fa8f5996057e'
            '0e30f53da2212c19e696b6d9c6f3c9b736278f14eb765698ec02ef70b4d4ba6b31d402b598383401b25aa36342f375f25366a8931e698c2c9c8a7311d15e3bad')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i ../0001-Get-timezone-from-systemd-s-timedate1-interface.patch
  patch -p1 -i ../0002-Not-a-phone.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}
