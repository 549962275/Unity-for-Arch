# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-datetime
_ubuntu_rel=0ubuntu1
pkgver=12.10.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'libido3' 'geoclue' 'evolution-data-server' 'gnome-control-center' 'libtimezonemap')
makedepends=('intltool' 'libtimezonemap')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/12.10/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        '0001_Revert_port_to_EDS_3.6_API.patch')
sha512sums=('8427eba92c8ed526a529bd40946e0607cf41a52e8bdac1799038129ec02b8531ee9649dfa4eb95c5db5687cdf40a86e6fda0d92ebd399dc4203b4795c152491a'
            '4507f7072179dc05190fcd57aa40af3ec77671953875babb28e53b46dc73dd88b5c9f08d6404f6d6242bd19f9a3b1c645bdab713c9d3acb54033f176ff89293c'
            '86552b1d50241e073558dccff1d0a26194de2b0bc33b31ee85ec955852d83c34ac30080fa27411eb9d1f9827f995c6bb34f3d55bebb0bfead8d305ba2f66eb2c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  patch -Np1 -i "${srcdir}/0001_Revert_port_to_EDS_3.6_API.patch"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
