# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+14.04.20140311.1
_translations=20130418
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=100
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('ido' 'evolution-data-server' 'libcanberra' 'libtimezonemap' 'geoclue' 'properties-cpp' 'unity-control-center')
makedepends=('intltool' 'cmake')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-datetime.tar.gz"
        'indicator-datetime.service'
        '0001_Port_to_systemd_timedated.patch'
        '0002_Not_a_phone.patch')
sha512sums=('0d58da0d6ff8f1ed403f7bc609e393d5a6062e59a10623c2c33d62dcc346667f99541e882fc0858287c2812fe0a6c14ed48ff3f7ff4b20bb73ed0c4544e6d4e4'
            '5cb8d7ab7cfb1e57d9b0d3e54e09caa1cbfdf44191f32e19251983e061bf5c27812dbcb743cad17cb4ff13e6f6f22167c6d3bd0f2faa8e5a84a7bf145f90223c'
            '217b93ef6705e80853d009277df5ec43d0d2fde1cb9945cabd3183e4c3f695b4bc4cfa8168052398ebc0d56fe5e9f8828c2e58accf856e0dee719534fef918bf'
            '57c0b1846c946bcc2fffd45b933c83fa7ef14e1e19e4431238be4a406bc6565aba5c80dabaed5fc8b46d83377c71d5d2c55331599813827fa70290a9f02dd51b'
            'a200e2ae03a8684db00b1c3b30d122a37dbadab146eac3365b3a6ee25da9aa1ff991fe6561f040d54ee2950c47e6d4176616c5804dc28bd0471c80f75c0d52a6')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated
  #patch -p1 -i "${srcdir}/0001_Port_to_systemd_timedated.patch"

  patch -p1 -i "${srcdir}/0002_Not_a_phone.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-datetime.service" \
                "${pkgdir}/usr/share/dbus-1/services/"
}

# vim:set ts=2 sw=2 et:
