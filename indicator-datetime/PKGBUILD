# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+15.10.20151005
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-datetime"
license=(GPL)
depends=(evolution-data-server gstreamer libtimezonemap properties-cpp
         unity-control-center)
makedepends=(intltool cmake)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0002-Not-a-phone.patch)
sha512sums=('3d28cab736f427c3902460a76b93ef8f7ca5ad3738b909829e4b25a4fb8b356c8ab230d3bb7d0a53c727cd324e681feaa2eb72107415b11d46a402d0bf6672e4'
            '0b516676392a0e09e16f2529b917db905dfb3404e5fdd758d86444359ac4e0a836e03451c4f8543382adea77b7ee09cc4699fc339f8aa578e9ec504ac4e48c71')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0002-Not-a-phone.patch
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    export CFLAGS+=" -Wno-error"

    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF

    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

    make DESTDIR="${pkgdir}/" install

    rm -r "${pkgdir}/usr/share/upstart/"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
