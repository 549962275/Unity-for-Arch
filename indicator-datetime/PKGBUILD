# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-datetime
_actual_ver=12.10.3
_extra_ver=daily13.02.01
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'ido' 'evolution-data-server' 'gnome-control-center')
makedepends=('intltool')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Port_to_systemd_timedated.patch'
        '0002_Remove_timezone_functionality.patch'
        'revert_r201.patch')
sha512sums=('bf5968400fd506d385ddfe0a5f06dd7fa676e5895cf1ca500a2cd71adb96612b0e4a49dbe10fcbef01ff4de33ac05d41628831af0b6adf13bdb7e4326461dd5a'
            '1a931f25552b916229cd9e773423910803cc453a8a088e70b0b8f889d235e75d985264e90f2045b2c40246245c20b29017ae861386264d46eb317976d87f8039'
            '79b563e7d31e1f0c56c9b592fd370e1b5f3efd82f84b52fcee937e35306cdf74966de5f2c3997cddd9078ac9cfd3e60233832be8efac27bb80e043fa13964838'
            'eb3d061dbaa9451373f25a977eb3280b903fd9b046fa26246a08bae0a214afc2aa54cc474e9c135aa1f5f2488751fdb29e1fa5239c69a48f5b8907a8c7d4826c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated
  patch -p1 -i "${srcdir}/0001_Port_to_systemd_timedated.patch"

  # Removes all timezone functionality from the indicator.
  # - Removes indicator-datetime's own timezone settings. They do not work with
  #   systemd and requires much work to be ported when GNOME's own timezone
  #   settings can be used instead.
  # - Removes dependencies on libtimezonemap and geoclue
  # - Removes ability to disable multiple timezones
  # - Removes ability to changes timezone based on physical location
  patch -p1 -i "${srcdir}/0002_Remove_timezone_functionality.patch"

  patch -p0 -i "${srcdir}/revert_r201.patch"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
