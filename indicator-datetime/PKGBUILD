# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-datetime
_actual_ver=13.10.0
_extra_ver=+14.10.20140701
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('evolution-data-server' 'libcanberra' 'libtimezonemap' 'properties-cpp' 'unity-control-center')
makedepends=('intltool' 'cmake')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Get-timezone-from-systemd-s-timedate1-interface.patch'
        '0002_Not_a_phone.patch')
sha512sums=('4d9ae9297c91c616533f73c1b3f2e5a8c65f6746eca1dbb86e5cdb52e55485c29822b71ebe8183c47f0fd793e76671c71cf772b95f2c4ab7e01b18893ced950b'
            'dff4c2d02631a68a3ade6b3fb662f38495685498ff4e7c9ca1f2ee7d82eb7f93f991d071c050e6d012fe03947d30579c95b5e27483b0096df24be7254d468de3'
            'b91c27dba2b08b69e597f9165406f68325b5d822e26d7dc5aa271a7f62b77d715cb78dfae93792b345d922ace383f6ed98cb63f096745ef37a3f7df5c4b37298')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated (because /etc/timezone is no longer used)
  patch -p1 -i "${srcdir}/0001-Get-timezone-from-systemd-s-timedate1-interface.patch"
  patch -p1 -i "${srcdir}/0002_Not_a_phone.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -Denable_tests=OFF

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

# vim:set ts=2 sw=2 et:
