# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>
# Contributor: thn81 <root@scrat>

pkgname=mutter-ubuntu
_ubuntu_rel=1ubuntu1
pkgver=3.2.2.${_ubuntu_rel}
pkgrel=100
pkgdesc="A window manager for GNOME3 with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('startup-notification' 'gconf' 'zenity' 'libcanberra' 'clutter' 'gobject-introspection')
makedepends=('intltool' 'gnome-doc-utils')
groups=('gnome')
provides=("mutter=${pkgver}")
conflicts=('mutter')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=mutter.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'disable_-Werror.patch')
sha512sums=('def486c808c0300087a2bc8872f203e376c59a6f669ed8d03586f0176e5d9102f3847229f28be50f5253f01474a9a32145e2d8d6b26f32270a3d394ac12dd466'
            'efef89dc7b987bd8bf6f86bc0c2033c2b3c79fc93cf2620fab3f094b424e09909243ca74ae55abdbaa40a3167a44f842f5375ed1c1f18be1745cae748b23e596'
            'bbec0c740557d936135d8c11636deb5ba83d57912c0edcc72d438d3040b89f2d2f5a95892a89d1e95ea0749664987e9d1201780d0a67ef9bd1ef9ff4f702701d')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Disable '-Werror'
  patch -Np1 -i "${srcdir}/disable_-Werror.patch"

  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/mutter \
    --enable-compiler-warnings=no \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification=yes # From debian/rules
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain mutter "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas

  # Install Ubuntu stuff
  install -d -m755 "${pkgdir}/usr/share/sgml/mutter-common/"
  install -m644 "${srcdir}/debian/mutter-common.catalog" "${pkgdir}/usr/share/sgml/mutter-common/catalog"
}
