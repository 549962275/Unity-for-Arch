# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gnome-control-center-unity
_translations=20130418
_actual_ver=1.3
_extra_ver=+13.10.20131004
pkgver=${_actual_ver}daily13.10.04
pkgrel=1
pkgdesc="A set of settings panels for configuring the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/gnome-control-center-unity"
license=('GPL')
groups=('unity')
depends=('gnome-control-center-ubuntu' 'gsettings-desktop-schemas' 'libnotify' 'libxml2')
makedepends=('gnome-common' 'intltool')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-control-center-unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-control-center-unity.tar.gz")
sha512sums=('6b4f1ba32e1be89402d3f78b258ed170475c93c0b15626e49d33b15fc649f85b12a1126ddcb0fbd5a402fa16de2df7ac5bf06749df3a4c756f1ad1a90db78d78'
            'e9dd7b099df99c55938cf882639e8cfc03101e97761f3209db7ad5b218f5f6345cd49e5679fe7033264f1676dea03a093351ecf14b5ea8d172390a08bfa7759d')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po "${srcdir}"/gnome-control-center-unity/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
