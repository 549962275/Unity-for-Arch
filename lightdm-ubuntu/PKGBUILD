# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# This package no longer depends on a specific toolkit. It will build the GTK and
# Qt libraries and recommend the installation of those respective tookits using
# optdepends.

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=yakkety
# vercheck-launchpad: name=${pkgname%-*}

pkgname=lightdm-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=1.19.0
pkgrel=1
pkgdesc="A lightweight display manager"
arch=(i686 x86_64)
url="https://launchpad.net/lightdm"
# Libraries are LGPLv3+, everything else is GPLv3+
license=(GPL3 LGPL3)
groups=(unity)
depends=(libgcrypt libxcb libxdmcp libxklavier)
# Useful dependencies, but not required
#depends=('gnome-themes-standard' 'gnome-backgrounds' 'gnome-icon-theme' 'webkitgtk3')
makedepends=(gnome-common gobject-introspection gtk-doc gtk3 intltool qt4 qt5-base vala yelp-tools)
optdepends=('accountsservice: DBus interface for querying user information'
            'bindfs: Used for guest session to avoid copying skeleton dotfiles'
            'gnome-keyring: For pam_gnome_keyring.so in the greeter PAM config'
            'gtk3: For using the GTK greeter'
            'lightdm-unity-greeter: Default Ubuntu 13.04 Greeter'
            'qt4: To use the qt4 version of liblightdm-qt'
            'qt5-base: To use the qt5 version of liblightdm-qt')
provides=("lightdm=${pkgver}" "liblightdm-qt4=${pkgver}")
conflicts=(lightdm liblightdm-qt4)
options=(emptydirs)
backup=(etc/lightdm/keys.conf
        etc/lightdm/lightdm.conf
        etc/lightdm/users.conf)
install=lightdm.install
source=(#"https://launchpad.net/ubuntu/+archive/primary/+files/lightdm_${pkgver}.orig.tar.gz"
        #"https://launchpad.net/ubuntu/+archive/primary/+files/lightdm_${pkgver}-${_ubuntu_rel}.diff.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/lightdm_${pkgver}-${_ubuntu_rel}.tar.gz"
        lightdm.service
        lightdm.tmpfiles.d
        lightdm.pam
        lightdm-autologin.pam
        lightdm-greeter.pam
        lightdm.rules
        0001-guest-account-Add-default-GSettings-support.patch)
sha512sums=('b4395f42adcb2256d877cde0107151a21035f7975975b28749205280e1abc54f0703fea1b59a513b11c612674ee7e8b0d81e86c63858c0f0d9653366f3fe6b4e'
            '3e6f9827da3c69278c175e4bb2d819f4960f2c7ec94debb34075a20ee634d963532750ca78952f5f3de7c725a2c67381afd05e78076142394366137b83a8c08d'
            '81a76a49eb208b1d33f5ac0184d87a377cd37c522d74a93ccd3d96b3d6e32c44872a65873b91fec3daba0846cdb5a938b51944697e636c045d03259bd5424644'
            '3e68365562173ef48de5cce9da3711aff8c1194f1577e3eab86f68c681861fc79b9bc83f8309bd627f5e6070a8f0d4b55e2173f47115f68fa5d31a505f0b4b54'
            'cdb2565103830a7ae4b0923126af356fab00f9b9595802ab97068f4c70893895aee6edfb094095fcfb7aa6b88e1f7403dea6784c7236021a45c793700e8e11df'
            'e147dcaf4a0052a84133fd5cd0e48bbc4decd398a4cc7e2cfc40c50e80a0a4ff56b361e688419f7b9eac958d074d20737158dd56abd25e3a1f6304558b8d9dfa'
            '8d6aa12c4d129c25e56ecf2904db4e294d46631d11bd8bec2f20a76c871ba758094abb24616d3d2038a684fbb736ee61d1f80697d525d62c4dc68113e101194f'
            '393ede18563aab484aace26c6c470b07e16f79025fec5eec03e0f1072e39316bc73a73d45da843c6b76f356e8b069fda66fa3863f39d1106dbf1f3ff7e4175a6')

prepare() {
    cd "lightdm-${pkgver}"

    #patch -p1 -i "../lightdm_${pkgver}-${_ubuntu_rel}.diff"

    # Do not use Ubuntu's language-tools
    sed -i '/04_language_handling.patch/d' debian/patches/series

    for i in $(grep -v '#' debian/patches/series); do
        patch -p1 -i "debian/patches/${i}"
    done

    # Add support for settings GSettings/dconf defaults in the guest session. Just
    # put the files in /etc/guest-session/gsettings/. The file format is the same
    # as the regular GSettings override files.
    patch -p1 -i ../0001-guest-account-Add-default-GSettings-support.patch
}

build() {
    cd "lightdm-${pkgver}"

    export MOC4=moc-qt4
    export MOC5=moc-qt5

    gtkdocize
    aclocal --install --force
    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --disable-static \
        --libexecdir=/usr/lib/lightdm \
        --localstatedir=/var \
        --with-user-session=ubuntu \
        --with-greeter-user=lightdm \
        --disable-libaudit

    make
}

package() {
    cd "lightdm-${pkgver}"
    make DESTDIR="${pkgdir}/" install

    install -dm755 "${pkgdir}/usr/lib/systemd/system/"

    # Additional LightDM configuration files
    install -dm755 "${pkgdir}"/etc/lightdm/lightdm.conf.d/
    install -m644 debian/50-{xserver-command,greeter-wrapper,guest-wrapper,disable-log-backup}.conf \
                  "${pkgdir}"/etc/lightdm/lightdm.conf.d/

    # Install PAM service
    install -dm755  "${pkgdir}"/etc/pam.d/
    install -m644 "${srcdir}"/lightdm.pam "${pkgdir}"/etc/pam.d/lightdm
    install -m644 "${srcdir}"/lightdm-autologin.pam \
                  "${pkgdir}"/etc/pam.d/lightdm-autologin
    install -m644 "${srcdir}"/lightdm-greeter.pam \
                  "${pkgdir}"/etc/pam.d/lightdm-greeter

    # Install configuration files
    install -dm755 "${pkgdir}"/usr/share/doc/lightdm/
    install -m644 "${pkgdir}"/etc/lightdm/lightdm.conf \
                  "${pkgdir}"/usr/share/doc/lightdm/
    install -m644 "${pkgdir}"/etc/lightdm/keys.conf \
                  "${pkgdir}"/usr/share/doc/lightdm/

    # Install binaries and scripts
    install -dm755 "${pkgdir}"/usr/bin/
    install -m755 debian/guest-account.sh "${pkgdir}"/usr/bin/guest-account
    install -m755 debian/guest-session-auto.sh "${pkgdir}"/usr/lib/lightdm/
    install -m755 debian/lightdm-session "${pkgdir}"/usr/bin/
    install -m755 debian/lightdm-greeter-session "${pkgdir}"/usr/lib/lightdm/
    install -m755 debian/config-error-dialog.sh "${pkgdir}"/usr/lib/lightdm/

    # Skeleton files for guest account
    install -dm755 "${pkgdir}"/usr/share/lightdm/guest-session/skel/.config/autostart/
    install -m644 debian/guest-session-startup.desktop \
                  "${pkgdir}"/usr/share/lightdm/guest-session/skel/.config/autostart/
    install -m755 debian/guest-session-setup.sh \
                  "${pkgdir}"/usr/share/lightdm/guest-session/setup.sh

    # Install systemd service
    install -m644 "${srcdir}"/lightdm.service "${pkgdir}"/usr/lib/systemd/system/

    # Install systemd tmpfiles.d file
    install -dm755 "${pkgdir}"/usr/lib/tmpfiles.d/
    install -m644 "${srcdir}"/lightdm.tmpfiles.d \
                  "${pkgdir}"/usr/lib/tmpfiles.d/lightdm.conf

    # Install PolicyKit rules from Fedora which allow the lightdm user to access
    # the systemd-logind, consolekit, and upower DBus interfaces
    install -dm750 -g 102 "${pkgdir}"/usr/share/polkit-1/rules.d/
    install -m644 "${srcdir}"/lightdm.rules \
                  "${pkgdir}"/usr/share/polkit-1/rules.d/

    # Configuration settings that differ from Ubuntu
    sed -i \
        -e 's/^\(minimum-uid=\).*$/\11000/g' \
        "${pkgdir}"/etc/lightdm/users.conf

    # Configuration files specific to Ubuntu
    rm -rvf "${pkgdir}"/etc/init/

    # Create GSettings defaults directory
    install -dm755 "${pkgdir}"/etc/guest-session/gsettings/

    # Remove apparmor stuff
    rm -rvf "${pkgdir}"/etc/apparmor.d/
}
