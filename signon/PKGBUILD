# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=signon
_ubuntu_rel=0ubuntu1
pkgver=8.43.${_ubuntu_rel}
pkgrel=100
pkgdesc="Single Sign On Framework"
arch=('i686' 'x86_64')
url="https://code.google.com/p/accounts-sso/"
license=('LGPL')
groups=('unity')
depends=('openssl' 'qt')
makedepends=('doxygen' 'graphviz')
source=("https://accounts-sso.googlecode.com/files/signon-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/signon_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        '0001_Disable_cryptsetup_extension.patch')
sha512sums=('279b1fea5fbfc9b8b486f09ca838ce76310ad3e7136e53a768a23146c2b82032cd6dbfeb77e0bf47391881354ad02120d98659548e47050d5f66c0a2d4b7ee29'
            'ef32c5c1e27209b5c4f1c78d019f08d8ca6e9b397ac60329c39b03cc42c12bfeb0f9cc3cbbfbe7193397317959f028461571c307e4cac840ffbab4636b5bc9d3'
            '39efe6bf469c4cc0ecb58d91a3e2e28d699e2b1a0895394c730776cdf3185fe4c24345d00281f2db8723d14ecaa9d6616a09ddedbb99741bf6272e098da4db7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Disable cryptsetup extension (not ported to current API)
  patch -Np1 -i "${srcdir}/0001_Disable_cryptsetup_extension.patch"

  qmake PREFIX=/usr LIBDIR=/usr/lib QMAKE_CXXFLAGS="${CXXFLAGS}"

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make INSTALL_ROOT="${pkgdir}/" install

  # Remove tests
  find "${pkgdir}" -type f -name '*tests*' -delete
}

# vim:set ts=2 sw=2 et:
