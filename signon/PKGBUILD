# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=signon
_ubuntu_rel=0ubuntu2
pkgver=8.42.${_ubuntu_rel}
pkgrel=100
pkgdesc="Single Sign On Framework"
arch=('i686' 'x86_64')
url="https://code.google.com/p/accounts-sso/"
license=('LGPL')
groups=('unity')
depends=('openssl' 'qt')
makedepends=('doxygen' 'graphviz')
source=("https://accounts-sso.googlecode.com/files/signon-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/signon_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        '0001_Disable_cryptsetup_extension.patch')
sha512sums=('af7ad85139106eeb9adcc8f3618c85283c0c7e4c29e43f6420bd833826d9d0a9fd83938457992bb63d0db938601311a823e9065fc7a248a0b1b698b644bb5e44'
            '777dc591a0d01e6203dfc0f6527d82bcf99ed7e870250d0a8308387c29691295f9ea81f461803f7d061d9962c5c6d30005bc1635ace021725765f8b925983d39'
            '39efe6bf469c4cc0ecb58d91a3e2e28d699e2b1a0895394c730776cdf3185fe4c24345d00281f2db8723d14ecaa9d6616a09ddedbb99741bf6272e098da4db7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Disable cryptsetup extension (not ported to current API)
  patch -Np1 -i "${srcdir}/0001_Disable_cryptsetup_extension.patch"

  qmake PREFIX=/usr LIBDIR=/usr/lib QMAKE_CXXFLAGS="${CXXFLAGS}"

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make INSTALL_ROOT="${pkgdir}/" install

  # Remove static libraries
  find "${pkgdir}" -type f -name '*.a' -delete

  # Remove tests
  find "${pkgdir}" -type f -name '*tests*' -delete
}

# vim:set ts=2 sw=2 et:
