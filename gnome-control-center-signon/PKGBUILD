# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Why are there three names?
# - gnome-control-center-signon (Ubuntu's packaging)
# - credentials-control-center (Source tarball)
# - online-accounts-gnome-control-center (Website)

# We'll use the same package names as Ubuntu (gnome-control-center-signon)

pkgname=gnome-control-center-signon
pkgver=0.0.17
pkgrel=100
pkgdesc="GNOME Control Center extension for single signon"
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-gnome-control-center"
license=('GPL')
groups=('unity')
depends=('dbus-glib' 'gobject-introspection' 'libaccounts-glib' 'gnome-control-center' 'libsignon-glib' 'signon' 'hicolor-icon-theme')
makedepends=('gnome-common' 'gnome-doc-utils' 'gtk-doc' 'itstool' 'vala' 'yelp-tools')
options=('!libtool')
install=gnome-control-center-signon.install
source=("https://launchpad.net/online-accounts-gnome-control-center/trunk/${pkgver}/+download/credentials-control-center-${pkgver}.tar.bz2")
sha512sums=('b1fc0e81a272a685406361ca0e89f3348bd931f77075e068d2566808b82a3db0e105d1052907e0c62b77be36feb21a4f61a1d8293a6cf4c1aa1f04e3a137a1ab')

build() {
  cd "${srcdir}/credentials-control-center-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --enable-gtk-doc
  make -j1
}

package() {
  cd "${srcdir}/credentials-control-center-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
