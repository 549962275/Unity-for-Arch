# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libqtdee
_ubuntu_rel=0ubuntu1
pkgver=0.2.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="Provides a set of Qt bindings for libdee"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee-qt"
license=('GPL')
depends=('qt' 'dee')
makedepends=('cmake')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('7d2b0c8dc9fe83c99d265e6e65cc9c2f250bcd5f1786f4b5feb75861ba7c96555534e52a3ccec2a2dec1691822ac70b2969eb3e479ffab798433f77c38c0c9e8'
            'c3607e2e089772d5471c4437e6deeac08ddd4f92a158e0e630212f6de0300696772972656a5ae570f1709b692e5cbaeacb63e5d1ff7f08ecdcd1bdf8922e5f0a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Fix Qt imports directory
  sed -i '/set(IMPORT_INSTALL_DIR/d' CMakeLists.txt

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DIMPORT_INSTALL_DIR=lib/qt/imports/dee \
    -DCMAKE_BUILD_TYPE=Release
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install

  # Install pkgconfig file
  install -dm755 "${pkgdir}/usr/lib/pkgconfig/"
  install -m644 'libqtdee.pc' "${pkgdir}/usr/lib/pkgconfig/"
}
