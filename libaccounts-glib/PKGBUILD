# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libaccounts-glib
_ubuntu_rel=0ubuntu2
pkgver=1.3.${_ubuntu_rel}
pkgrel=100
pkgdesc="Account management library for GLib Applications"
arch=('i686' 'x86_64')
url="https://code.google.com/p/accounts-sso/"
license=('LGPL')
groups=('unity')
depends=('dbus-glib' 'gobject-introspection' 'libxml2' 'python-gobject' 'python2-gobject' 'python2' 'python3' 'sqlite')
makedepends=('check')
source=("https://accounts-sso.googlecode.com/files/libaccounts-glib-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libaccounts-glib_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('30b2c3463bc9d021086c3dfe45addf5994435013c9ede479ab725453c2e22d7e65c3d066cd712f735925205d72727435c7910c8f2ef5517f891708f10a46333d'
            '34079a22c284950ccbc93b157b3c2f11984039abbe5501c5b64a254cea6a022c02a122082d7d400f05291f5820603baacb76ae133475daef4059f5b1c4a67c32')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  mkdir build-python2 build-python3

  pushd build-python2/
  PYTHON=python2 ../configure --prefix=/usr --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
  popd

  pushd build-python3/
  PYTHON=python3 ../configure --prefix=/usr --disable-static
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  pushd build-python2/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-python3/pygobject/
  make DESTDIR="${pkgdir}/" install
  popd

  # Remove Meego specific file
  rm -v "${pkgdir}/usr/share/backup-framework/applications/accounts.conf"

  # Remove useless empty files
  rm -rv "${pkgdir}/usr/doc/"
}

# vim:set ts=2 sw=2 et:
