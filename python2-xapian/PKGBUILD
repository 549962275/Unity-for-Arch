# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=python2-xapian
pkgver=1.2.10
pkgrel=100
pkgdesc="SWIG and JNI bindings allowing Xapian to be used from various other programming languages"
arch=('i686' 'x86_64')
url="http://xapian.org/"
license=('GPL')
depends=('xapian-core' 'python2')
replaces=('python-xapian')
conflicts=('python-xapian')
groups=('unity')
source=("http://oligarchy.co.uk/xapian/${pkgver}/xapian-bindings-${pkgver}.tar.gz")
sha512sums=('1a8eccb8a38d256f26d14493e8afff69a7e5d51db1e30548ea21bb991247dd59614dfce6edd2d9314723672607b6a283b03a37d2795006739ca4bcda188e22c3')

build() {
  cd "${srcdir}/xapian-bindings-${pkgver}"
  sed -i 's|^\(#!.*python\)$|\12|g' python/docs/examples/*

  ./configure --prefix=/usr --disable-static --with-python --without-php --without-ruby --without-tcl --without-csharp --without-java --without-perl PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/xapian-bindings-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
