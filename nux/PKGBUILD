# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
_actual_ver=4.0.0
_extra_ver=daily13.01.18
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=100.${_ubuntu_rel}
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite' 'ibus' 'libxdamage' 'libxxf86vm' 'geis')
makedepends=('boost' 'glproto' 'dri2proto' 'doxygen' 'graphviz')

groups=('unity')
options=('!libtool' '!emptydirs')

install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch')
sha512sums=('29e7f4f3ceca33af2f88e906b61d4d54314c9e37c6cbbe3c8014e1e8e7a51a422cc36337aa627e64f3671ee82ce71b2eb9f8989018f29953fe1a15ed10fb0ffd'
            '891809ee3ab5ba2a5b6e8e11d12bd4425e3adfb3899c55cd2482b038dbd18d8f7e28c0e62a187ca6b996e5f60bc43131afcfb187b8a70f1504e1757820b08eeb'
            'e0a3d3d9cf7d4ba7da541a6383e5c545b37ce4099c1e7b716b18c47e68f0068d522ab06658f759df9c717503a46f0ca946ef2f256264c5c3fbb03fc7635038e2')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
