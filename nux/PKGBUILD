# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
pkgver=2.12.0
pkgrel=100
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite' 'ibus' 'libxdamage' 'libxxf86vm' 'utouch-geis')
makedepends=('boost' 'glproto' 'dri2proto' 'gtest' 'gmock' 'doxygen' 'graphviz')
groups=('unity')
options=('!libtool' '!emptydirs')

# Do not use ccache or distcc
BUILDENV=(fakeroot !distcc color !ccache check !sign)

install=${pkgname}.install
source=("http://launchpad.net/nux/${pkgver%%.*}.0/${pkgver%.*}/+download/nux-${pkgver}.tar.gz")
sha512sums=('d001fd92775c91d73edb4c2d0c6cceddfae66601e4a13212dd896832e3403d4fef1ac2cbdfd81fae5ecd16f9c5ee3682fe770760c53d16ba4fe3f484129f8482')

# GPG public key:
#   gpg --keyserver keyserver.ubuntu.com --recv-keys 90EFF32C
case ${CARCH} in
i686)
  # GPG signature: http://ompldr.org/vZGpmbw/gcc46-4.6.3-1-i686.pkg.tar.xz.asc
  source+=('http://ompldr.org/vZGpmYw/gcc46-4.6.3-1-i686.pkg.tar.xz')
  sha512sums+=('bf9c42d0c0ef88793c5d3ab502d61876e0094055c10b28aff843097c206cfb7abda893ce2131fcbfe709277647b05e2f20da76d9d13611eb4ee979c0393bafae')
  ;;
x86_64)
  # GPG signature: http://ompldr.org/vZGpmdg/gcc46-4.6.3-1-x86_64.pkg.tar.xz.asc
  source+=('http://ompldr.org/vZGpmdA/gcc46-4.6.3-1-x86_64.pkg.tar.xz')
  sha512sums+=('39e8bda36d1422472d52f820ebc600627c104afa461d47f2b2f94b889fcc3d7f604abd25887ef123b120e2933a9b4cf00b5dd601b34d6610daf55b9c891e38e2')
  ;;
esac

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Ensure that gcc 4.6.3 is used
  PATH="${srcdir}/usr/bin:/usr/bin:/bin"
  case ${CARCH} in
    x86_64)
      CC="${srcdir}/usr/bin/x86_64-unknown-linux-gnu-gcc-4.6"
      CXX="${srcdir}/usr/bin/x86_64-unknown-linux-gnu-g++-4.6"
      ;;
    i686)
      CC="${srcdir}/usr/bin/i686-pc-linux-gnu-gcc-4.6"
      CXX="${srcdir}/usr/bin/i686-pc-linux-gnu-g++-4.6"
      ;;
  esac

  CPP="${srcdir}/usr/bin/cpp-4.6 -x c"
  CXXCPP="${srcdir}/usr/bin/cpp-4.6 -x c++"

  export PATH CC CXX CPP CXXCPP

  ./configure \
    --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  #MAKEFLAGS="-j1"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
