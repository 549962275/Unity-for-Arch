# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
_actual_ver=4.0.0
_extra_ver=daily13.01.11
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite' 'ibus' 'libxdamage' 'libxxf86vm' 'geis')
makedepends=('boost' 'glproto' 'dri2proto' 'doxygen' 'graphviz')

groups=('unity')
options=('!libtool' '!emptydirs')

install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/nux_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001_autoconf.patch')
sha512sums=('7d1cd695b0f4efa67c4f98f3c08f59b002abd6be073a915694d95cbde10a9e06dbef1f0af6e3c53676e8b908ae3d74b1dc8acb25bd108349e84b3437c2a11e9f'
            '511ed3d055e4eb94f62836db90025a0389bdaf2dac1beed684b579696f19f9bc5b300ea2354206a5896c25a2d99c4a57b230c8edaf209e24ddc4e443082c4f7f'
            'e0a3d3d9cf7d4ba7da541a6383e5c545b37ce4099c1e7b716b18c47e68f0068d522ab06658f759df9c717503a46f0ca946ef2f256264c5c3fbb03fc7635038e2')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
