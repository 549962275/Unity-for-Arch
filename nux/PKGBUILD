# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
pkgver=3.0.0
pkgrel=100
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite' 'ibus' 'libxdamage' 'libxxf86vm' 'geis')
makedepends=('boost' 'glproto' 'dri2proto' 'gtest' 'gmock' 'doxygen' 'graphviz')

# glew 1.7.0 dependencies
glew_depends=('libxmu' 'libxi' 'mesa')
depends=(${depends[@]} ${glew_depends[@]})

groups=('unity')
options=('!libtool' '!emptydirs')

# Do not use ccache or distcc
BUILDENV=(fakeroot !distcc color !ccache check !sign)

install=${pkgname}.install
source=("http://launchpad.net/nux/3.0/3.0/+download/nux_${pkgver}.orig.tar.gz"
        '0001_geis.patch'
        '0002_disable_documentation.patch'
        'http://downloads.sourceforge.net/project/glew/glew/1.7.0/glew-1.7.0.tgz')
sha512sums=('04131393f87d387c48e6da10358a5b3bcd8aa119ae0bfe5ab8eaeb3dd0135f6639b4172a09cc4823e787b917a2d75185402f52b8e9db412199a6879b1755854f'
            '4c367124f8c2548f6c961c7a31057a8b850c48577c57f8bb6c4e6b929a629284cbb328c8ff8ddfed14f6f7660a00750493af5708d3e7ed14b71b56fbcee0b40d'
            'd8e12b012c68e2726d88470cbd3f3c04e441db3ab3a625212347d2861f600d0f8bbb46a326dcac95f40d314cb57782a15bf5c95c1b9b56b60fb9b876f01781b5'
            '03d7a816fde0c445c964280ac9e679a0d2dfca839e87345360adec4fdb2292b4ddfc85538954b052c538ca355e559d8ee3a5ea7ea2a99130687054a92e0df857')
noextract=('glew-1.7.0.tgz')

# GPG public key:
#   gpg --keyserver keyserver.ubuntu.com --recv-keys 90EFF32C
case ${CARCH} in
i686)
  # GPG signature: http://ompldr.org/vZXFsZA/gcc46-4.6.3-1-i686.pkg.tar.xz.asc
  source+=('http://ompldr.org/vZXFsYw/gcc46-4.6.3-1-i686.pkg.tar.xz')
  sha512sums+=('a715da56bce0cf39b28cd55d6f7d6be07324400fbb4b550e289ebf301820f2f006f68149129fd6d2842268e698d053b4d40799d2f066a47a32913db2a065de57')
  ;;
x86_64)
  # GPG signature: http://ompldr.org/vZXFsZg/gcc46-4.6.3-1-x86_64.pkg.tar.xz.asc
  source+=('http://ompldr.org/vZXFsaA/gcc46-4.6.3-1-x86_64.pkg.tar.xz')
  sha512sums+=('a707fd950273140355bad5af8ca5da7c4b6c8f69ccaa6945d427f8fc1104ef308bdfbd32b46e65a62b06576ff7cecf22182579888767d6cb33d4133f2f37c4b0')
  ;;
esac

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Ensure that gcc 4.6.3 is used
  PATH="${srcdir}/usr/bin:/usr/bin:/bin"
  case ${CARCH} in
    x86_64)
      CC="${srcdir}/usr/bin/x86_64-unknown-linux-gnu-gcc-4.6"
      CXX="${srcdir}/usr/bin/x86_64-unknown-linux-gnu-g++-4.6"
      ;;
    i686)
      CC="${srcdir}/usr/bin/i686-pc-linux-gnu-gcc-4.6"
      CXX="${srcdir}/usr/bin/i686-pc-linux-gnu-g++-4.6"
      ;;
  esac

  CPP="${srcdir}/usr/bin/cpp-4.6 -x c"
  CXXCPP="${srcdir}/usr/bin/cpp-4.6 -x c++"

  export PATH CC CXX CPP CXXCPP

  # utouch-geis was renamed to geis upstream
  patch -Np1 -i "${srcdir}/0001_geis.patch"

  # The documentation files causes autoreconf to fail (docs aren't packaged in
  # Ubuntu anyway)
  patch -Np1 -i "${srcdir}/0002_disable_documentation.patch"

  # Build glew 1.7.0. It does not need to be installed as libraries built
  # with glew 1.7.0 will work with glew 1.8.0.
  [ -d "${srcdir}/glew_install" ] && rm -rv "${srcdir}/glew_install"
  [ -d glew-1.7.0 ] && rm -rv glew-1.7.0
  tar zxvf "${srcdir}/glew-1.7.0.tgz"
  pushd glew-1.7.0
  sed -i 's/lib64/lib/g' config/Makefile.linux
  make install.all GLEW_DEST="${srcdir}/glew_install" ${MAKEFLAGS}
  popd

  if [ -z "${PKG_CONFIG_PATH}" ]; then
    export PKG_CONFIG_PATH="${srcdir}/glew_install/lib/pkgconfig:/usr/lib/pkgconfig"
  else
    export PKG_CONFIG_PATH="${srcdir}/glew_install/lib/pkgconfig:${PKG_CONFIG_PATH}"
  fi

  cp NuxGraphics/GLResource.h{,.orig}
  sed -i \
    -e "s|GL/glew.h|${srcdir}/glew_install/include/GL/glew.h|g" \
    -e "s|GL/wglew.h|${srcdir}/glew_install/include/GL/wglew.h|g" \
    -e "s|GL/glxew.h|${srcdir}/glew_install/include/GL/glxew.h|g" \
    -e "s|<GL/glew.h>|\"${srcdir}/glew_install/include/GL/glew.h\"|g" \
    NuxGraphics/GLResource.h \
    "${srcdir}/glew_install/include/GL/glxew.h"

  # For Unity to compile
  sed -i \
    -e 's|"GL/glew.h"|<GL/glew.h>|g' \
    -e 's|"GL/glxew.h"|<GL/glxew.h>|g' \
    NuxGraphics/GLResource.h.orig

  autoreconf -vfi

  ./configure \
    --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cp NuxGraphics/GLResource.h.orig \
    "${pkgdir}/usr/include/Nux-3.0/NuxGraphics/GLResource.h"
}

# vim:set ts=2 sw=2 et:
