# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=nux
pkgver=3.8.0
pkgrel=100
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite' 'ibus' 'libxdamage' 'libxxf86vm' 'geis')
makedepends=('boost' 'glproto' 'dri2proto' 'gtest' 'gmock' 'doxygen' 'graphviz')

groups=('unity')
options=('!libtool' '!emptydirs')

install=${pkgname}.install
source=("http://launchpad.net/nux/3.0/${pkgver%.*}/+download/nux-${pkgver}.tar.gz"
        '0002_disable_documentation.patch'
        '0003_Glew_1.8.0_fix.patch')
sha512sums=('e99e6c35ff735154c4d241263139aa6270d44aae1d80276a441e546452005a61ae13e88ed3c5ea8f2211fdbe88cb1480ba878354d54fdcf3468444dcca58981b'
            'd8e12b012c68e2726d88470cbd3f3c04e441db3ab3a625212347d2861f600d0f8bbb46a326dcac95f40d314cb57782a15bf5c95c1b9b56b60fb9b876f01781b5'
            '60a6fd75896af92ba6a511968e35b16697da39416877b611323e4b57f70ed6bec81ba3a0467f175dcb30aaefb8d238943f59fc57c5d462047492078bd545a90a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0002_disable_documentation.patch"

  # Fix glew 1.8.0 headers and build with them
  [ -d glew-headers/ ] && rm -rv glew-headers/
  mkdir glew-headers/
  cp /usr/include/GL/glxew.h glew-headers/
  patch -Np1 -i "${srcdir}/0003_Glew_1.8.0_fix.patch"

  cp NuxGraphics/GLResource.h{,.orig}
  sed -i \
    -e "s|GL/glxew.h|$(pwd)/glew-headers/glxew.h|g" \
    NuxGraphics/GLResource.h

  # For Unity to compile
  sed -i -e 's|"GL/glxew.h"|"/usr/include/GL/glxew-fixed.h"|g' \
    NuxGraphics/GLResource.h.orig

  autoreconf -vfi

  ./configure \
    --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cp NuxGraphics/GLResource.h.orig \
    "${pkgdir}/usr/include/Nux-3.0/NuxGraphics/GLResource.h"

  # Install patched glxew.h
  install -dm755 "${pkgdir}/usr/include/GL/"
  install -m644 ./glew-headers/glxew.h "${pkgdir}/usr/include/GL/glxew-fixed.h"
}

# vim:set ts=2 sw=2 et:
