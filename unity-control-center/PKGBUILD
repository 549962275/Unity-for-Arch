# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Arch's gnome-control-center package

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial

pkgname=unity-control-center
_actual_ver=15.04.0
_extra_ver=+16.04.20160119
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="The Control Center for Unity"
arch=(i686 x86_64)
groups=(unity)
url="https://launchpad.net/unity-control-center"
license=(GPL)
depends=(accountsservice cups-pk-helper fcitx libnotify gnome-menus
         gsettings-desktop-schemas gtk3-ubuntu libgtop libtimezonemap
         network-manager-applet sound-theme-freedesktop upower
         libpwquality gnome-color-manager smbclient libmm-glib libgnomekbd
         grilo clutter-gtk unity-settings-daemon system-config-printer
         webkitgtk libgeonames)
makedepends=(gnome-common gnome-doc-utils intltool docbook-xsl modemmanager
             ttf-ubuntu-font-family vala)
optdepends=('gnome-control-center: For online accounts & region panel')
conflicts=(upower-compat)
options=(!emptydirs)
install=unity-control-center.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-control-center_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
        0002-Skip-region-panel.patch
        0003-Remove-Ubuntu-specific-language-code.patch
        0004-Remove-Ubuntu-help-links.patch
        0005-Allow-gnome-control-center-to-run-at-the-same-time.patch
        0006-Remove-overlay-scrollbar-from-GTK_MODULES.patch
        0007-Rename-libgnome-bluetooth-to-libunity-bluetooth.patch
        0008-Move-faces-images-usr-share-pixmaps-faces-unity.patch
        system-config-printer-unity.desktop
        unity-online-accounts-panel.desktop
        unity-region-panel.desktop)
sha512sums=('6fc7366ae256a24590d6e84af332cea461ed50ce22e51a61558153ad5314bb92e84d3191bca3b83e941d42f50f8527a3d740b3a04f14e4d6b30685722a694e01'
            '1bca29b181c409c3d958399a2d71453c1eaaf06aeb73f37c58622dc15ed34cf92f3ec2ef7e1156acd55aa8677e1b3636ef0bdfe9c6dad0a1b80a01f995b0b147'
            '5c0826789392e14e48c9cead7484cd67e4b41954e46a75cd1e21c9bf94e8665f60afdd105975efad06cf54010393e03b025d26e8eb1026e24f0e2b774eca33fd'
            '44aad84f62ed70b7142fa191248df1cb3e1e2fb742458f7ee961bf982ef6365852ee5b7e07e01eb6ddb5a35e8c11fcd65d5788803d2283519058597bfca71e6e'
            '4322e5696b8c60cc29bd08b1bd46e9538d0daa1b25c656436d3c4ee1a0fd237de6fe4c611d9034c5faac8b109459a0b94e8fd1add8ff81503a7bd4a758a2412a'
            '9cfdc52a53d53c831b0788eab2d2f3304b4de7ffa4af47bb876721ce107909c8d82fe176946d66c6039b2040d20c8bd74b62123f4d6417117f46d00e57d61972'
            'ba67144b7dc006303b759d3e497472e81acd967dd8ca68a837aad001f796d4b81dd67b95be01904c84485184625d5e1c78f00c03913846f0952d99aa4fcabe57'
            '5e41cf141402c018454f2d7ca20bce483735d50465d75b0d5b884c631b7a3c3183c2c311b8d6fe2608f1d92c5bad0ec9cc6d3716eab7ab4709239a7920be63e2'
            '6e92195120dc061ac8c63c237be7c7cf667c186885879da5c8d655375e8d51f16edd6ba269de9a4257ab918d5f00449dd19883a09e19ed14026992913024bfe0'
            '863e47afa95c8b16c8824c311c2b9d97b29f53153ee85a12989c63bbee4abb2e2626ef2d85988ac9d279baa60edcb32b65582acf4541f56fbc7efc22e2565de0'
            '287bf010b9a6d9424e2f07f7271cd27fd9a0fb9e29ea704dbb211a67d23558c66c0c654ee2a23456cba69bee725e962fbe79713cc0d2ad0ba4d830b0053777e7'
            '1378c3c15cb8c60575a55c86d400878f021c35adc87618c219496fee086b3c1286d7a46051747312960806330b6bb911a0c3e33814acedb2d83a1ef57d5e7a3a')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    patch -p1 -i ../0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
    patch -p1 -i ../0002-Skip-region-panel.patch
    patch -p1 -i ../0003-Remove-Ubuntu-specific-language-code.patch
    patch -p1 -i ../0004-Remove-Ubuntu-help-links.patch
    patch -p1 -i ../0005-Allow-gnome-control-center-to-run-at-the-same-time.patch
    patch -p1 -i ../0006-Remove-overlay-scrollbar-from-GTK_MODULES.patch
    patch -p1 -i ../0007-Rename-libgnome-bluetooth-to-libunity-bluetooth.patch
    patch -p1 -i ../0008-Move-faces-images-usr-share-pixmaps-faces-unity.patch
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --enable-systemd \
        --enable-fcitx

    # https://bugzilla.gnome.org/show_bug.cgi?id=656229
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}" install

    # Fix a warning
    chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
    chmod 750 "${pkgdir}/usr/share/polkit-1/rules.d/"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"

    panels/info/logo-generator \
        --logo panels/info/UbuntuLogoBlank.png \
        --text "Unity for Arch" \
        --output "${pkgdir}/usr/share/unity-control-center/ui/UbuntuLogo.png"

    # We'll just ship another desktop file for system-config-printer instead of
    # providing a patched package
    install -m644 "${srcdir}/system-config-printer-unity.desktop" \
                  "${pkgdir}/usr/share/applications/"
    install -m644 "${srcdir}/unity-online-accounts-panel.desktop" \
                  "${pkgdir}/usr/share/applications/"
    install -m644 "${srcdir}/unity-region-panel.desktop" \
                  "${pkgdir}/usr/share/applications/"
}
