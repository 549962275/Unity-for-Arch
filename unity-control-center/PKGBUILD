# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Arch's gnome-control-center package

pkgname=unity-control-center
_actual_ver=14.10.0
_extra_ver=+14.10.20140604
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="The Control Center for Unity"
arch=('i686' 'x86_64')
groups=('unity')
url="https://launchpad.net/unity-control-center"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'libnotify' 'gnome-desktop-compat' 'gnome-menus' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'libtimezonemap' 'network-manager-applet' 'sound-theme-freedesktop' 'upower-compat' 'libpwquality' 'gnome-color-manager' 'smbclient' 'libmm-glib' 'libgnomekbd' 'grilo' 'clutter-gtk' 'unity-settings-daemon' 'system-config-printer' 'webkitgtk')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'modemmanager')
options=('!emptydirs')
install=unity-control-center.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-control-center_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Use-gnome-desktop-compat.patch'
        '0002-Skip-bluetooth-panel.patch'
        '0003-Remove-Ubuntu-specific-language-code.patch'
        '0004-Remove-Ubuntu-help-links.patch'
        '0005-Allow-gnome-control-center-to-run-at-the-same-time.patch'
        '0006-Use-upower-compat.patch')
sha512sums=('fb89795a618f00af029e2458ea7f7590cedd967a52ffb608999c1fbcd928ae3db5d4bd110074019728f2cf3fb7ddb3085aae1f606b5e52d7e3bf33e146c9a2dd'
            'ebd452cdd41fc5d77b3a933e65198afc298fa9f3459a3ec5d3667a1a1a094385922e2247f656257a0c2d675dfe36ba3a26d09c15ae4be064ba307ec16c619fcf'
            '79cc29781661c99c87eca41b5cfcd9ec8b9235bec29c53b7185a99a20af58f76258bf05db8eb44748c0157a7c59836159548b8e8f341b7565f1fe760c9bdbabe'
            'daf787cb63033d6f6489157b202b42106136a0b827db9f4dd26dad3cd7f150f4019f8f6ec09aca055f7b1376fa34dc6af5ebb767b4a8768c78126045b33119d4'
            '404bf3df6609c7e4a2e105729b25e544bdfef181a404ce77435e769e0b74d3b5a0b4d3c27415b028e55a4610f513a4ccc68d97af9e16825bac878e4806132c87'
            '889c7bfe64a29fba11fb1f6318ae8ed406ee8db88b4d8e03a9c132069c361ade1355817285e78bfeed82e27f36926beda665d62a5d0a3c697be3e71e9e5fd366'
            'ba19ca83a00ec13139d1fd8a3f13cce1fa8cb798b7db1f3b9a80aae26512ba8328fa49e2a3f4866b490ab73b46acacbfac542f16c7c26af168b54242c2f76c4e')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i "${srcdir}/0001-Use-gnome-desktop-compat.patch"
  patch -p1 -i "${srcdir}/0002-Skip-bluetooth-panel.patch"
  patch -p1 -i "${srcdir}/0003-Remove-Ubuntu-specific-language-code.patch"
  patch -p1 -i "${srcdir}/0004-Remove-Ubuntu-help-links.patch"
  patch -p1 -i "${srcdir}/0005-Allow-gnome-control-center-to-run-at-the-same-time.patch"
  patch -p1 -i "${srcdir}/0006-Use-upower-compat.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
#   chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"

  panels/info/logo-generator \
    --logo panels/info/UbuntuLogoBlank.png \
    --text "Unity for Arch" \
    --output "${pkgdir}/usr/share/unity-control-center/ui/UbuntuLogo.png"
}

# vim:set ts=2 sw=2 et:
