# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Arch's gnome-control-center package

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic

pkgname=unity-control-center
_actual_ver=14.10.0
_extra_ver=+14.10.20140922
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
pkgdesc="The Control Center for Unity"
arch=(i686 x86_64)
groups=(unity)
url="https://launchpad.net/unity-control-center"
license=(GPL)
depends=(accountsservice cups-pk-helper libnotify gnome-menus
         gsettings-desktop-schemas gtk3-ubuntu libgtop libtimezonemap
         network-manager-applet sound-theme-freedesktop upower-compat
         libpwquality gnome-color-manager smbclient libmm-glib libgnomekbd
         grilo clutter-gtk unity-settings-daemon system-config-printer
         webkitgtk)
makedepends=(gnome-common gnome-doc-utils intltool docbook-xsl modemmanager
             vala)
optdepends=('gnome-control-center: For bluetooth panel')
options=(!emptydirs)
install=unity-control-center.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-control-center_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
        0002-Skip-bluetooth-panel.patch
        0003-Remove-Ubuntu-specific-language-code.patch
        0004-Remove-Ubuntu-help-links.patch
        0005-Allow-gnome-control-center-to-run-at-the-same-time.patch
        0006-Use-upower-compat.patch
        0007-Remove-overlay-scrollbar-from-GTK_MODULES.patch
        system-config-printer-unity.desktop
        unity-bluetooth-panel.desktop)
sha512sums=('77119e01c2e4115680e6080c7c4e063e4c9a311507f9f8244ef3b4bf457e73815859574f5616b3928f166153bbc997d043979adeb0b7c8a626a621bc0ff41efb'
            'baff5349e5ea08a2970f5baac308052ac2f90636ce8b56193be762603a2fb26e553c750c53baf439b238ea91123a37efdfc8e319021ff1309f30a597c8e3516e'
            '5e20922fdc28acf90757743fdbb93715cade67a93a135ffcefd27dbadab11e60fb3d5848be7240b267ec2ea24e415a3f0bef3461e4c479a63e6adf3adba6bb51'
            '753deaaf6fcf71998d2f9389bb34672cc60e96099b5d04e3e2eebc5e9af6f50e0d349e5202c17203617d430d1fe56bf9a6215bae417f198519e0f43efef292a5'
            '20bf406e5966269f669c50b8fd8a72bf0329089871a9b5267dcce460b8aca32cdb57ede944f38aeb2ae036e7ed55596f4aba1a32a9c7d35a8ee2c64801f338c8'
            'b42154629eb7ab34bbab83eabd1f225cceac63d029d5a3659313a4f37d2a76e99bc627c8a6915d76b25931a256148bb0277884dfe6c5d09eca0ea7fa49f13b3b'
            '6a741e4654abf78725b49a1cc62fbe803e541fba7bab7c2a959bdcebc5b2a47faafbb3078e590c689b7a5120abfa68f8e96606e5b5fe0d38cc79bde6ed7c406f'
            'cfe96957cec333e171b5ecb34116765e3214534470646c2c541343270900c17bdfedf2df88f3c8536e483ce5375abc02d95dfbef0c7ba773c7c73911b4572d00'
            '863e47afa95c8b16c8824c311c2b9d97b29f53153ee85a12989c63bbee4abb2e2626ef2d85988ac9d279baa60edcb32b65582acf4541f56fbc7efc22e2565de0'
            '1118e18b0df7bfaebfdd0f39ff88a44e6e296068562b5a980f5b0de7f750ca6843c812945abb75365df01214ee141c49a64bfcceb5a98f60d5c88808f7cfc16e')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i ../0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
  patch -p1 -i ../0002-Skip-bluetooth-panel.patch
  patch -p1 -i ../0003-Remove-Ubuntu-specific-language-code.patch
  patch -p1 -i ../0004-Remove-Ubuntu-help-links.patch
  patch -p1 -i ../0005-Allow-gnome-control-center-to-run-at-the-same-time.patch
  patch -p1 -i ../0006-Use-upower-compat.patch
  patch -p1 -i ../0007-Remove-overlay-scrollbar-from-GTK_MODULES.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
#   chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"

  panels/info/logo-generator \
    --logo panels/info/UbuntuLogoBlank.png \
    --text "Unity for Arch" \
    --output "${pkgdir}/usr/share/unity-control-center/ui/UbuntuLogo.png"

  # We'll just ship another desktop file for system-config-printer instead of
  # providing a patched package
  install -m644 "${srcdir}/system-config-printer-unity.desktop" \
                "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/unity-bluetooth-panel.desktop" \
                "${pkgdir}/usr/share/applications/"
}
