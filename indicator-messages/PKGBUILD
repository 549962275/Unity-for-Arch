# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-messages
_actual_ver=12.10.6
_extra_ver=daily13.02.13
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libdbusmenu-gtk3' 'libindicator3' 'telepathy-glib')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool')
groups=('unity')
options=('!libtool')
install=indicator-messages.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-messages_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch'
        'revert_r335.patch')
sha512sums=('60c7905c0ecf7646a6681f2e2a8bf9b50d9e97e516482653eb7e9c013e9d949714845c2f4306dd6fbe2322fec02d4be0b193a05be8c799a6bb15a5420d4c77b3'
            '75c5f0c2381b76b053ad980bc371772cbb6626ef5c7e01a3b212d42460ed8a5ea17c82ab8b6146992aca098c3a465b0b1075e460d2d5333ef6de193e5cf73e8a'
            'dce4fb905aca4e1b9c2e75d50addc57268500b3f4f013ebd1c3c07f6ee5209fcd1026803eadba6ac5e685cc65caeb6815e34630dc4d97d791c77195f1e38173c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
  patch -p0 -i "${srcdir}/revert_r335.patch"

  gtkdocize
  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
