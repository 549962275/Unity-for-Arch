# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-messages
pkgname=('indicator-messages' 'indicator-messages-gtk2')
_ubuntu_rel=0ubuntu1
pkgver=0.5.94.${_ubuntu_rel}
pkgrel=100
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libindicator3' 'libindicate' 'libdbusmenu-gtk3' 'telepathy-glib')
makedepends=('intltool>=0.35.0')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/0.6/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('33c0ee7a6fbd37e4e940543c62ec6da9ff089b366f3c2cba95b88c0fd140fb8e4b4aacffd47aef21a527aad3b7f09793257f94aab468ae26c12ec7118e5af233'
            'fd466a6d53e74f3b0321913680580db8481fc466011bfb4d6d25e528b4ab292eb71a50e67b927171e8cc3a8740f1921b7a2c1ddd9b43db599550b1aa09d064ad')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-messages() {
  pkgdesc="Indicator that collects messages that need a response - GTK 3 library"
  depends=('libdbusmenu-gtk3' 'gtk3-ubuntu' 'libindicator3' 'libindicate')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-messages-gtk2() {
  pkgdesc="Indicator that collects messages that need a response - GTK 2 library"
  depends=('libdbusmenu-gtk2' 'gtk2-ubuntu' 'libindicator' 'indicator-messages')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  INDICATOR_VERSION=$(ls /usr/lib | grep libindicator | grep 'libindicator.so.[0-9]$' | awk -F'.' '{print $NF}')

  install -dm755 "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
  install -m755 'src/.libs/libmessaging.so' "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
}
