# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-messages
_actual_ver=12.10.6
_extra_ver=daily12.11.22
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libdbusmenu-gtk3' 'libindicator3' 'libindicate' 'telepathy-glib')
makedepends=('intltool')
groups=('unity')
options=('!libtool')
install=indicator-messages.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-messages_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('87028005f193c3c90e981a591daa3efec5f3b7671fe04732f706710264b8d57b0ada430f1d6735a4f413fed5b1710330e91fc0c883bef5532a4bc3482b2a88fe'
            '75c5f0c2381b76b053ad980bc371772cbb6626ef5c7e01a3b212d42460ed8a5ea17c82ab8b6146992aca098c3a465b0b1075e460d2d5333ef6de193e5cf73e8a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  gtkdocize
  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
