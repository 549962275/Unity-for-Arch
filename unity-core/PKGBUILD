# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-core
pkgver=5.4.0
pkgrel=100
pkgdesc="Unity core library and services"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('qt' 'nux' 'libunity')
makedepends=('cmake')
groups=('unity')
source=("http://launchpad.net/unity/${pkgver%%.*}.0/${pkgver}/+download/unity_${pkgver}.orig.tar.bz2"
        'launcher_bfb.png')
sha512sums=('0158892af2d23e4c120dd5fb83745dbd6b117a47fdda2a982c5e0ad41bef8243caf2121f5a8125d033138b2a1fe531c054049b4667e3373dcd142d48b480a6c7'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054')

build() {
  cd "${srcdir}/unity-${pkgver}"

  # Only build the core
  sed -i \
    -e '/add_subdirectory.*tests/d' \
    -e '/add_subdirectory.*tools/d' \
    -e '/add_subdirectory.*services/d' \
    -e '/add_subdirectory.*guides/d' \
    -e '/add_subdirectory.*doc/d' \
    -e '/add_subdirectory.*standalone-clients/d' \
    -e '/add_subdirectory.*plugins\/unityshell/d' \
    -e '/add_subdirectory.*plugins\/gtkloader/d' \
    -e '/add_subdirectory.*plugins\/networkarearegion/d' \
    -e '/add_subdirectory.*plugins\/unitydialog/d' \
    -e '/add_subdirectory.*plugins\/unity-mt-grab-handles/d' \
    -e 's/compiz;//g' \
    CMakeLists.txt

  # Fix pkgconfig
  sed -i -e 's/\${libdir}/@LIBDIR@/g' -e 's/\${includedir}/@INCLUDEDIR@/g' UnityCore/unity-core.pc.cmake

  mkdir build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGSETTINGS_LOCALINSTALL=1

  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/unity-${pkgver}/build"
  make DESTDIR="${pkgdir}/" install
  cd '../plugins/unityshell/resources'
  install -dm755 "${pkgdir}/usr/share/unity/5/"
  install -m644 *.* -t "${pkgdir}/usr/share/unity/5/"
  install -m644 "${srcdir}/launcher_bfb.png" "${pkgdir}/usr/share/unity/5/launcher_bfb.png"
  rm -rvf "${pkgdir}/usr/share/locale/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}
