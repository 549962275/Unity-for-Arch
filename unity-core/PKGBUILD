# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Marius Dransfeld <marius.dransfeld@googlemail.com>
# Contributor: thn81 <root@scrat>

pkgname=unity-core
pkgver=5.12.0
pkgrel=100
pkgdesc="Unity core library and services"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('nux')
makedepends=('cmake')
groups=('unity')
source=("http://launchpad.net/unity/${pkgver%%.*}.0/${pkgver}/+download/unity-${pkgver}.tar.bz2"
        'launcher_bfb.png'
        'fix_gtest_directory.patch'
        'gcc_4.7_fix.patch')
sha512sums=('0960c909980b9657cba9c2428ad5ed3dbdd8125c81cb88bf448073e5861d704b1e9c64d4d05b29d52b72caa84a04778581b38ef8fa0ab748e417c902a51aeb6c'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054'
            '5a7396d02e532eaa3fe57a03646274d716c53760967f0d8caeb323e451622a1ccf8f92c71c4b40c5c602016ae62d9d5df314d9015943586149b0432a1515d6fc'
            'fe64519131c45669e7e7ab2fc71e69470b41169e12c717d2826d243d3870bbfbe6aabdcb9e6ee7e9fda7c692962dc836924031948784e6c17bc2ecb591f97696')

build() {
  cd "${srcdir}/unity-${pkgver}"

  # Only build the core
  sed -i \
    -e '/add_subdirectory.*tests/d' \
    -e '/add_subdirectory.*tools/d' \
    -e '/add_subdirectory.*services/d' \
    -e '/add_subdirectory.*guides/d' \
    -e '/add_subdirectory.*doc/d' \
    -e '/add_subdirectory.*standalone-clients/d' \
    -e '/add_subdirectory.*plugins\/unityshell/d' \
    -e '/add_subdirectory.*plugins\/gtkloader/d' \
    -e '/add_subdirectory.*plugins\/networkarearegion/d' \
    -e '/add_subdirectory.*plugins\/unitydialog/d' \
    -e '/add_subdirectory.*plugins\/unity-mt-grab-handles/d' \
    -e 's/compiz;//g' \
    CMakeLists.txt

  # Fix gtest directory
  patch -Np1 -i "${srcdir}/fix_gtest_directory.patch"

  # Fix pkgconfig
  sed -i -e 's/\${libdir}/@LIBDIR@/g' -e 's/\${includedir}/@INCLUDEDIR@/g' UnityCore/unity-core.pc.cmake

  # GCC 4.7 build fix
  patch -Np1 -i "${srcdir}/gcc_4.7_fix.patch"

  mkdir build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGSETTINGS_LOCALINSTALL=1

  make ${MAKEFLAGS}
}

package() { 
  cd "${srcdir}/unity-${pkgver}/build"
  make DESTDIR="${pkgdir}/" install
  cd '../plugins/unityshell/resources'
  install -dm755 "${pkgdir}/usr/share/unity/5/"
  install -m644 *.* -t "${pkgdir}/usr/share/unity/5/"
  install -m644 "${srcdir}/launcher_bfb.png" "${pkgdir}/usr/share/unity/5/launcher_bfb.png"
  rm -rvf "${pkgdir}/usr/share/locale/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

# vim:set ts=2 sw=2 et:
