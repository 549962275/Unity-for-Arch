# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=inputproto-ubuntu
_ubuntu_rel=2
pkgver=2.1.99.5.${_ubuntu_rel}
pkgrel=100
pkgdesc="X11 Input extension wire protocol"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('xorg-util-macros')
provides=("inputproto=${pkgver}")
conflicts=('inputproto')
source=("${url}/releases/individual/proto/${pkgname%-*}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/x11proto-input_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('5ee2d7883d9104b91f4ca4aeee8ec8400b3e8208e1c23fbed9887fe30b8aaa3fcde3eba85b96c25c3388633ae2f93e639c13a890812e829ad033104f6e0b8b9b'
            'bc28f5d7c7d0f42b23eadf239f02ca7694033cd5cd75eb080264f22fe719a1ddd60e993e1a7debd85f3d117584c6bb19c0ecca96f5ec30583c73cbdffa323668')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/x11proto-input_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure --prefix=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
}
