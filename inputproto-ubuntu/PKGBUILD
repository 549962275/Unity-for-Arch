# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=inputproto-ubuntu
_ubuntu_rel=1
pkgver=2.1.99.5.${_ubuntu_rel}
pkgrel=100
pkgdesc="X11 Input extension wire protocol"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('xorg-util-macros')
provides=("inputproto=${pkgver}")
conflicts=('inputproto')
source=("${url}/releases/individual/proto/${pkgname%-*}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/x11proto-input_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('5ee2d7883d9104b91f4ca4aeee8ec8400b3e8208e1c23fbed9887fe30b8aaa3fcde3eba85b96c25c3388633ae2f93e639c13a890812e829ad033104f6e0b8b9b'
            'ebafcafe73d0d8331edc0d05f2ad85243b3cf4eb30d2022d80bba688916571b1387d155d03eddbdfd3074f6a5ecebedce450fb0d20201ef3ba6a9a24df10568a')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/x11proto-input_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure --prefix=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
}
