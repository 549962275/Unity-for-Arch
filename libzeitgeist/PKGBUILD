# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libzeitgeist
_ubuntu_rel=1ubuntu1
pkgver=0.3.18.${_ubuntu_rel}
pkgrel=100
pkgdesc="Provides a client library for applications that want to interact with the Zeitgeist daemon"
arch=('i686' 'x86_64')
url="https://launchpad.net/libzeitgeist"
license=('LGPL')
depends=('glib2')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/${pkgname}/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('6ad3f0ed74935c20ce23d80208c5a787e4db2e2d58ae5036c6681c3d531e20cc0981be961d66a408a7e1b03909af0212693feddc515240d724acbd5ac5c8cf6e'
            '2f20f648f9cbae741a835d479a4da2e8f736d8882a146b2e98d0940822ef10299486c35a4b7700c76aac3c8bedd71a53d706091993e62b4de15de8d508c7bd6a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
