# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xpathselect
_actual_ver=1.4
_extra_ver=+14.04.20140108
pkgver=${_actual_ver}daily14.01.08
pkgrel=1
pkgdesc="Select objects in an object tree using XPath queries"
arch=('i686' 'x86_64')
url="https://launchpad.net/xpathselect"
license=('GPL')
groups=('unity')
makedepends=('boost' 'cmake')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xpathselect_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Disable_Tests.patch')
sha512sums=('abb8d377782c500f1b08a5ca8a88380d2d84a60463fd06b9c900b055dac7955dc28dbd2ec9942ef731b28ecdb417b9bac136d2bf84e04860bb450401f080205b'
            '3bdee2bd7f89903ece07bb81848365d5e493895b72a58a874c285b51f795884b6d191bef8d6910479ba39b091bce1828575b8c81f20f113a56c2fc3683c94d15')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  if [ ! -d /usr/src/gtest/ ]; then
    patch -p1 -i "${srcdir}/0001_Disable_Tests.patch"
  fi
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}
