# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Christopher Reimer <github@creimer.net>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=gtk+3.0, repo=wily
# vercheck-archlinux: name=gtk3, repo=extra, arch=x86_64
# vercheck-gnome: name=gtk+, majorver=3.16
# vercheck-ppa: name=gtk+3.0, url=ppa:gnome3-team/gnome3-staging

_use_ppa=false

pkgname=gtk3-ubuntu
#_ppa_rel=0ubuntu1~vivid1
_ubuntu_rel=1ubuntu2
pkgver=3.16.5
pkgrel=1
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libepoxy libxcursor libxinerama
         libxrandr libxi libxcomposite libxdamage pango shared-mime-info
         colord at-spi2-atk wayland libxkbcommon adwaita-icon-theme
         json-glib rest)
makedepends=(gobject-introspection)
provides=("gtk3=${pkgver}")
conflicts=(gtk3)
license=(LGPL)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        settings.ini)

if [[ "${_use_ppa}" != "true" ]]; then
  source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
else
  source+=("http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gtk+3.0/gtk+3.0_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
fi

sha512sums=('f8dd318d5d72e8c59534bf49e505de3575f7519d57398030f4c47a576c3b37e4f00fec1b5a3ba2a3769e94373504dfb6ca24b1015f1e8f8f7dca6f46114b95f8'
            'ad2c0b0388f4169592b9574f0b3db673a969b7c4721548c4ac7c438eddbcdc378fcaac04e2b6c858a1562cc23ddf4804e5f7be08068340b7c9365e2b11ddcfb8'
            '68045b5aaf1b8ee3265667a50a7d5585b9b6f07523530bb5bb2c46b909d5e7accb6220ff316a0a312453643e989f8d793ee35786eee81d76e9f509883135d913')

prepare() {
    cd "gtk+-${pkgver}"

    # Apply Ubuntu Patches
    echo > ../debian/patches/series

    # https://bugs.launchpad.net/indicator-messages/+bug/1088162
    # Dependency check: grep -R ubuntu-private.h * | cut -d/ -f1
    echo 'ubuntu_gtk_custom_menu_items.patch' >> ../debian/patches/series
    # Do not allow offscreen widgets to grab the cursor
    echo '016_no_offscreen_widgets_grabbing.patch' >> ../debian/patches/series
    echo '017_no_offscreen_device_grabbing.patch' >> ../debian/patches/series
    # Allow printing to printers advertised using Avahi/Bonjour when CUPS 1.6
    echo 'print-dialog-show-options-of-remote-dnssd-printers.patch' >> ../debian/patches/series
    # Unity-specific
    #echo '0001-Add-style-classes-to-the-title-buttons.patch' >> ../debian/patches/series
    echo 'message-dialog-restore-traditional-look-on-unity.patch' >> ../debian/patches/series
    # Other
    echo 'bzg_gtkcellrenderer_grabbing_modifier.patch' >> ../debian/patches/series
    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done
}

build() {
    cd "gtk+-${pkgver}"

    autoreconf -vfi

    #CXX=/bin/false ./configure \
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-schemas-compile \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 ../settings.ini "${pkgdir}"/usr/share/gtk-3.0/settings.ini
    rm -f "${pkgdir}"/usr/bin/gtk-update-icon-cache
}
