# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu3
pkgver=3.2.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GTK+ Toolkit (v3) with Ubuntu's patches"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord')
makedepends=('gobject-introspection')
provides=("gtk3=${pkgver%.*}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/3.2/gtk+-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/gtk+3.0/gtk+3.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'settings.ini')
sha512sums=('e5b50edbb2b9b68880fd8c185f4f9bfdc62ba493e5df1af73d2db2416e87322a88258a477e3c34ce73f0e797eb2cfc7a9d342d93d00964c0a1d64264d136939c'
            '046613932578915e31449c0e57fca435ffbff72d41388778436daa1cb12ed6ecf6dd45bd74fcd61208115945c8abc50dcca34f7edb3625b9ff99e8a2a07e7b60'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*}"

  #Apply Ubuntu Patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  automake
  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-test-print-backend \
    --enable-xinput
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
