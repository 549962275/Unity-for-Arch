# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu0.3
pkgver=3.4.2.${_ubuntu_rel}
pkgrel=100
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'glib2>=2.31.0' 'dconf' 'pango>=1.30.0')
makedepends=('gobject-introspection>=1.32.0')
provides=("gtk3=${pkgver%.*.*}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*.*.*}/gtk+-${pkgver%.*.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${pkgver%.*.*}-${_ubuntu_rel}.debian.tar.gz"
        'settings.ini')
sha512sums=('b9adf9d40db013acd23b355244cac3ca19e8d688425fb2ae99b375ad84d7e49064ed4a3660a221bfdb82164b9f46fd1eae23855cee611389d75d1383971325d4'
            '995d51bdecf181e99b3d5eeacfbbe511761c32881f1a328577a99731e8d00b65e2297fc0fff58bf69ace1587698e1db893c1e2e4a1050e79f373e91f9f63f988'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*.*}"

  # Apply Ubuntu Patches
    # Disable patches
      # No multiarch
        sed -i '/061_multiarch_module_fallback.patch/d' "${srcdir}/debian/patches/series"
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gtk+-${pkgver%.*.*}"
  make mkdir_p="/bin/mkdir -p" DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
