# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=3.3.16.${_ubuntu_rel}
pkgrel=100
pkgdesc="GTK+ is a multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'glib2>=2.31.0')
makedepends=('gobject-introspection')
provides=("gtk3=${pkgver%.*}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*.*}/gtk+-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'settings.ini')
sha512sums=('b3d1a10ea501d62dd3213ca6c76eaf492675ad2b55906695b61d9f9cb13c50a70268cc2d1fe4a37d1dff485c50345429102825490f54b2389ce88b27796fbcdc'
            '49dc30066144325c428417e38d5a71ac556f527109d17d4433046d48b3b1a972cf808b3cd6e8647e14671209e17a407d277bec85a0b0a4901f9340e21f6e4af7'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*}"

  # Apply Ubuntu Patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-test-print-backend \
    --enable-xinput

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gtk+-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
