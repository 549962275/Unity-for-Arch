# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu4
pkgver=3.4.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'glib2>=2.31.0' 'dconf')
makedepends=('gobject-introspection')
provides=("gtk3=${pkgver%.*}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*.*}/gtk+-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'settings.ini')
sha512sums=('4f224bb73f469d6e7cb9b96d201d3bf8b417c442f5e039058664c1ef23e7ecfe6b1d05e81860d0df5cc8bb1187b7edfb9cd8cfb07b6866aeeffed84fdb34015f'
            '22933571f6c29fd13ba59c851e140a478ec01e2dd7fb5335b112304167c2a74a9b3b8632b0d49c691d944c6273184e0d7a22276b7a24563096a8dc4b13ce16ee'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*}"

  # Apply Ubuntu Patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  export CFLAGS+=" -g -O0"

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gtk+-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
