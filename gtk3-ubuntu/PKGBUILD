# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Christopher Reimer <github@creimer.net>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=gtk+3.0, repo=vivid
# vercheck-archlinux: name=gtk3, repo=extra, arch=x86_64
# vercheck-gnome: name=gtk+, majorver=3.14
# vercheck-ppa: name=gtk+3.0, url=ppa:gnome3-team/gnome3-staging

_use_ppa=false

pkgname=gtk3-ubuntu
#_ppa_rel=0ubuntu1~utopic3
#_ppa_ver=3.14.4
_ubuntu_rel=0ubuntu1
pkgver=3.14.7
pkgrel=1
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libxcursor libxinerama
         libxrandr libxi libxcomposite libxdamage pango shared-mime-info
         colord at-spi2-atk wayland libxkbcommon adwaita-icon-theme)
makedepends=(gobject-introspection python2)
provides=("gtk3=${pkgver}")
conflicts=(gtk3)
license=(LGPL)
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz")

if [[ "${_use_ppa}" != "true" ]]; then
  source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
else
  source+=("http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gtk+3.0/gtk+3.0_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
fi

sha512sums=('d3d8ac5b65666a53f7c905ff087f460f423532135b7d6cdb9818b414e878193b7395d2eb5ec11ef64dd5495695ca672c4bd91481df53c76b5a155cf7041d6bc7'
            '0bb748b10aa23a0642e220a932a35c00a20b2994e2a4507095480ed35f8369ee6e9eb4120a622e1bdc3f38cdaa8002132a67c6f5aad8bc9a7d29a4edea14cf92')

prepare() {
  cd "gtk+-${pkgver}"

  # Apply Ubuntu Patches
    echo > ../debian/patches/series
    # Enable patches
      # https://bugs.launchpad.net/indicator-messages/+bug/1088162
      # Dependency check: grep -R ubuntu-private.h * | cut -d/ -f1
        echo 'ubuntu_gtk_custom_menu_items.patch' >> ../debian/patches/series
      # Do not allow offscreen widgets to grab the cursor
        echo '016_no_offscreen_widgets_grabbing.patch' >> ../debian/patches/series
        echo '017_no_offscreen_device_grabbing.patch' >> ../debian/patches/series
      # Allow printing to printers advertised using Avahi/Bonjour when CUPS 1.6
        echo 'print-dialog-show-options-of-remote-dnssd-printers.patch' >> ../debian/patches/series
      # Other
        echo 'bzg_gtkcellrenderer_grabbing_modifier.patch' >> ../debian/patches/series
        echo '0001-Menu-items-force-loading-16x16-icons.patch' >> ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done
}

build() {
  cd "gtk+-${pkgver}"

  autoreconf -vfi

  #CXX=/bin/false ./configure \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --enable-broadway-backend \
    --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
