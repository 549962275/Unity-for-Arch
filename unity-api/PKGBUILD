# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety

pkgname=unity-api
_actual_ver=7.116
_extra_ver=+16.10.20160730
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="API for Unity shell integration"
arch=(i686 x86_64)
url="https://launchpad.net/unity-api"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(cmake doxygen graphviz python qt5-base)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('9970a0b2c8521223bb2a200865500c1798eebff848c29b791ac4d2bd1cedae2995d5857134bdbf9bc73301b382edb0c0017f6e53c7071f1e6025542e9c97967f')

build() {
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DNO_TESTS=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}/" install
}
