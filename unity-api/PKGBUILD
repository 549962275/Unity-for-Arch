# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-api
_actual_ver=7.105
_extra_ver=+16.04.20160104
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="API for Unity shell integration"
arch=(i686 x86_64)
url="https://launchpad.net/unity-api"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(cmake doxygen graphviz python qt5-base)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8d9f5e06b1573c6ada39deb33c32fd8dfb5ef1eed748f492303ec8a62e6ce4e37afd83d9a8ffdaecb89da42848e4c1c8f13ef2a418680b441bc7ab48194f7cb5')

build() {
	cd "${pkgname}-${_actual_ver}${_extra_ver}"
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DNO_TESTS=TRUE
	make
}

package() {
	cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
	make DESTDIR="${pkgdir}/" install
}
