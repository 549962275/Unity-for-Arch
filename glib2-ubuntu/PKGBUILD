# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=glib2-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=2.31.14.${_ubuntu_rel}
pkgrel=101
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pcre' 'libffi')
provides=("glib2=${pkgver%.*}")
conflicts=('glib2')
makedepends=('python2' 'pkgconfig')
options=('!libtool' '!docs' '!emptydirs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*.*}/glib-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/glib2.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'glib2.sh'
        'glib2.csh')
sha512sums=('5107f2d6070cb21f9219b4a1105fe554f9479365db7ff63f4ebd46b0ba57ad19ee0882b24f7945316e037ead8f2b803b3fb56a820148e63533b9167a0185358b'
            'de11bbdbdf9c5f99370cd7530f589d117c79a1237f85e33d3260549920235552af15f7f70ed37b1eacb245b6814b6d4ed81b04bd52a48d7953dd852a5b12ca77'
            'dca2bc74d2013fcb24145ac794eef457aa3213c039e40a1a26ca5017694930768e7c80e334e17a56834549dff6549c781ddd91fae6c7bbb26fdd6a083ad8217a'
            'c3899eb7fa5482ce8a35fe02db90fd0f928d50aa7e4365a9529ef35a2dcd1ed86d5a24f6bc5c635ef5b2d95a0ebfebc2bb6bc90404c99f6fb7484ed2fa032c06')

build() {
  cd "${srcdir}/glib-${pkgver%.*}"

  #Apply Ubuntu patches

  # Do not apply these patches
    # Multiarch
      sed -i '/gio-modules-multiarch-compat.patch/d' "${srcdir}/debian/patches/series"
    # Patches accepted upstream
      sed -i '/10_gvariant_check_null.patch/d' "${srcdir}/debian/patches/series"
    # Do not change path of glib-compile-schemas
      sed -i '/61_glib-compile-schemas-path.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pcre=system \
    --disable-fam
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/glib-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/"

  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
      chmod -x "${_i}"
  done
  sed -i "s|^\(#!.*python\)$|\12|" "${pkgdir}"/usr/bin/gdbus-codegen
}
