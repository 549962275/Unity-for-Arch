# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=glib2-ubuntu
_ubuntu_rel=1ubuntu3
pkgver=2.32.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pcre' 'libffi' 'python2' 'elfutils')
provides=("glib2=${pkgver%.*}")
conflicts=('glib2')
makedepends=('python2' 'pkgconfig')
options=('!libtool' '!docs' '!emptydirs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*.*}/glib-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/glib2.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'glib2.sh'
        'glib2.csh')
sha512sums=('6788b2bb9d35f566efd7bf02f05e4e56184fc11160cf1e08ef833dbd49314bc5ef74b7abf9257fa5fdf298148ab6aa6db11faa8b362e2114e31b08027bdff50f'
            'ea69f1cce84f1d210226e747c68cd549cf447556dc379576ef087bc570111ae6312c42f252f711b10e6bf9e953bf137394aa2222dff6a16c832a812e68bbfd7b'
            'dca2bc74d2013fcb24145ac794eef457aa3213c039e40a1a26ca5017694930768e7c80e334e17a56834549dff6549c781ddd91fae6c7bbb26fdd6a083ad8217a'
            'c3899eb7fa5482ce8a35fe02db90fd0f928d50aa7e4365a9529ef35a2dcd1ed86d5a24f6bc5c635ef5b2d95a0ebfebc2bb6bc90404c99f6fb7484ed2fa032c06')

build() {
  cd "${srcdir}/glib-${pkgver%.*}"

  # Apply Ubuntu patches

  # Do not apply these patches
    # Do not change path of glib-compile-schemas
      sed -i '/61_glib-compile-binaries-path.patch/d' "${srcdir}/debian/patches/series"
    # Debian/Ubuntu multiarch stuff
      sed -i '/90-gio-modules-multiarch-compat.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pcre=system \
    --disable-fam
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/glib-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/"

  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
    chmod -x "${_i}"
  done
  sed -i "s|^\(#!.*python\)$|\12|" "${pkgdir}"/usr/bin/gdbus-codegen
}

# vim:set ts=2 sw=2 et:
