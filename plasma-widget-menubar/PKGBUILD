# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=plasma-widget-menubar
pkgver=0.1.17
pkgrel=100
pkgdesc="A Plasma widget to display menubar of application windows"
arch=('i686' 'x86_64')
url="https://launchpad.net/plasma-widget-menubar"
license=('GPL')
depends=('kdelibs' 'qjson')
makedepends=('cmake' 'automoc4')
optdepends=('appmenu-gtk: support for GTK+ apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
groups=('unity')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a88c73e9f07e8d0b6e5150acf69f50e640f051eb2d3e8401a93ee4de1517e2ba2420d0b2b9ed3eec6b2c8c6aa8a483cadeb5b035ddf6ee8168539f1283d87081')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install
}
