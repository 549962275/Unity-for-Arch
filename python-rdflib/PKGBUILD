# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=python-rdflib
pkgver=3.2.0
pkgrel=100
pkgdesc="A Python library for working with RDF"
arch=('any')
url="http://www.rdflib.net/"
license=('BSD')
depends=('python2-distribute')
groups=('unity')
source=("http://rdflib.googlecode.com/files/${pkgname#*-}-${pkgver}.tar.gz")
sha512sums=('2303a93a1c0310439c05b111391f2972265d0aa53669a0e79f24d79252af240a76b87da5723ee15230cba56def530ee7a02af46be0759dbd2181f9d5ce47285c')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  sed -i 's|^\(#!.*python\)$|\12|g' rdflib/plugins/parsers/{ntriples,notation3}.py
}

package() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname#*-}/LICENSE"
}
