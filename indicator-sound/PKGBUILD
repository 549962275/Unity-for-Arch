# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-sound
pkgname=('indicator-sound' 'indicator-sound-gtk2')
pkgver=0.7.9.1
pkgrel=100
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
makedepends=('intltool' 'vala' 'libgee')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/fourth/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('3247c5046ccf5313582c39581f2ff1216a44d501723f0a5c0f3ccbb45aeb7d24ffffb59fe67c2dc05aa31370a94af675b465783e62d7b349315c93d0bcd0ac96')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile --with-gtk=3
  make
  popd
}

package_indicator-sound() {
  pkgdesc="A unified sound menu - GTK 3 library"
  depends=('libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'libido3' 'libnotify' 'libgee' 'gconf' 'dconf' 'libgee')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-sound-gtk2() {
  pkgdesc="A unified sound menu - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'libido' 'libnotify' 'indicator-sound')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  install -d -m755 "${pkgdir}/usr/lib/indicators/6/"
  install -m755 'src/.libs/libsoundmenu.so' "${pkgdir}/usr/lib/indicators/6/"
}
