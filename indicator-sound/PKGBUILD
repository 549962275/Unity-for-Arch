# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+14.04.20140311.1
_translations=20130418
pkgver=${_actual_ver}daily14.03.11.1
pkgrel=100
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('dconf' 'libgee06' 'libnotify' 'libpulse' 'libxml2')
makedepends=('cmake' 'intltool' 'vala')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-sound.tar.gz"
        'indicator-sound.service'
        'com.ubuntu.sound.gschema.xml'
        '0001_Not_a_phone.patch'
        '0002_Remove_tests.patch')
sha512sums=('e385385313aa568c561e1f65fb8fdac7a40b69818a60c4e1e53f4470eafb3616eb634c72eeb8dda419bdf1dc36404e010cbb1c766206c14701276b50648e43f9'
            '88578aab046cc6e87e015e414b21cf06ab51d26ee932e4213c4e532fd70f373fb03257c5d7b2bbcddbc852816dd1206b934ff9dc4d65dbac3325b382553230b0'
            '80257e6c0d4187c3b904c1177d2879176b653f6fe35ef9bc35fb48ae5659e0f27b4fea8464145f418ffc25e29144862cc3783c93a5fd907321f8e03772e3ccbd'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            '3ca63afb388afb5f32a5ad090db3cc50d40a48deb5bdf206cde6c92963481dcc244157560908613255801c2f09058111f66b6774216f5272f197371b64fd2ece'
            '42f59923419ff5e9265f1737db21ff475fa92fcff045bbac646d0b7da799e190c4a91cdff506cb8053d633eec6a86d6a3c5d00e57de0977614e0d643bcbece2c')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Not_a_phone.patch"
  patch -p1 -i "${srcdir}/0002_Remove_tests.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  # Please fix CMake scripts, Ubuntu...
  for i in ../po/*.po; do
    FILELANG=$(basename ${i})
    FILELANG=${FILELANG%.po}
    install -dm755 "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/"
    msgfmt -v ${i} -o \
      "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/indicator-sound.mo"
  done

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-sound.service" \
                "${pkgdir}/usr/share/dbus-1/services/"

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                "${pkgdir}/usr/share/glib-2.0/schemas/"

  rm -r "${pkgdir}/usr/share/upstart/"
}

# vim:set ts=2 sw=2 et:
