# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=daily13.04.09
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'ido' 'libnotify' 'libgee' 'dconf')
makedepends=('intltool' 'vala' 'libgee')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('9f9541501355cb17d2800e30b564a7056b007dcbd5693103ae4d8f1b957b278ca132166b4e3e247b2258ab9d7072652ff9cf1cadd4979fdd2540f71075bd60d1')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make -j1
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
