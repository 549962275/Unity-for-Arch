# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+16.04.20160406
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee libnotify libpulse libxml2
         pulseaudio qt5-base unity-api unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        com.ubuntu.sound.gschema.xml
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch)
sha512sums=('dff7d5de45b72382f44e488ba9782dd5a832b322d88eda66dda873ebf2c07ad9454e6d9efc42a41b6d3bfdfb9f3a9726b7290f3c81edfff541c0da8367611087'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            '3c57829cd55b1aea14b5bc2f66e8b015d28ccdd8a7d56c5d445efb61d149a49159e6be0c267d8eded5884d53361705373837cfe789d39ced9d262a5fa630491f'
            'a2c8b9f23419c8554a38ac6e3128841e38d15e6ca2f1c70a28a01546ca61090059304974a2d5d8ed5e7c9cc68a2762c6b856d575bae3d74e81b45c0b13e517ce')

prepare() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
    patch -p1 -i ../0002-Remove-tests.patch
}

build() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    [[ -d build ]] || mkdir build
    pushd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
    popd
}

package() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"
    cd build

    make DESTDIR="${pkgdir}/" install

    install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
    install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                  "${pkgdir}/usr/share/glib-2.0/schemas/"

    rm -r "${pkgdir}/usr/share/upstart/"

    sed -i 's/NotShowIn/OnlyShowIn/g' \
        "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
