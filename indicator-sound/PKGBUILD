# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+14.10.20141007
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee06 libnotify libpulse libxml2
         unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        com.ubuntu.sound.gschema.xml
        0001_Not_a_phone.patch
        0002_Remove_tests.patch
        libpulse.vapi)
sha512sums=('fcdac4e2cac69ab3474a0509bd8e74c80ec0865525c2039df6ae8ce44503f4aaefb29fc23098f572d0f426952572df6f1e303ee2e6880e6c7434c975dd98fbcc'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            '57649313d8bd5e053d95e52882039a39ac505ac26a90c9144fc18705fb618548c3c83aafb2a2fc89f6ed7539720811ffa0e9055566ad612f473b6a427e58ac4f'
            '95235ea7e399cc88cfe0a682059fc2d3c3536e86f577d16141de8a2532b4236bd9881f1ccc5201b187977075ba1d99180a63e526968f571219f0d6e0573e449b'
            '5c8920490356e35f994de15959658d4f6665732aa6a3feba10090791b2cd2233d27cffbab2b29bb165d06f449e96c8a35fc4a0a161baaef81780b9f5585cf52c')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001_Not_a_phone.patch
  patch -p1 -i ../0002_Remove_tests.patch

  cp ../libpulse.vapi vapi/
  sed -i '/libpulse$/iposix' src/CMakeLists.txt
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  [[ -d build ]] || mkdir build
  pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                "${pkgdir}/usr/share/glib-2.0/schemas/"

  rm -r "${pkgdir}/usr/share/upstart/"

  sed -i 's/NotShowIn/OnlyShowIn/g' \
    "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
