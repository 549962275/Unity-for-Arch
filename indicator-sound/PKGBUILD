# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-sound
_ubuntu_rel=0ubuntu2
pkgver=12.10.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'libido3' 'libnotify' 'libgee' 'dconf')
makedepends=('intltool' 'vala' 'libgee')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/12.10/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "http://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('4a344139af92324858539dd5e3189bf6d212f3d940d1e53555c6ac181856a92bfa1584b625d23e25928f32a730c5588d45668c84836ccebb3137d86a4967a5e9'
            'eacc5b2a6827df65b0cc46a33cdf8c4e82694d3c7d366c1545791da566d20219bd9e8b0094dc91755ce1ecb6e96162fe038389403716ce94bfed7c347361fc94')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Fix build
  CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
