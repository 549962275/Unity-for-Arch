# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+15.04.20150205
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf libgee06 libnotify libpulse libxml2
         unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        com.ubuntu.sound.gschema.xml
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch
        0003-mpris2-Position-property-should-be-int64.patch
        libpulse.vapi)
sha512sums=('4a7b5479982b96443fe6dc1d7d3c11e300ef5aead9f3e3219d58a8836fd560c83acc0ff85fea5b6d4610a5b00de3684bee89890f5c51fe71de2fb675ebbb61bc'
            '04e662c9724f983c0b9129e13394937541949d481de3722e72046a68a49772707a145854a09bc7830e10ddd9bcf3b3095c9101fbc0c29fd03cfd267a074aee3c'
            '5057e41ee29eb345331c2ba15795d3b365fdc5a13e616f19d820de6713afcf317bb31d5054a36aa1251a0ead4c8f7a6774869bd65dc9fd93683e55cdaefc2edf'
            '9c305542eee8cf907677690d2c8f1a34ff2d131e20833ad419f1775e1d17fc18b58db4b7fb6af942e73fc5d22c3c18477b9bf671a870c165df5bb5dbf65e281b'
            'd37e049252127303caedff694b317d6c9f8a2c3184ada0c7558f9ad5317c33e97c6864d103e3b6fc442aa156ba4e288e91ed023fa0672a3085efb4ea1734c2b0'
            '5c8920490356e35f994de15959658d4f6665732aa6a3feba10090791b2cd2233d27cffbab2b29bb165d06f449e96c8a35fc4a0a161baaef81780b9f5585cf52c')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
  patch -p1 -i ../0002-Remove-tests.patch
  patch -p1 -i ../0003-mpris2-Position-property-should-be-int64.patch

  cp ../libpulse.vapi vapi/
  sed -i '/libpulse$/iposix' src/CMakeLists.txt
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  [[ -d build ]] || mkdir build
  pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build/"

  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -m644 "${srcdir}/com.ubuntu.sound.gschema.xml" \
                "${pkgdir}/usr/share/glib-2.0/schemas/"

  rm -r "${pkgdir}/usr/share/upstart/"

  sed -i 's/NotShowIn/OnlyShowIn/g' \
    "${pkgdir}/etc/xdg/autostart/indicator-sound.desktop"
}
