# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=dee
_ubuntu_rel=0ubuntu1
pkgver=1.0.10.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2' 'icu')
makedepends=('python2' 'gobject-introspection' 'gtk-doc' 'vala')
groups=('unity')
options=('!libtool')
source=("http://launchpad.net/dee/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('fb18694d49426c44a91b766e9929d33099d869c1de94952b4d835ee19a95ace9b5f5e9a8b24f28dc989255173205e939e289f7b9955c894a982b52c5766f8326'
            'e3f1d47b0d92b6c1b938fe1d9aadd56e74b4f74a1fe38591ce98d05d8249e2ea2ca34b2f38d30a27333de50f3da87ad2c1a95c1785422852884cbb4fc04d4dbc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Add gmodule-2.0 to compiler flags
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
