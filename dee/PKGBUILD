# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=dee
_ubuntu_rel=0ubuntu1
pkgver=1.0.8.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2' 'icu')
makedepends=('python2' 'gobject-introspection' 'gtk-doc' 'vala')
groups=('unity')
options=('!libtool')
source=("http://launchpad.net/dee/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('120ea08af78f99683c2aa9fb03e92880d682647b2a7187e7adfce841a02f9dba5b939dbc12149607592bc80c45181b747aea305930c4e7f5850331d4297d762e'
            '48411500c007fdbfb5bb080269f75d5b7261ae7cf7862223b9787808389f8ac445c7d7bea2f904d96c926aff695322dc4620f21b8d6646c9ab5b36ad2b0a4e9a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Add gmodule-2.0 to compiler flags
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}
