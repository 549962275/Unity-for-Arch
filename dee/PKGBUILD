# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=dee
_ubuntu_rel=0ubuntu1
pkgver=1.0.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2')
makedepends=('python2' 'gobject-introspection' 'gtk-doc' 'vala')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/dee/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('7eccf55b8b9b4db3c66e20c797b7f8a6daa311ad8b64d75af6a37d6c44f7d99950a129ffc92185296353d7c5a07f7c3635ec0e377a00f12ce53fbe85627e91be'
            'ae1d848b09275985647bc860de2717e222b099910d5555f2a7b5c487ba75e844dc6b98bc27e1f8c657fe0ed81b61670062096d2595a034bc2d8fe8fbf254652d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}
