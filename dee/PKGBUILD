# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=dee
_ubuntu_rel=0ubuntu1
pkgver=1.0.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library that uses DBus to provide objects allowing you to create Model-View-Controller type programs across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
depends=('glib2' 'icu')
makedepends=('python2' 'gobject-introspection' 'gtk-doc' 'vala')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/dee/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('275276b936be921b517db34d81469d61fdd4f67d9d41d12515b7b6659e7725aa59db310eee284e2325cde4cec8d71d3a966eb79c1166e6993ab136f0ed1a2ac8'
            'd509e830e14606dc0bef4a018237a8550be89db4239057a89c746c94690f87e5be4d12d735dc8362cde0a38f13540b4eaee7dafe045ab627daf7095f2802e317')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Add gmodule-2.0 to compiler flags
  CFLAGS="${CFLAGS} $(pkg-config --libs gmodule-2.0)"

  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}
