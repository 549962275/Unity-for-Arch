# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=('liboverlay-scrollbar' 'liboverlay-scrollbar3')
pkgbase=overlay-scrollbar
pkgver=0.2.15
pkgrel=100
pkgdesc="Ayatana Scrollbars use an overlay to ensure that scrollbars take up no active screen real-estate."
arch=('i686' 'x86_64')
url="https://launchpad.net/ayatana-scrollbar"
license=('LGPL')
groups=('unity')
depends=('glib2-ubuntu' 'gtk2-ubuntu' 'gtk3-ubuntu' 'cairo')
source=("http://launchpad.net/ayatana-scrollbar/${pkgver%.*}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('33e3d4c9df6882f63df5d5cb39d7df12d655d5104ce871e0ecfe9b2b05f74e4bdd5676068cd9bc03c8dd624477b86830e1ee1b86dd159af1f8f65678bffc6143')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's/3\.3\.6/3.2.3/' configure.ac
  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --disable-static
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_liboverlay-scrollbar3() {
  pkgdesc="Scrollbar overlayed widget - GTK 3 library"
  depends=('liboverlay-scrollbar' 'glib2-ubuntu' 'gtk3-ubuntu' 'cairo')
  provides=('overlay-scrollbar')
  conflicts=('overlay-scrollbar')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"
  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
}

package_liboverlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget - GTK 2 library"
  depends=('glib2-ubuntu' 'gtk2-ubuntu' 'cairo')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"
  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
}
