# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=('overlay-scrollbar' 'liboverlay-scrollbar' 'liboverlay-scrollbar3')
pkgbase=overlay-scrollbar
_actual_ver=0.2.16
_bzr_rev=r353
_ubuntu_rel=0ubuntu2
pkgver=${_actual_ver}.${_bzr_rev}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Ayatana Scrollbars use an overlay to ensure that scrollbars take up no active screen real-estate."
arch=('i686' 'x86_64')
url="https://launchpad.net/ayatana-scrollbar"
license=('LGPL')
groups=('unity')
makedepends=('gtk2-ubuntu' 'gtk3-ubuntu' 'gsettings-desktop-schemas-ubuntu')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/overlay-scrollbar_${_actual_ver}+${_bzr_rev}-${_ubuntu_rel}.tar.gz")
sha512sums=('c1c2578f237454fbfefed4f3da2f02976a8bad33f2460f117a8d9e7ad48982ec7ac20be647428f164d2763d899a31e9e1e42b00489af9a5fe8724488221dc3d4')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}+${_bzr_rev}"

  sed -i 's/3\.3\.6/3.2.3/' configure.ac
  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --disable-static
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_overlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget"
  depends=('liboverlay-scrollbar' 'liboverlay-scrollbar3')
  install=overlay-scrollbar.install

  cd "${srcdir}/${pkgbase}-${_actual_ver}+${_bzr_rev}/build-gtk3"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/usr/lib/"
}

package_liboverlay-scrollbar3() {
  pkgdesc="Scrollbar overlayed widget - GTK 3 library"
  depends=('gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}+${_bzr_rev}/build-gtk3"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

package_liboverlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget - GTK 2 library"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}+${_bzr_rev}/build-gtk2"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

# vim:set ts=2 sw=2 et:
