# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gconf-ubuntu
_ubuntu_rel=1ubuntu3
pkgver=3.2.3.${_ubuntu_rel}
pkgrel=100
pkgdesc="A configuration database system"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libxml2' 'polkit' 'libldap' 'dbus-glib' 'gtk3')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
provides=("gconf=${pkgver}")
conflicts=('gconf')
options=('!libtool' 'emptydirs')
install=gconf.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/GConf/3.2/GConf-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'gconf-merge-schema'
        'gconfpkg'
        'gconf-reload.patch')
sha512sums=('1b160a77d73d1c30b3b308c21d5f75734ed1d844d7e3a930342f511580166eaafe3f355d8b11c3070e2a6282c0c7dc71c82ca1ec112a0f43fb351cbcb16cf278'
            'f4cae62fc35c278f8bf87315d58af75d1d1dbb9b1289614966afb720c9f62f3149e512b72f55e722a8f7dea9dc3eb04816fce40075611f208121c5a361f0e02b'
            '0d2a099c772c9fbe535b6c5c35078038987d214a2b68f2d589f46b8bf4e5a8c604624363cd32747b6447d04037d2fb0870502982425629af25ac3ef8ebb2cde4'
            'c1a180fdbd88415f4d0277919bdd3af8c1003e44f81a6ab7f7c341a4065f9e01e320e639177ad93d53463ceecb7f1331171980edabcebdc10998d7f16c490494'
            '1af1d0c926d622794b5d2f21646783c276a8a5c2c6d8b8236804bfcaeb71ab40c6658b17eb4599ca01fb8fe33a513ebe7ec9e8c23e9a21ccae622f6d10aa5657')

build() {
  cd "${srcdir}/GConf-${pkgver%.*}"
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}/gconf-reload.patch"

  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/GConf \
    --disable-static \
    --enable-defaults-service \
    --with-gtk=3.0 \
    --disable-orbit

  make pkglibdir=/usr/lib/GConf ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/GConf-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/gconf/gconf.xml.system"
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/usr/bin/"
  install -Dm755 "${srcdir}/gconfpkg" "${pkgdir}/usr/sbin/gconfpkg"

  #Install Ubuntu stuff

  #Create directories -> from gconf2-common.dirs
  install -dm755 "${pkgdir}/etc/gconf/2/"
  install -dm755 "${pkgdir}/etc/gconf/gconf.xml.mandatory/"
  install -dm755 "${pkgdir}/etc/gconf/gconf.xml.defaults/"
  install -dm755 "${pkgdir}/etc/gconf/gconf.xml.system/"
  install -dm755 "${pkgdir}/usr/share/gconf/defaults/"
  install -dm755 "${pkgdir}/usr/share/gconf/mandatory/"
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  install -dm755 "${pkgdir}/var/lib/gconf/defaults/"
  install -dm755 "${pkgdir}/var/lib/gconf/debian.defaults/"
  install -dm755 "${pkgdir}/var/lib/gconf/debian.mandatory/"

  #Install xinit file
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/debian/70gconfd_path-on-session" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  #Install configuration files
  install -dm755 "${pkgdir}/usr/share/gconf/"
  for i in "${srcdir}"/debian/default.path*; do
    install -m644 "${i}" "${pkgdir}/usr/share/gconf/"
  done
  install -dm755 "${pkgdir}/etc/gconf/2/"
  install -m644 "${pkgdir}/usr/share/gconf/default.path" "${pkgdir}/etc/gconf/2/path"

  #Install binaries
  install -dm755 "${pkgdir}/usr/sbin/"
  install -m755 "${srcdir}/debian/gconf-schemas" "${pkgdir}/usr/sbin/"
  #Python 2 fix
    sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/sbin/gconf-schemas"
  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/debian/update-gconf-defaults" "${pkgdir}/usr/bin/"
  #Python 2 fix
    sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/bin/update-gconf-defaults"

  #Install manuals
  install -dm755 "${pkgdir}/usr/share/man/man8/"
  install -m644 "${srcdir}/debian/update-gconf-defaults.8" "${pkgdir}/usr/share/man/man8/"
}
