# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=hud
_actual_ver=13.10.1
_extra_ver=+14.04.20140218.2
pkgver=${_actual_ver}daily14.02.18.2
pkgrel=100
pkgdesc="Backend for the Unity HUD"
arch=('i686' 'x86_64')
url="https://launchpad.net/hud"
license=('GPL')
groups=('unity')
depends=('bamf' 'libdbusmenu-gtk3' 'sqlite')
makedepends=('cmake' 'dee-qt' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gsettings-qt' 'gtk-doc' 'intltool' 'libcolumbus' 'libdbusmenu-qt5' 'qt5-base' 'python2-distribute' 'vala')
optdepends=('indicator-appmenu: HUD support')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Python_2.patch'
        '0002_Remove_tests.patch'
        '0003_Remove_voice_recognition.patch')
sha512sums=('e280be43813332c6d21d656990ac383ea0123505a77d99c61251ddcfa63bd89e933e61a4305b6d20aa90ee1c4f5f51398a983c3b1ce6674d7efae6c43caa59f7'
            '1d560db046c56ecff0d6566c98684962f179326009fb2bfdbaab3fc1df265afa5cb2da202fb13b4889ade0244eaf39f3aea4d7c9e434c2bd1334f75bc969cc90'
            'c9cc41a62ddc8403f42448ae1beaf76fe5149b7356af7f98aa8e248d838bb26e8e7a206ea6fa37ad00fe60b3291da9535311c5f42e35534b5a711b2682942a7f'
            '599b9c5e1acbb530bc225a8b0f5e68ff4863b57009da35d75bebbf9b40655903405fe00434b375f1d699d52bdeb4c27ef6539f36634e77ce8c72f09ff4a21805')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Python_2.patch"
  patch -p1 -i "${srcdir}/0002_Remove_tests.patch"
  patch -p1 -i "${srcdir}/0003_Remove_voice_recognition.patch"

  sed -i '/mscgen/d;/UseMscgen/d' docs/CMakeLists.txt
  rm cmake/UseMscgen.cmake
  rm cmake/FindGMock.cmake
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_VOICE_TESTS=OFF \
    -DENABLE_SCALABILITY_TESTS=OFF \
    -DFULL_WARNINGS=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
