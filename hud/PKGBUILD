# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=hud
_actual_ver=13.10.1
_extra_ver=+14.04.20140326
pkgver=${_actual_ver}daily14.03.26
pkgrel=1
pkgdesc="Backend for the Unity HUD"
arch=('i686' 'x86_64')
url="https://launchpad.net/hud"
license=('GPL')
groups=('unity')
depends=('bamf' 'libdbusmenu-gtk3' 'sqlite')
makedepends=('cmake' 'dee-qt' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gsettings-qt' 'gtk-doc' 'intltool' 'libcolumbus' 'libdbusmenu-qt5' 'qt5-base' 'python2-distribute' 'vala')
optdepends=('indicator-appmenu: HUD support')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        'com.canonical.Unity.WindowStack.service'
        '0001_Python_2.patch'
        '0002_Remove_tests.patch'
        '0003_Remove_voice_recognition.patch')
sha512sums=('f78341332b931b18caa7014d30f85b9a25e9375bd1dea3d3f2b7105178acab87d80a6377714e70ff726c618a3d954adda6574697b96b32a4a99a620f0448a524'
            '7873062b669d39c1399a5110457bf5607c2105e914fbcd0d10c1189ca6810cd0210e6cc642bebfc284226f00ea15ae4bccfba24931f73abffdb8af68df225e39'
            '1d560db046c56ecff0d6566c98684962f179326009fb2bfdbaab3fc1df265afa5cb2da202fb13b4889ade0244eaf39f3aea4d7c9e434c2bd1334f75bc969cc90'
            '546aa6b2c1a6e9f9de8f6c4a5d53fe48af39e0f0a96a2e2c3923d44187046ee7da89cba6b9e043e2cf14fd1b333db18fc0d91cb7530b7bc2394080d2b690e602'
            '599b9c5e1acbb530bc225a8b0f5e68ff4863b57009da35d75bebbf9b40655903405fe00434b375f1d699d52bdeb4c27ef6539f36634e77ce8c72f09ff4a21805')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Python_2.patch"
  patch -p1 -i "${srcdir}/0002_Remove_tests.patch"
  patch -p1 -i "${srcdir}/0003_Remove_voice_recognition.patch"

  sed -i '/mscgen/d;/UseMscgen/d' docs/CMakeLists.txt
  rm cmake/UseMscgen.cmake
  rm cmake/FindGMock.cmake
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  rm -rf build && mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_TESTS=OFF \
    -DENABLE_VOICE_TESTS=OFF \
    -DENABLE_SCALABILITY_TESTS=OFF \
    -DFULL_WARNINGS=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/com.canonical.Unity.WindowStack.service" \
                "${pkgdir}/usr/share/dbus-1/services/"

  rm -r "${pkgdir}/usr/share/upstart/"
}

# vim:set ts=2 sw=2 et:
