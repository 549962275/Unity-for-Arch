# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=utouch-geis
pkgver=2.2.9
pkgrel=100
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('GPL' 'LGPL')
depends=('utouch-grail' 'xorg-xinput' 'python2' 'dbus-core')
options=('!emptydirs' '!libtool')
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c2bc07840d199b6e627ebdeeb48d31f61efcef884cfedf677a69aee77f5d862d05a4ed71f515a3f69220a6ce62b065ae7ba1791105d49b2ed261d76546a781fb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis} "${pkgdir}/usr/lib/python2.7/site-packages/geisview/__init__.py"
}

# vim:set ts=2 sw=2 et:
