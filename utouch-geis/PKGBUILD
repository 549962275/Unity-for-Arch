# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=utouch-geis
_ubuntu_rel=0ubuntu1
pkgver=2.2.5.${_ubuntu_rel}
pkgrel=101
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('GPL' 'LGPL')
depends=('utouch-grail' 'xorg-xinput')
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('43e58c5e1df07946ed52069b3f8d72a43f73c6742ed6f542f98db7f88560951981a9ec28d639d7b7d924d8d6c5f4e78a61783ed6a0ae713db09b15e9178ebd6d'
            '2905ed539a5dd9d19576ac6ca9a59daa42a9346904c854c932eab0f7591cf8f43383d5887b980e330d8241a70f18e4f74df1a0c31761d875d6428564a1378b9a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis}
}
