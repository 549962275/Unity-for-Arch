# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=utouch-geis
_ubuntu_ver=0ubuntu4
pkgver=2.1.2.${_ubuntu_ver}
pkgrel=1
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('GPL' 'LGPL')
depends=('utouch-grail')
makedepends=('xorg-server-ubuntu')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/u/utouch-geis/utouch-geis_${pkgver%.*}-${_ubuntu_ver}.debian.tar.gz")

sha512sums=('a9525ab53acea0e37d797aacd78f7ef052009129a50a336a315c606c0d64103638a7d3899980f4f8dd01a171b26f25c19fb736c4b2845dea0dda0c058f903635'
            '7e7c2d3df9b45ab5f35d0ae80e01e81749054c1de4bceeacf545af9a8fef3b6c23c07977cc4405df461ca97d491df67a83d0f586164d00f79adf03a6423bd155')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -fi

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis}
}

