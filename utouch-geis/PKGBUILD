# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=utouch-geis
_ubuntu_rel=0ubuntu2
pkgver=2.2.7.${_ubuntu_rel}
pkgrel=100
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('GPL' 'LGPL')
depends=('utouch-grail' 'xorg-xinput')
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('45733279bc6e2b9eb53f7fa8feb61fd8e1660da167f076a8ec54b81bb8ca042e2a1f163bcafa370bf5d2478471a858b80cc08128e7883655860f8dd0cb18f7e1'
            'ad4054186c6f53f55e0f288320e679fbe76c39add3f349c43686abcadd1ed1febcca41fe84204008c9a4b35c04d07eacc76636557a682c128b5dc5998a2880d1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis}
}
