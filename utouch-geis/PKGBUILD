# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=utouch-geis
_ubuntu_rel=0ubuntu1
pkgver=2.2.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/utouch-geis"
license=('GPL' 'LGPL')
depends=('utouch-grail' 'xorg-server-ubuntu' 'xorg-xinput-ubuntu')
optdepends=('xf86-input-evdev-ubuntu: for Apple multitouch trackpad support'
            'xf86-input-synaptics-ubuntu: for multitouch touchpad support')
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('2d5c47c3b00f71d69842872b78e02d8477cdf7a54d75d422a87e02bad4134d1aed818d5c0edbeffc7663fab44ae8b3d48cfcb1f61ce515897423b6333f1bcf68'
            '43abce56e1fcfec301d22d5c8390644a0e96944c46f6376f12a52479aa86e45597cbe39d758e6db64d231cea3b9af1576b56c40373ee2bf9126cad01e5a69012')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis}
}

