# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-application
_actual_ver=12.10.1
_extra_ver=daily13.01.11
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A library and indicator to take menus from applications and place them in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-application"
license=('GPL')
depends=('dbus-glib' 'libappindicator3')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-application_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('7a7fa649d1634b7d22f4a7a429704f14977f1b24868dcd4728b6391aef470277f63fffb0ac3df16580fa271dc20b654f59781acfe9ddbf525c874019e39d2731'
            '7ef9f2eab8dd17d8957b706ba479c38f5f9e74a69e77af7c8e318809c67b4aea214433f0364741fc4fcbfaefd6bbff3e370e24048cd23f2e09ebe6bca32b1511')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/share/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
