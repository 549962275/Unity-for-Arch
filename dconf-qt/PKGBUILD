# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=dconf-qt
_ubuntu_rel=0ubuntu3
pkgver=0.0.0.110722.${_ubuntu_rel}
pkgrel=102
pkgdesc="Qt and QML bindings for dconf"
arch=('i686' 'x86_64')
url="https://launchpad.net/dconf-qt"
license=('LGPL')
depends=('qt' 'dconf')
makedepends=('cmake')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}.orig.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('4c7e2f02f7dfce3d532a455c3a2b8da5900260f570280b0d4da312746e8925c0b9fe842006d441fc17e193b07b16016d13a5f1a1b47eba725419e9e4f0f7fd47'
            'b251f922b35dacf3a42969aac2a7158a668fa6b6c02dbeb9ba13adff4b23fb966d1496249a1951d3fe3e5fc720a2e4c0b6518c7d10e0df11205420b8c9ab70ab')

build() {
  cd "${srcdir}/lib${pkgname}-${pkgver%.*.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Fix Qt imports directory
  sed -i '/set(IMPORT_INSTALL_DIR/d' qml/CMakeLists.txt

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DIMPORT_INSTALL_DIR=lib/qt/imports/QConf \
    -DCMAKE_BUILD_TYPE=Release
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/lib${pkgname}-${pkgver%.*.*}"

  make DESTDIR="${pkgdir}/" install
}
