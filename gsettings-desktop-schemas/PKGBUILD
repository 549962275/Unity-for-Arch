# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas
_ubuntu_rel=0ubuntu1
pkgver=3.4.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2' 'dconf')
makedepends=('intltool')
install=${pkgname}.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*.*}/${pkgname}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('0a2d0bae1821a94d39bb3bd7e9417d09d4392acc67260b300d565cb87021ccf48c68b0d56365dd8fb6428207666c19525cb4ceecd1db69ca0392a752262aa6bf'
            '56a5262c87767d2bf18e87184b5f9f1481c078e62f95f21919b6e06521ea5760ec77e0cd341705cd4a8f2e10825f40ebade080717e965a6d9f139c564b4de0bb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
