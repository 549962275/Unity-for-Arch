# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic

pkgname=indicator-keyboard
_actual_ver=0.0.0
_extra_ver=+14.10.20140620.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
pkgdesc="Indicator for switching keyboard layouts and input methods"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-keyboard"
license=(GPL)
groups=(unity)
depends=(accountsservice gnome-desktop gucharmap ibus libgee06 libgnomekbd
         lightdm-ubuntu unity-control-center)
makedepends=(gobject-introspection intltool vala)
conflicts=(ibus-ubuntu)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-keyboard_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-vala-build-fix.patch
        0002-Remove-code-depending-on-accountsservices-patch.patch
        0003-Remove-unavailable-DBus-method-activate_input_source.patch
        0004-Remove-unavailable-DBus-method-xkeyboard-layouts.patch
        0005-Vala-0.26-fixes.patch)
sha512sums=('82c9ba708a248f654984ae01cebd098905bab75308124d7cfca6f8dbb3a8f838b9239015802853237472ed5ce780a50a09a6f178afecad771baf4ba62f4ee118'
            '87b34733c0200e092da0fe9329769a3d2fdf3c3702ca1a9ff9fb6023fc8a7cfcfb0176b3df2d5078bb1b0e15d17f9634783397786fc04bf12d498c4f20d1bd35'
            '730eeb74af9ff202cc739255b1afe35faa7b2cc764a4d39036b6fea4b5c7e689baca6e281202d42a7d8aacf0866b0dd6f31e93f3a557db3d844d6ec0fed72d3f'
            '792144126c5517bb731dd5f2a25c40825b1cd186c0e0f9edaefcf6f11130c6e674a2d6708ec9594d51fa073b8862b9d7e0f0af93dc0636c2cf8db45faf3811de'
            '17e8996fce09209a00a6f36bd8a0a1d7369c8897517b37b64dff9cff579a61f93124d83a86eb081eb03c7b96c8b9408ca18eee0c17680426bfb761fdeb99b64b'
            '64d4bd8a2b4dd920137908295e74cdf508bc43e74345d46da100bd5b42a10827f49ac6039d06272bc2d801ce748dae49ecfe53aa110837cc28aaf5a1ff82b0d7')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i ../0001-vala-build-fix.patch
  patch -p1 -i ../0002-Remove-code-depending-on-accountsservices-patch.patch
  patch -p1 -i ../0003-Remove-unavailable-DBus-method-activate_input_source.patch
  patch -p1 -i ../0004-Remove-unavailable-DBus-method-xkeyboard-layouts.patch
  patch -p1 -i ../0005-Vala-0.26-fixes.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/indicator-keyboard
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}"/usr/share/upstart/
}
