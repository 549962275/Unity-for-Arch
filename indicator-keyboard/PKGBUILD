# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-keyboard
_actual_ver=0.0.0
_extra_ver=+14.04.20140410.1
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Indicator for switching keyboard layouts and input methods"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-keyboard"
license=('GPL')
groups=('unity')
depends=('accountsservice' 'gnome-desktop' 'gucharmap' 'ibus' 'libgee06' 'libgnomekbd' 'lightdm-ubuntu')
makedepends=('gobject-introspection' 'vala')
conflicts=('ibus-ubuntu')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-keyboard_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-vala-build-fix.patch'
        '0002-Remove-code-depending-on-accountsservices-patch.patch'
        '0003-Use-gnome-control-center.patch'
        '0004-Remove-unavailable-DBus-method-activate_input_source.patch')
sha512sums=('70acaa61fe8ad0c53aad7d5d5d47493b22a6e8ba88b6953725a8e66f4eac0b9a261af500f812e4f35a41f9c52ccd3cc3d898035e2e6f59258a331254a0243087'
            '3d428f4146e3e6b2bc6f0a67e226a3771cdf333c281603a2a6f83a5830dec4153730e38a77a578996a7b8b1e9cf9e5b43449307969f138992dc49abb8a3a507a'
            'bb6f800a9d5bad855c95077593cd723bc9d3e5aec4c401fc58ff91fc6995607d050f2df582d18be0f97c650c1125ca01fac15368b6586fe9030e86eebb84c4a7'
            'f0cc79c63a6ea2d084043fc583c415d108cb37dbc21138d755912bef9001c1c7553d436f6ba84972600bc7e56a5479959344571db1a3acd68f67c53ed8cebf9f'
            '636b7d1692e4508aee0f2e96f4dc3afdce29fa6eb0d5403f1960f58624d3f9cdadac2b4aef510d0af3c188c547a5fb8e7a1d6f1833a6b9bff0dc6b7aa08faf69')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i "${srcdir}/0001-vala-build-fix.patch"
  patch -p1 -i "${srcdir}/0002-Remove-code-depending-on-accountsservices-patch.patch"
  patch -p1 -i "${srcdir}/0003-Use-gnome-control-center.patch"
  patch -p1 -i "${srcdir}/0004-Remove-unavailable-DBus-method-activate_input_source.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  intltoolize -f
  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/lib/indicator-keyboard
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
