# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_ubuntu_rel=0ubuntu2
pkgver=0.4.92.${_ubuntu_rel}
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
mekedepends=('gtk3-ubuntu')
options=(!libtool)
groups=('unity')
source=("http://launchpad.net/${pkgbase}/0.5/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('d8a1b21f9354e86e9e5c16f3b3f6c54d3a6b680af55e8f70126ea01d0d11215c718ec57be01ad712680c97d071c26c9ad1c8bd4e990e6ee1b41276b0e9608b70'
            '99829236e16b8e1fd13608a84d25bc76b6dbe7bea1cec9f79e79c117376c2894bc2ca60e2094374717b07443f3570cb95a56a80db7b5ee4c349cb65d8c894e18')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  #Identical in both packages, but Ubuntu puts it in the GTK 2 package
    #Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}
