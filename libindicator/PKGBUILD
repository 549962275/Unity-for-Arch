# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_ubuntu_rel=0ubuntu1
pkgver=0.4.93.${_ubuntu_rel}
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
mekedepends=('gtk3-ubuntu')
options=(!libtool)
groups=('unity')
source=("http://launchpad.net/${pkgbase}/0.5/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('3eff14b95ee349b72cd3a4d6ade8f9767ac69977a09b5e1718f66b683ca0d242b3ccdffea515aa83aa9ae9f0960b1569787153184c4991dfdf57537eee376cb4'
            'cd6dc195372caeece1ef98994d0a6b535fc3060c11de3961a0c2e622c725de16b8be8ba33c168a842f0ce64c803744e9320d53b2406e51d4f04da871c30a41df')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  #Identical in both packages, but Ubuntu puts it in the GTK 2 package
    #Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}
