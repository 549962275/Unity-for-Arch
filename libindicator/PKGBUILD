# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_ubuntu_rel=0ubuntu1
pkgver=0.5.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
mekedepends=('gtk3-ubuntu')
options=('!libtool')
groups=('unity')
source=("http://launchpad.net/${pkgbase}/0.5/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('119a7633f5bd742c49d2a0a696516a4d4df0573f32c1358a3ca74e7c8a5ae598e975364bce37ca0e0bec9a469d053426d23c30b8742db7a190d4f8d086cb84e7'
            '9188942f876ad88c9c81274f192e8fe79d52f225c540d6eaa2ef5eaecec33b559b49d8edba4c6b47a0fac0dfc2ee5d85c144584e2a9a484d66c52cd13213d192')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')
  options=(${options[@]} '!emptydirs')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  # Identical in both packages, but Ubuntu puts it in the GTK 2 package
    # Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}

# vim:set ts=2 sw=2 et:
