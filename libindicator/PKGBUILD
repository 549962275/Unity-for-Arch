# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_actual_ver=12.10.2
_extra_ver=daily13.02.15
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2-ubuntu' 'gtk3-ubuntu')
options=('!libtool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('2dd89b3896816ccb8b3931bcb3f6d9b3b2733f0f34d1e8801a5329fd4d7f8dc8c6d6595be9223f58ab9d998bf28a71987ebd85e9c95b5cc9bf29edb21851aeca')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  MAKEFLAGS="-j1"
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"

  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')
  options=(${options[@]} '!emptydirs')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"

  MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  # Identical in both packages, but Ubuntu puts it in the GTK 2 package
    # Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}

# vim:set ts=2 sw=2 et:
