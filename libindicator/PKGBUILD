# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_actual_ver=12.10.2
_extra_ver=daily13.06.07
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2-ubuntu' 'gtk3-ubuntu')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
options=('!libtool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Revert_r490.patch'
        '0002_Revert_r489.patch')
sha512sums=('4db6de4d135b8631a00b115b9a5568dcc382b2214ad590180a8dea14630b3ebaf89f01ebadebfbe8183a5f9069e23317ac61470dde60411ce0707dbe7bfc8dda'
            '2684c430b72948bf00cc37b00329d41521afa1037ed87d08972843ab21fff2b59b7e1dde10a3c8f1f03ea9891681b2ee1273c1ca9f329dc7e984627b14a058be'
            '988366faad27e055bc582caa12a65eb90dac6cab6d8020abcca76a072cde543e96ef8afdbf478be8308a14e9dddc65e808b81d6cb2c4de67ef2aaac6456bdf28')

prepare() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  patch -p0 -i "${srcdir}/0001_Revert_r490.patch"
  patch -p0 -i "${srcdir}/0002_Revert_r489.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make
  popd
}

# Ubuntu/Debian's dh_auto_test is not being run for this package (probably a
# packaging mistake)
check() {
  # test-indicator-ng fails under GTK 3
  #cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"
  make check
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')
  options=(${options[@]} '!emptydirs')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  # Identical in both packages, but Ubuntu puts it in the GTK 2 package
    # Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}

# vim:set ts=2 sw=2 et:
