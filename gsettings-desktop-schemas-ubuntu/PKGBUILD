# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas-ubuntu
_ubuntu_rel=0ubuntu3
_ubuntu_ver=3.6.0
_actual_ver=3.6.1
pkgver=${_actual_ver}.ubuntu${_ubuntu_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
provides=("${pkgname%-*}=${_actual_ver}")
conflicts=("${pkgname%-*}")
install=${pkgname}.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('dc9d9173f4f89ece2f9b36748784d6832d28e73c5c90dabee3e7a4aa99a9f3e9f36986d85e64c9ddea8905d8de41fd5616b727b1a4b1818a30d37e66cf4ba9c4'
            '3e0195f29f760328418aabb0037c61ba057cfc5260588ed7bef27bacc7bcf34ed0b859c689dd47991c403ef96109b2f2dba8f02d6c3a1714caf02db50a2ac3d8')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
