# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=geoclue
_ubuntu_ver=1ubuntu10
pkgver=0.12.0.${_ubuntu_ver}
pkgrel=100
pkgdesc="Modular geoinformation service built on the D-Bus messaging system."
arch=('i686' 'x86_64')
url="https://launchpad.net/geoclue"
license=('LGPL')
depends=('gconf')
makedepends=('gtk-doc' 'autoconf' 'automake')
groups=('unity')
options=('!libtool')
source=("http://archive.ubuntu.com/ubuntu/pool/main/g/${pkgname}/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/${pkgname}/${pkgname}_${pkgver%.*}-${_ubuntu_ver}.debian.tar.gz")
sha512sums=('c65fef518fea5708067def3595a57b2e3cb4c04a3d1c367ac02c4802386a6c8420b3d20f5194896d8e0fdeee6cc6af2744cc920af1ab680a3ca197cb5a5bfab5'
            '5bcd008fa26257bd8edc9673188025df0ca54fe5c9e9d1ba154168801de4b4209a02e6f5b353d0fcb02523320e5e4565dff5c62f7d33f8c2e5400bb61b1702f4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --enable-gpsd=no --enable-gypsy=no

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR=${pkgdir} install
}
