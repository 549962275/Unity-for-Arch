# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libsignon-glib
_ubuntu_rel=0ubuntu1
pkgver=1.6.${_ubuntu_rel}
pkgrel=100
pkgdesc="Authentication management library for GLib applications"
arch=('i686' 'x86_64')
url="https://code.google.com/p/accounts-sso/"
license=('LGPL')
groups=('unity')
depends=('glib2' 'gobject-introspection' 'python-gobject' 'python2-gobject' 'python2' 'python3' 'signon')
makedepends=('check' 'gtk-doc')
options=('!libtool')
source=("https://accounts-sso.googlecode.com/files/libsignon-glib-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libsignon-glib_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('c733bb826d951c01540106990228af3ecef55e29d2c51891b2c396ebd35b6e30651002ecac6c7611502bff44e0cfc755582b124724729c4d557e4d92099d128b'
            '81f2a6114c5c191f330d082509971b25357a880fe52790047da4517dc9b5d441c3e248012bb116c3a7f83d370423b33ae55ffb9e5aa661e300188e25e8868f4b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  mkdir build-python2 build-python3

  pushd build-python2/
  PYTHON=python2 ../configure --prefix=/usr --disable-static --enable-gtk-doc
  make -j1
  popd

  pushd build-python3/
  PYTHON=python3 ../configure --prefix=/usr --disable-static
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  pushd build-python2/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-python3/pygobject/
  make DESTDIR="${pkgdir}/" install
  popd

  # Remove useless empty files
  rm -rv "${pkgdir}/usr/doc/"
}

# vim:set ts=2 sw=2 et:
