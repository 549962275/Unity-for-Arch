# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=zeitgeist-datahub
_ubuntu_rel=1ubuntu1
pkgver=0.8.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="The datahub provides passive plugins which insert events into Zeitgeist"
arch=('i686' 'x86_64')
url="https://launchpad.net/zeitgeist-datahub"
license=('GPL')
depends=('libzeitgeist' 'gtk2-ubuntu')
makedepends=('intltool' 'vala')
groups=('unity')
source=("http://launchpad.net/${pkgname}/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('4e846a9b38dd45a4c778968706d6f0c030c959fa01edddb4c7912240fa3e3f6cfd27a55c32edb6a526324ef944471149d0c59814152bab95e2ab6dcc3ea32963'
            '1f51c62ab5a14f608f0e71ea5fd1c37e3649d6d0933137857ebf21222ee0a9bf0e54f8f7861f765c8577f90bda5cd2b63bbf348a67707385137201c514048a98')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/debian/patches/nodisplay_autostart.patch"
  patch -Np1 -i "${srcdir}/debian/patches/add_datahub_autostart_delay.patch"

  ./configure --prefix=/usr --sysconfdir=/etc
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}
