# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau-ubuntu
_ubuntu_ver="+git20111201+b5534a1"
_ubuntu_rel=1
_actual_ver=0.0.16
pkgver=${_actual_ver}$(echo ${_ubuntu_ver} | tr '+' '.')
pkgrel=100
pkgdesc="Open Source 3D acceleration driver for nVidia cards (experimental)"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/wiki/"
license=('GPL') #and MIT, not yet a license file, see http://nouveau.freedesktop.org/wiki/FAQ#head-09f75d03eb30011c754038a3893119a70745de4e
depends=('libdrm' 'udev')
optdepends=('nouveau-dri:	experimental gallium3d features')
makedepends=('xorg-server-devel' 'libdrm' 'xf86driproto')
provides=("xf86-video-nouveau=${pkgver}")
conflicts=('xorg-server>=1.11.0' 'xf86-video-nouveau')
options=('!libtool')
install=${pkgname%-*}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-nouveau_${_actual_ver}${_ubuntu_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-nouveau_${_actual_ver}${_ubuntu_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('d0ff54ead6e2b2e5eb640ac1a88f41d08757b3edda25c9ffcf4389aaf9e792b729a6d42a6b5f0f9a355a1d7e158cd79ccec5634d45493ec7a054cf27696432b9'
            '108db48159abc3214871f0b441c991e0aa54f7bc86e294388b313a6fcdd1e6a179b676e06da47df331bea17cd8dc9202abf323886858a95dcec6d4f1ce3ffbf3')

build() {
  cd "${srcdir}/${pkgname%-*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/xserver-xorg-video-nouveau_${_actual_ver}${_ubuntu_ver}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  ./autogen.sh --prefix=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}"
  make DESTDIR="${pkgdir}/" install
}
