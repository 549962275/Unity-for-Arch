# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=dee, repo=utopic
# vercheck-launchpad: name=dee

pkgname=dee-ubuntu
_actual_ver=1.2.7
_extra_ver=+14.04.20140324
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
pkgdesc="Model to synchronize multiple instances over DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dee"
license=('LGPL')
groups=('unity')
depends=('dbus-glib' 'glib2' 'icu' 'python' 'python-gobject' 'python2' 'python2-gobject')
makedepends=('gnome-common' 'gobject-introspection' 'gtk-doc' 'vala')
provides=("dee=${pkgver}")
conflicts=('dee')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dee_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('ce961e94a0d925dff778a654d694664a440748287b986cafeadc5a370e4bbb1a9dcbed0ca5b9624ddd4f975d92d702f61e0ca086e2718727f68aca8bd7091f0f')

build() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  mkdir build-python3 && cd build-python3
  PYTHON=python3 ../configure --prefix=/usr --enable-gtk-doc
  make
  cd ..

  mkdir build-python2 && cd build-python2
  PYTHON=python2 ../configure --prefix=/usr
}

package() {
  cd "${srcdir}/dee-${_actual_ver}${_extra_ver}"

  cd build-python3/
  make DESTDIR="${pkgdir}/" install
  cd ..

  cd build-python2/bindings/python/
  make DESTDIR="${pkgdir}/" install
}
