# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-applications
pkgver=5.8.0
pkgrel=100
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('dconf' 'libzeitgeist' 'libunity' 'zeitgeist-extensions' 'gnome-menus2')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/${pkgver%%.*}.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '10-no-db51.patch')
sha512sums=('ee3003cf1b7fee249f8d00c7ff2c48e942e958f1543a18e91870ddad1d0ee291c167ff2f4523773ba3084fca2367646ec300e1fbda8dcc82f87a8913f4f6404f'
            '51126591417c1edf29fe7bfcd01ea7f61456505cc6a2a37ca8e67d2b53d8622bdc631765741f2eea36413ab036cfc0e6e0e1dfd7d53e0bf10e50f81bbc9883d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove check for db version 5.1
  patch -Np1 -i "${srcdir}/10-no-db51.patch"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static --libexecdir=/usr/lib/${pkgname} --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
