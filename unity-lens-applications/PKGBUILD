# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-applications
_actual_ver=6.10.0
_extra_ver=daily13.02.28
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=100
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('dconf' 'gnome-menus' 'libzeitgeist' 'libunity' 'xapian-core')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-applications_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '10-no-db51.patch')
sha512sums=('5158c1793772c5ff92fa588b791e1841bfc3b59baf93fbe4e8dd4b9f4e1a17e10b01fe69437bd1294a0e17e5ad39a7bf5541f3d9146b2fa026510e1b45853cd8'
            '51126591417c1edf29fe7bfcd01ea7f61456505cc6a2a37ca8e67d2b53d8622bdc631765741f2eea36413ab036cfc0e6e0e1dfd7d53e0bf10e50f81bbc9883d9')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Remove check for db version 5.1
  patch -Np1 -i "${srcdir}/10-no-db51.patch"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libexecdir=/usr/lib/${pkgname} --disable-schemas-compile
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
