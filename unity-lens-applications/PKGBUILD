# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-applications
_actual_ver=7.1.0
_extra_ver=+16.04.20160217.2
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=(i686 x86_64)
url="https://launchpad.net/unity-lens-applications"
license=(GPL)
depends=(dconf gnome-menus libcolumbus libgee libunity libzeitgeist
         xapian-core zeitgeist-ubuntu)
makedepends=(vala intltool)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-applications_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8d5b8bd49104a98bffbfaa6e84693e16495a9c6a9b065a3431311fc03dea1ad1fc1c21d818e6ddab09db17b14d1e974255162c137a606f1fdc54858647fd1594')

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    aclocal --install --force
    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-static \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-schemas-compile
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}/" install

    # Use language packs
    rm -r "${pkgdir}/usr/locale/"
}
