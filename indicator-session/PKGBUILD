# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-session
pkgver=12.10.2
pkgrel=100
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'dbus-glib' 'dconf' 'gnome-packagekit')
makedepends=('intltool' 'packagekit')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/12.10/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '0001_Revert_new_glib_stuff.patch'
        '0002_There_is_no_help.patch'
        '0003_Workaround_disappearing_icon.patch')
sha512sums=('51fc509a028ee31f5ad31829503f398b6b9ea34dddf53b713ea765c7170337755874c59327100299decc724d24bdedfbe6d8804e820e34bd34370a4d00ddff37'
            '7889156ac9a16035d033d81a9eb12480d6b78f0fd566ffae58971dec7c95357cff4c81c507b2e027f4e665ad9588ad07aaa65815c8ccc888df7b1a072fa5bc7e'
            'f00ea3d3c00da05211e869fbde613807671e2faf712f832aa47100486b5991ed00e0d301f0e05c56090f37fd556663de87f912b7af5e762f7925d21d2719f99d'
            '11abe32e165a04ef0a035db59fe88b27cb50538dfe6051e99445cf3b852f10ac04f1a371fcb3fed9bc0114ee66a422c179ae0045411e8dc5fcf18cabcd5f43d8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001_Revert_new_glib_stuff.patch"
  patch -Np1 -i "${srcdir}/0002_There_is_no_help.patch"
  patch -Np1 -i "${srcdir}/0003_Workaround_disappearing_icon.patch"

  sed -i 's/2\.33/2.22/g' configure.ac
  autoreconf -vfi

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
