# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=+15.10.20150915
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Indicator showing session management, status and user switching"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-session"
license=(GPL)
depends=(gnome-settings-daemon)
makedepends=(cmake intltool python2)
optdepends=('lightdm: support for lightdm')
groups=(unity)
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        indicator-session.service
        0001-There-is-no-help.patch
        0002-Remove-url-dispatcher-dependency.patch)
sha512sums=('31646ca4bd58f5dd3a4b83e103e5c60df2ca687f4bfc23576941f6702c1485cc5b0e5b02e33e2601b3efa274c4eac871491c32eb8316167eabdea57849670243'
            '540c76c0f68b028194bb1000dd61a0c24f2f16427e79d237b94b8c65fe5e3e16eb6ea645f0fd33d734113727e25b6eb7452cb72f72399c9e8d370f74abba7511'
            'cc5aef256129d885380a171fecde3ca2cf63332c08fb1701f05acb33c7662e3ae95b729b43ab5e3f56ce8aa7962120cb187c1d06bb10abec8613af0f3f8a50bd'
            '71c127c6630f237624c2ba02b8ea612aef39280fc79441a0a0fb17a3eac71308104eeee79715343b4e21e10cb58c89e16161b23381808feaddbbda5051f00524')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0001-There-is-no-help.patch
    patch -p1 -i ../0002-Remove-url-dispatcher-dependency.patch
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    mkdir build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -Denable_tests=OFF \
        -Denable_lcov=OFF

    make
}

package() {
    cd "${pkgbase}-${_actual_ver}${_extra_ver}/build"

    make DESTDIR="${pkgdir}/" install

    rm -vf "${pkgdir}/test-service"

    install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
    install -m644 "${srcdir}/indicator-session.service" \
                  "${pkgdir}/usr/share/dbus-1/services/"

    rm -r "${pkgdir}/usr/share/upstart/"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
