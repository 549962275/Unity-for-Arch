# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=daily13.03.08
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'dconf')
makedepends=('intltool')
checkdepends=('dbus-test-runner' 'gtest-ubuntu' 'xorg-gtest' 'xorg-util-macros')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Lock_Screen_Live_CD_Detection.patch'
        '0002_There_is_no_help.patch'
        'revert_r382.patch')
sha512sums=('2c97c158af4e33890db7bba1a2a20702637b6906015e7ede7d5ea7c73226b86856d1fb39298f665c1d04d6d8614e758236633681d665baffff9a44be0a91cddc'
            '99e33a2234a2d05e80607608290aa95496f9f74672dddb47fff4abdd8642e5aa456c96d52198c3b4a01bf23dfe0e321d31b05f471bcd554a85db011436f6757b'
            'f00ea3d3c00da05211e869fbde613807671e2faf712f832aa47100486b5991ed00e0d301f0e05c56090f37fd556663de87f912b7af5e762f7925d21d2719f99d'
            '0bae670655e2cebd82f702bd7e804828729f0a23eac8212637875ebe19b56151066add500e6580e856f6820dc4304327b609a0b5fe82b7c119769373eaa38018')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p0 -i "${srcdir}/0001_Lock_Screen_Live_CD_Detection.patch"
  patch -p1 -i "${srcdir}/0002_There_is_no_help.patch"
  patch -p0 -i "${srcdir}/revert_r382.patch"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/"

  env -u LD_PRELOAD make check
}

package() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
