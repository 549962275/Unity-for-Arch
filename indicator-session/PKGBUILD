# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-session
pkgname=('indicator-session' 'indicator-session-gtk2')
pkgver=0.3.7.1
pkgrel=100
pkgdesc="Quick! Change your status. Switch users. Close your session. All provided by the The Session Menu these tasks are conviniently placed in the upper-right corner of the desktop to make them available and easy to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
makedepends=('intltool')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/${pkgver%.*.*}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('5cf55ab872de45cf87206128210bdd32b83901b06cf445ff81237ebaea1845663061287936cfcec5391ee8b721abe2b7ddd25c28b32064f21ec25b397e495aad')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-session() {
  pkgdesc="Indicator showing session management, status and user switching - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'dbus-glib' 'dconf')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-session-gtk2() {
  pkgdesc="Indicator showing session management, status and user switching - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'indicator-session')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  install -d -m755 "${pkgdir}/usr/lib/indicators/6/"
  install -m755 'src/.libs/libsession.so' "${pkgdir}/usr/lib/indicators/6/"
}
