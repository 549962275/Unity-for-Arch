# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-session
pkgname=('indicator-session' 'indicator-session-gtk2')
pkgver=0.3.96
pkgrel=100
pkgdesc="Quick! Change your status. Switch users. Close your session. All provided by the The Session Menu these tasks are conviniently placed in the upper-right corner of the desktop to make them available and easy to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('gnome-packagekit')
makedepends=('intltool' 'packagekit')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("http://launchpad.net/${pkgbase}/0.4/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz"
        'use_gnome-packagekit.patch')
sha512sums=('f28ddf0d519da1a0eef79e85f27f77dbf8a125e79d9766f078340ed506f603e7b49dd5adf32db79b706cfda5a3a9bf6513ec2af2e2cf9d06d643cbadfdbdd9ba'
            '2314a5516c8f71d8fd233605a87732c9868f45a25e55f89c9f6edfc2f4f8589f4c4427c71657363b2dd8071fe24aa787b799a7d0d5d24a5869d586c4eea7b96a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Use gnome-packagekit
  patch -Np1 -i "${srcdir}/use_gnome-packagekit.patch"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-session() {
  pkgdesc="Indicator showing session management, status and user switching - GTK 3 library"
  depends=('libindicator3' 'libdbusmenu-gtk3' 'dbus-glib' 'dconf' 'packagekit')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-session-gtk2() {
  pkgdesc="Indicator showing session management, status and user switching - GTK 2 library"
  depends=('libindicator' 'libdbusmenu-gtk2' 'indicator-session')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  INDICATOR_VERSION=$(ls /usr/lib | grep libindicator | grep 'libindicator.so.[0-9]$' | awk -F'.' '{print $NF}')

  install -dm755 "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
  install -m755 'src/.libs/libsession.so' "${pkgdir}/usr/lib/indicators/${INDICATOR_VERSION}/"
}

# vim:set ts=2 sw=2 et:
