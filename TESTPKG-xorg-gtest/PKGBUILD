# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xorg-gtest
pkgver=0.7.1
_ubuntu_rel=0ubuntu1
pkgrel=1
pkgdesc="X.org Google Test Framework"
arch=('any')
url="https://launchpad.net/xorg-gtest"
license=('GPL')
groups=('unity-tests')
depends=('gtest-ubuntu' 'libx11' 'libxi' 'xf86-video-dummy')
makedepends=('doxygen' 'valgrind' 'xorg-server-devel')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xorg-gtest_${pkgver}.orig.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xorg-gtest_${pkgver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('2bd900b86337e904e074ea0b68b96d72529a85a2c88ade1a20a7b7baeaa26e8813e2bc8b45ef016dfe132487b10091c67ec786b160b711a37b92d219f71a85d1'
            '415ef1791976d5fdac89645e407526797e60f00519be8403e7df95cef1db58958e19d7f3b138a4e17fe30512f20d5b7d2108ee9becf503ae49b0db610a7d2639')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --prefix=/usr
  make
  make doc
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Tests currently fail
  XORG_GTEST_CHILD_STDOUT=true make -k -j1 check || :
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  rm -rvf "${pkgdir}/usr/include/gtest/"
}

# vim:set ts=2 sw=2 et:
