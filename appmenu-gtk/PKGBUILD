# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=appmenu-gtk
pkgname=('appmenu-gtk2' 'appmenu-gtk3')
pkgver=0.3.91
pkgrel=100
arch=('i686' 'x86_64')
url="https://launchpad.net/appmenu-gtk"
license=('LGPL')
makedepends=('gnome-common' 'gtk2-ubuntu' 'gtk3-ubuntu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/appmenu-gtk/0.4/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('e4237a2a1715405157502af57002e4092f8dbbd6b0339c1102bbf7322eda8c058262e64c3039f73c310e01cb62a887d35a732f8c64b13b235aadfc229a1368df')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk2 --disable-static
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_appmenu-gtk2() {
  pkgdesc="Adds appmenu support for GTK+ 2 apps"
  depends=('gtk2-ubuntu' 'libdbusmenu-gtk2')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d"
}

package_appmenu-gtk3() {
  pkgdesc="Adds appmenu support for GTK+ 3 apps"
  depends=('gtk3-ubuntu' 'libdbusmenu-gtk3')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu-gtk3" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu-gtk3"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d"
}
