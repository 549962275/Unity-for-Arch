# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=appmenu-gtk
pkgname=('appmenu-gtk2' 'appmenu-gtk3')
pkgver=12.10.2
pkgrel=100
arch=('i686' 'x86_64')
url="https://launchpad.net/appmenu-gtk"
license=('LGPL')
makedepends=('gnome-common' 'gtk2-ubuntu' 'gtk3-ubuntu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
groups=('unity')
options=(!libtool)
source=("http://launchpad.net/appmenu-gtk/12.10/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('5c3e3b82c9d18de5fa2013dad5cb1240e547a7f97295b01ce08b9f03ebaa5e165ed326271ceed7d7014dd0f5c7960e6633090bddf5d18bdf8306157acf39ccdd')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk2 --disable-static
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_appmenu-gtk2() {
  pkgdesc="Adds appmenu support for GTK+ 2 apps"
  depends=('libdbusmenu-gtk2')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d"
}

package_appmenu-gtk3() {
  pkgdesc="Adds appmenu support for GTK+ 3 apps"
  depends=('libdbusmenu-gtk3')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu-gtk3" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu-gtk3"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d"
}

# vim:set ts=2 sw=2 et:
