# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xf86-input-evdev-ubuntu
_ubuntu_rel=1ubuntu3
pkgver=2.6.99.901.${_ubuntu_rel}
pkgrel=100
pkgdesc="X.org evdev input driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'xorg-server-ubuntu' 'mtdev')
makedepends=('xorg-server-devel-ubuntu')
provides=('xf86-input-evdev')
conflicts=('xorg-server<1.11.0' 'xf86-input-evdev')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')
source=("${url}/releases/individual/driver/${pkgname%-*}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-input-evdev_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('2cf0a6399bf377cc2f47586d29e719ec7cd36eb6904ee3403b6c1dbaa352f3800248e3b41be1e0dc86968df0fa0ea7834a364c8cbf04b8dc239a55f5f20b8e1a'
            '58d1e0f1ab8240b8cfdced76af0807c1290b82fc3952bdd2cad4b9725ef219eb68aff82101bbde0556fb4efc9e9878edd6af72fedf13b3945852ea3cc99a5d78')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/xserver-xorg-input-evdev_${pkgver%.*}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  chmod +x autogen.sh
  ./autogen.sh --prefix=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
  install -dm755 "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 'debian/local/11-evdev-quirks.conf' "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m644 'debian/local/11-evdev-trackpoint.conf' "${pkgdir}/etc/X11/xorg.conf.d/"
}
