# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
pkgname=('launchpad-integration' 'liblaunchpad-integration' 'liblaunchpad-integration3' 'python-launchpad-integration')
pkgbase=launchpad-integration
pkgver=0.1.54
pkgrel=1
pkgdesc="Launchpad integration library to integrate launchpad urls into the menus."
arch=('i686' 'x86_64')
url="https://launchpad.net/launchpad-integration"
license=('LGPL')
makedepends=('gtk2-ubuntu' 'gtk3-ubuntu' 'pygtk' 'mono' 'gtk-sharp-2' 'gobject-introspection')
source=("http://archive.ubuntu.com/ubuntu/pool/main/l/${pkgbase}/${pkgbase}_${pkgver}.tar.gz"
        "https://github.com/mono/mono/blob/master/mcs/class/mono.snk?raw=true")
sha512sums=('b1a3bb2d5d7f45676bbcd5a86897804a39191118a50e4e41b85bfbe02df71a708ce98047987e204742bf73cd8607b869e5b5462841383825be964096362d05d5'
            '7f3c4a3bd3eb64769a24be714f603d2c69642d8ff636792b9fb57a5be9b93479d8fbb6872e3243cdc4cc50d9dfc402f55910da339b46e2caf307c59124547541')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  #Python 2 fix
  export PYTHON=python2

  #mono fix
  sed -i "/AssemblyKeyFile/ s@\".*\"@\"${srcdir}/mono.snk\"@g" lib/AssemblyInfo.cs
  mv "${srcdir}"/mono.snk\?raw\=true "${srcdir}/mono.snk"
  sed -i '/configdir/ s@/cli@@g' Makefile.am Makefile.in

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --with-gtk=2 --disable-static
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --with-gtk=3 --disable-static
  make
  popd
}

check() {
  msg "Nothing to check."
}

package_launchpad-integration() {
  pkgdesc="Provides an easy way to set menu items for an application using GtkUIManager"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  install -d -m755 "${pkgdir}/usr/bin/"
  install -d -m755 "${pkgdir}/usr/share/${pkgbase}/launchpadintegration/"
  install -d -m755 "${pkgdir}/usr/share/icons/hicolor/16x16/apps/"
  install -m755 'launchpad-integration' "${pkgdir}/usr/bin/"
  pushd ../launchpadintegration
  for i in *.py; do
    install -m644 "${i}" "${pkgdir}/usr/share/${pkgbase}/launchpadintegration/"
  done
  popd
  pushd ../pixmaps
  for i in *.png *.svg; do
    install -m644 "${i}" "${pkgdir}/usr/share/icons/hicolor/16x16/apps/"
  done
  popd
  make -C po DESTDIR="${pkgdir}/" install
}

package_liblaunchpad-integration() {
  pkgdesc="GTK+ 2.0 library for launchpad-integration"
  depends=('gtk2-ubuntu' 'launchpad-integration' 'mono' 'gtk-sharp-2')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"
  make DESTDIR="${pkgdir}/" install
  rm -rvf "${pkgdir}/usr/bin/"
  rm -rvf "${pkgdir}/usr/share/"
  rm -rvf "${pkgdir}"/usr/lib/python*
}

package_liblaunchpad-integration3() {
  pkgdesc="Gobject introspection information for the GTK+ 3.0 version of launchpad-integration"
  depends=('gtk3-ubuntu' 'launchpad-integration')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"
  make DESTDIR="${pkgdir}/" install
  rm -rvf "${pkgdir}/usr/bin/"
  rm -rvf "${pkgdir}/usr/share/"
}

package_python-launchpad-integration() {
  pkgdesc="Python bindings for launchpad-integration"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"
  PYTHON2VER=$(find /usr/lib -maxdepth 1 -type d -name python2* | sed -e 's/^.*\///g')
  install -d -m755 "${pkgdir}/usr/lib/${PYTHON2VER}/site-packages/gtk-2.0/LaunchpadIntegration/"
  install -m644 "../lib/__init__.py" "${pkgdir}/usr/lib/${PYTHON2VER}/site-packages/gtk-2.0/LaunchpadIntegration/"
  install -m755 'lib/.libs/_lpi.so' "${pkgdir}/usr/lib/${PYTHON2VER}/site-packages/gtk-2.0/LaunchpadIntegration/"
}
