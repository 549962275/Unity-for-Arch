# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_UBUNTU=true # Using Ubuntu patches?

pkgname=unity
pkgver=5.10.0

if [ "${_UBUNTU}" == true ]; then
  _ubuntu_rel=0ubuntu6
  pkgver="${pkgver}.${_ubuntu_rel}"
  _actual_ver="${pkgver%.*}"
else
  _actual_ver="${pkgver}"
fi

pkgrel=101
pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity/"
license=('GPL')
depends=('bamf' 'boost' 'ccsm-ubuntu' 'clutter-gtk' 'gconf-ubuntu' 'gjs' 'glib2-ubuntu' 'gnome-desktop' 'gnome-session-ubuntu' 'libgnomeui' 'libindicator' 'libnotify' 'libunique' 'libunity' 'libunity-misc' 'nux' 'python2-gconf' 'unity-asset-pool' 'vala' 'xorg-server>=1.12.0-100' 'libindicator3')
makedepends=('cmake' 'intltool' 'pkg-config' 'doxygen' 'gmock' 'gtest')
groups=('unity')
provides=("unity-core=${_actual_ver}")
conflicts=('unity-core')
install=unity.install
source=("http://launchpad.net/unity/${_actual_ver%%.*}.0/${_actual_ver}/+download/${pkgname}-${_actual_ver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}-${_ubuntu_rel}.diff.gz"
        # No longer using ompldr - too much downtime
        # kiwi6 is terrible too
        "launchpad_export.tar.gz::http://ubuntuone.com/4ShN81tFVBIHi3UrapNRtw" # Snapshot of translations from Launchpad as of 2012-04-26
        'fix_gtest_directory.patch'
        'launcher_bfb.png'
        'gcc_4.7_fix.patch')
sha512sums=('d93c3abbafe70bb09f4f4ef0d2a04072946fff59fffbff1bbf0680ce9643c039d9c07436f9746d1dacd2c0ec796eb3853b17ae3a8c47f3331b69da2a9b7e2bf3'
            '5682de20ba3df1d6681c3ad8c2bf8e7e68dd72d5d926841f9aa3090291adb828ac7750ea51cbafd403c6c15959705931ce40e043b57c435b3ed3061f24eb9e76'
            '20dad43d03d4e7c8c4f16c759b55045c105390ea4fa70311f8c89b1b2e2db83538fe8307bbdc8c5e482a805d892bc8c6175084c5ca3368166a244896e1bf7796'
            '5a7396d02e532eaa3fe57a03646274d716c53760967f0d8caeb323e451622a1ccf8f92c71c4b40c5c602016ae62d9d5df314d9015943586149b0432a1515d6fc'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054'
            '387f7b737732e2ab4489d29cddec5b904d266f006d3cbbb0467914cec8b21d93618f7825741492e0f213d01a5be360605822e468baaf21c1dd00246df6eef69f')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${_actual_ver}-${_ubuntu_rel}.diff"

  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Fix gtest directory
  patch -Np1 -i "${srcdir}/fix_gtest_directory.patch"

  # Cannot find gmodule
  CXXFLAGS="${CXXFLAGS} $(pkg-config --cflags --libs gmodule-2.0)"
  CFLAGS="${CFLAGS} $(pkg-config --cflags --libs gmodule-2.0)"

  # Fix pkgconfig
  #sed -i -e 's/\${libdir}/@LIBDIR@/g' -e 's/\${includedir}/@INCLUDEDIR@/g' UnityCore/unity-core.pc.cmake

  # Install translations from Launchpad
  pushd po
  rm LINGUAS && touch LINGUAS # Recreate LINGUAS (list of languages) file
  for i in "${srcdir}"/po/*.po; do
    _NEWFILE=${i##*/} # Strip path
    _NEWFILE=${_NEWFILE#*-} # Strip 'unity-'
    cp "${i}" "${_NEWFILE}" # Copy new translation
    echo "${_NEWFILE%.*}" >> LINGUAS # Add translation
  done
  cp "${srcdir}/po/unity.pot" unity.pot
  popd

  # Disable '-Werror'
  sed -i 's/[ ]*-Werror[ ]*//g' CMakeLists.txt services/CMakeLists.txt

  # GCC 4.7 build fix
  patch -Np1 -i "${srcdir}/gcc_4.7_fix.patch"

  mkdir -p build && cd build

  cmake \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}/build"
  make DESTDIR="${pkgdir}/" install

  # Install profile upgrade helper
  install -dm755 "${pkgdir}/etc/compizconfig/upgrades/"
  install -m644 ../debian/profile_upgrade/* \
    "${pkgdir}/etc/compizconfig/upgrades/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/migrate_favorites.py" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/5/launcher_bfb.png"
}

# vim:set ts=2 sw=2 et:
