# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.4.0
_extra_ver=+16.04.20151218
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs gnome-desktop
         gnome-screensaver gnome-session-ubuntu hud ido libgnomeui libindicator
         libindicator3 libnotify libunique libunity libunity-misc libxfixes nux
         unity-asset-pool unity-scope-home libxi zeitgeist-ubuntu
         lightdm-unity-greeter)
makedepends=(chrpath cmake doxygen intltool patchutils pkg-config
             python2-distribute)
groups=(unity)

#options=(!ccache)
install=unity.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Revert-r3134-Remove-Systray-Whitelist.patch
        0002-Disable-Werror.patch
        0003-Fix-launchers.patch
        0004-Remove-Upstart-stuff.patch
        0005-Remove-social.scope.patch
        0006-Remove-xpathselect-dependency.patch
        com.ubuntu.user-interface.gschema.xml
        launcher_bfb.png
        99unity-panel-service
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam)
sha512sums=('462e7f47fa2aed8f712d2737260ffd102efd6756db76c98bcc0a617cc7407ea6e8483fdec039eab37ee043af6e8e45034f63410e7e81cf1d923e2ce558691678'
            '157777ded0dd1e7fbe90cfc5af63cf300dbfbb5e7d19cad2b742b3cb7c2116a8114c7584d929c33ba38dca35a586e000740b3af0d00ced11a85adba6943ce045'
            'fcbd6b4a6d0ac0670a1073884529de0f120f8a165bc5a579e4945b40acc0cebf214d5ce2fea126764cf3b18e05ee3d87003fc4959fcc80fdeb1bae7771da9af2'
            'f13598aa5fd0b66641f482c1a132db53075625243d9838f0214798935f814f3d4e3fbee9237ed94054541706919c05d226805653aa4aaa6a6649db81663ea3ab'
            '1de30a75fda694167d497769d8fd8382653da5aff6e0c06d47a5a9e352fb5f417a793de59bf81d4ae05a5fd41b4a141a9026186e55a0dcc46db0e03fb1183384'
            '74567e32d9c838b89a65849338410380aeae7ffc30cf1abcc82caabd56be5822b2ade9094bf5e5dc094680caa386412fb0501597fa2d936a1c60cb7e3dff95a3'
            '18d29fc30f6c28ba42d6cb548357010bea87eddc624f1ad943b51af5c97030c6920d5bec95dd69e79d7e3d7d836c8cd9064d4e03732d4153ee93dce0d1f1f18e'
            '3e583056de2f3290eced4a91eddc71da328c3c0e4cf2a075a125851de9662afa588c37db69002dff8501ecdca3bc4904900a0b4bd05941f3302d604353ec5f8d'
            '126ed309012d60ef51da95b115d877f65c8f8dee415be781ad9f6c0efedc5639227f72218d9c790f5d00937378ba62691d0d29fd9ac757d75dd156fd4f4b7eba'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            '5bd357e03392c6abf76b51df78e59eafb1f04e997465fe1b73c02a8e265306b020bbdb05e66d959fb44a3df1aaf24559d70289aedf6803c917ca689ec848bf66'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            '1067bcb25b6d6d01256b176b5854d1ace700ba2b7323b4af257aa95d2f47d5043ac22811f65e99f1e961772cd1e81c153ef69b162918827bd9d7d50d458908df')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0001-Revert-r3134-Remove-Systray-Whitelist.patch
    patch -p1 -i ../0002-Disable-Werror.patch
    patch -p1 -i ../0003-Fix-launchers.patch
    patch -p1 -i ../0004-Remove-Upstart-stuff.patch
    patch -p1 -i ../0005-Remove-social.scope.patch
    patch -p1 -i ../0006-Remove-xpathselect-dependency.patch

    # Apply Ubuntu patches
    patch -p1 -i "../unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
    for i in $(grep -v '#' debian/patches/series); do
        patch -p1 -i "debian/patches/${i}"
    done
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    #export CXXFLAGS+=" -lc"

    # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
    export CCACHE_SLOPPINESS=time_macros

    [[ -d build ]] && rm -rvf build/
    mkdir build/ && cd build/

    cmake -Wno-dev \
        -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
        -DCOMPIZ_PACKAGING_ENABLED=TRUE \
        -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SYSCONFDIR=/etc \
        -DUSE_GSETTINGS=TRUE \
        -DENABLE_UNIT_TESTS=FALSE \
        ..

    make

    # Make sure that the GSettings schema files were created
    gen=generated/glib-2.0/schemas
    if \
        [ ! -f ${gen}/org.compiz.networkarearegion.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitydialog.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitymtgrabhandles.gschema.xml ] ||
        [ ! -f ${gen}/org.compiz.unityshell.gschema.xml ]; then
        make compiz_gsettings_compile_local
    fi
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
    make DESTDIR="${pkgdir}/" install

    # Install profile convert files
    install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
    install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

    # Taken from Ubuntu source package's debian/rules file
    find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
    rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
    rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
    rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
    rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

    # Fix Python 2 scripts
    sed -i 's|^\(#!.*python$\)|\12|g' \
        "${pkgdir}/usr/bin/unity" \
        "${pkgdir}/usr/lib/unity/makebootchart.py"

    # Arch Linux logo
    install -m644 "${srcdir}/launcher_bfb.png" \
        "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

    install -m644 "${srcdir}/com.ubuntu.user-interface.gschema.xml" \
        "${pkgdir}/usr/share/glib-2.0/schemas/"

    # Fix launching of unity-panel-service (from unity-gentoo project)
    install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
    install -m755 "${srcdir}/99unity-panel-service" \
                  "${pkgdir}/etc/X11/xinit/xinitrc.d/"

    # Install script for running Unity with debug output (from unity-gentoo project)
    install -m755 "${srcdir}/unity-debug" "${pkgdir}/usr/bin/"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
  
    # Fix insecure rpath in libunityshell.so
    chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib/compiz/libunityshell.so"

    # FIRE!
    install -m755 "${srcdir}/run_this_if_unity_is_on_fire.py" "${pkgdir}/usr/bin/"

    # Install PAM configuration file
    rm "${pkgdir}/etc/pam.d/unity"
    install -m644 "${srcdir}/unity.pam" "${pkgdir}/etc/pam.d/unity"
}
