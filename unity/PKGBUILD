# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.2.0
_extra_ver=+14.10.20140606.1
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('bamf' 'boost' 'cairo-ubuntu' 'compiz-ubuntu>=0.9.11' 'clutter-gtk' 'gjs' 'gnome-desktop' 'gnome-screensaver' 'gnome-session-ubuntu' 'hud' 'ido' 'libgnomeui' 'libindicator' 'libindicator3' 'libnotify' 'libunique' 'libunity' 'libunity-misc' 'libxfixes' 'nux>=4.0.6' 'unity-asset-pool' 'unity-scope-home' 'libxi' 'xpathselect' 'zeitgeist-ubuntu' 'lightdm-unity-greeter')
makedepends=('chrpath' 'cmake' 'doxygen' 'intltool' 'patchutils' 'pkg-config' 'python2-distribute')
groups=('unity')
provides=("unity-core=${_actual_ver}")
conflicts=('unity-core')

options=('!ccache')
install=unity.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        '0001-Disable-tests.patch'
        '0002-Arch-Linux-branding.patch'
        '0003-Revert-r3134-Remove-Systray-Whitelist.patch'
        '0004-Disable-Werror.patch'
        '0005-Fix-launchers.patch'
        '0006-Remove-Upstart-stuff.patch'
        '0007-Remove-social.scope.patch'
        'com.ubuntu.user-interface.gschema.xml'
        'launcher_bfb.png'
        '10_unity.gschema.override'
        '99unity-panel-service'
        'unity-debug')
sha512sums=('9c95da592d9e6e081a365191a1c3fda06f3ebe96b9837942f164b9850d1ad1d47e682ee1b8e04ca712db445efa67189e657a42d84ff44cbfbad2528bf8c0232e'
            '4114c00f772d21b12848c0e26367b51e8d0ade2c6a7b83b71852d85d231bd8b220c64005cb7f4213e91fe1697fb1629d5c3ff870bbfe4fc0b9ef8d064ee6eb50'
            '099d0d8825202875936dd68c1aa45ebcf6aafeaedff94e2ffd3cfc29371204752c476a44a64e5f02d980d20b89452444570b75beb699c6f6e7d27b09444b1445'
            'a59c9a0f01a484264fd1707d26e65c8cf4e367c113d24b6538b62f6871dac98f72bc6074099153a07dcf9151804fdfe68d694a1e8e3fa533c7f3fc59e29ff9ec'
            '8b242209c48a884c5384ac37e8e6193295815002b49ae8f2fa3f75847e7e9f72429b2c8fcabee0fbcea0af50b876bf9f84cc1d2b8f0624f4326cbb423663d3a7'
            'c88f127ec87e8ed8a91604e1a766d66753097c35f3653f033ba6aec7970b6b9cd834f7d8056a64fb4c4200db5f221c7e2a53bbff66f165f489f2e8ef9eab38b5'
            '0c81b0889a131c38eb0a424a85609024e8416d18dc43088c91d568afe602d4f845348246c4c800dabdd90c801dc32a13b5b0c4d68508070ba0b2e0bff610c882'
            '81151a09c36517690999488e85a19c2109bdd02b42d1b9748727ba905dd26769c52c35d7909041f814ead8ff75d874109bd6511787734a44c5868e889e92696c'
            '9af54da788fdda80679711c0c15658c044b11be9a0f5fb8066d9b0e36818a436138a9e56a3d9e20d6af061135cfd8c6def0adf84c9b5bc7ce9b770aed75bc5c7'
            '126ed309012d60ef51da95b115d877f65c8f8dee415be781ad9f6c0efedc5639227f72218d9c790f5d00937378ba62691d0d29fd9ac757d75dd156fd4f4b7eba'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            '872c48f85350d2d815418afab340946f85ec1f9f3e1ab748fc1199df342df52642447736a5b783fb17eb324a961bbf8badc4e8401512e7182e4ff2e25b53d3a1'
            '5bd357e03392c6abf76b51df78e59eafb1f04e997465fe1b73c02a8e265306b020bbdb05e66d959fb44a3df1aaf24559d70289aedf6803c917ca689ec848bf66'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Disable-tests.patch"
  patch -p1 -i "${srcdir}/0002-Arch-Linux-branding.patch"
  patch -p1 -i "${srcdir}/0003-Revert-r3134-Remove-Systray-Whitelist.patch"
  patch -p1 -i "${srcdir}/0004-Disable-Werror.patch"
  patch -p1 -i "${srcdir}/0005-Fix-launchers.patch"
  patch -p1 -i "${srcdir}/0006-Remove-Upstart-stuff.patch"
  patch -p1 -i "${srcdir}/0007-Remove-social.scope.patch"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
  export CCACHE_SLOPPINESS=time_macros

  [[ -d build ]] && rm -rvf build/
  mkdir build/ && cd build/

  cmake -Wno-dev \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_GSETTINGS=TRUE \
    ..

  make -j1

  # Make sure that the GSettings schema files were created
  pushd generated/glib-2.0/schemas/
  if \
    [ ! -f org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f org.compiz.unitymtgrabhandles.gschema.xml ] ||
    [ ! -f org.compiz.unityshell.gschema.xml ]; then
    make compiz_gsettings_compile_local
  fi
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install

  # Install profile convert files
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

  # Change window dragging key back to Alt. Super conflicts with Unity's other
  # hotkeys.
  install -m644 "${srcdir}/10_unity.gschema.override" \
    "${pkgdir}/usr/share/glib-2.0/schemas/"

  install -m644 "${srcdir}/com.ubuntu.user-interface.gschema.xml" \
    "${pkgdir}/usr/share/glib-2.0/schemas/"

  # Fix launching of unity-panel-service (from unity-gentoo project)
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/99unity-panel-service" \
                "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # Install script for running Unity with debug output (from unity-gentoo project)
  install -m755 "${srcdir}/unity-debug" "${pkgdir}/usr/bin/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
  
  # Fix insecure rpath in libunityshell.so
  chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib/compiz/libunityshell.so"
}

# vim:set ts=2 sw=2 et:
