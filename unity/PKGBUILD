# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.4.0
_extra_ver=+16.04.20160715
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=5
epoch=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(appstream-glib bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs
         gnome-desktop gnome-screensaver gnome-session-ubuntu
         gsettings-ubuntu-schemas hud ido libgnomeui libindicator-gtk2
         libindicator-gtk3 libnotify libunique libunity libunity-misc libxfixes
         nux unity-asset-pool unity-scope-home libxi zeitgeist-ubuntu
         lightdm-unity-greeter upstart-user)
makedepends=(chrpath cmake doxygen intltool patchutils pkg-config
             python2-distribute)
groups=(unity)

#options=(!ccache)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Revert-r3134-Remove-Systray-Whitelist.patch
        0002-Disable-Werror.patch
        0003-Fix-launchers.patch
        0005-Remove-social.scope.patch
        0006-Remove-xpathselect-dependency.patch
        launcher_bfb.png
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam
        ubuntu.gnome-session
        ubuntu.xsession)
sha512sums=('26b5cd62109bc08d8aa477266852582358e50ff7e18eb03b58ed0510b8fb4df2e3bef0d5c3a9efb572900e8955383ea43cb61867e1760d54f94189e0e18b2f6b'
            '79ba62320624a25f5d5f9677addf0260e57fe01a7560b9ab849b3db62a2c37d2e799c58250137e04a0898c215246869b6b9af732c7a6d104c8999264971c0ba7'
            '7785eabcac75b106a527342786fc8cd0d59a1114d949fb154aa00d58163cbe7a2de030be480474b02e1129fa38bc0393163a519c3180ed918fda6d8e753d92a8'
            'ec0d228b4a117883e5dfbca0404614bd107d27d709554cde5a4cbe946a500381ff729a7a5942fbbfc9c449d0d404bde343ff5e536e390c8146df5bca243b781e'
            '1f1cbd726b17d27ed6b5852c81c12f3e58e25dd381da20e19e4c912ab53de1ebca7a95d8868d615e366c404a9b04d178cb18803d674837e88e77dd0c5524cf2c'
            'f164ebfbad64eef16276b129d073e9411a511bba974b6c4616cc801c582ead5467cca2944755a3294a4a14d4b38b72c7025562e8cf145c855205193f5cfcb9de'
            '8ae32bb07dad5bcb264e3de621b29b1bd535fa80c205098990896f1c16f99cfa889234d55b4ce505b5ab5141c64269d9c5775a9666a45f65c699aa4f0382e94d'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            '08d43702e1e356725096e4dbf9f6b3833b21b5ff88cca4db35097743acba157b37478baad19e66bcc61e01c8d3bfdbe44a4958808860b22d54fb38309bf9f011'
            '975fa1c67092dc9b9dff639a05891f1a15131e3756d88cfe6534649644a7a09f78ec1151c70d56957906c51c73b29faaf83a33e26eca720867b6742f6bdc588f'
            '4cfba3ad561cc73b60ae3622b4ee968f283288249c17a541afbb856c24cfad1326ed591dea5fc93e3d2ae93fb59b80db691838905651bd7e4741959461f19978')

prepare() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i 0001-Revert-r3134-Remove-Systray-Whitelist.patch
    patch -p1 -i 0002-Disable-Werror.patch
    patch -p1 -i 0003-Fix-launchers.patch
    patch -p1 -i 0005-Remove-social.scope.patch
    patch -p1 -i 0006-Remove-xpathselect-dependency.patch

    # Apply Ubuntu patches
    patch -p1 -i "unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
    for i in $(grep -v '#' debian/patches/series); do
        patch -p1 -i "debian/patches/${i}"
    done
}

build() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}"

    #export CXXFLAGS+=" -lc"

    # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
    export CCACHE_SLOPPINESS=time_macros

    [[ -d build ]] && rm -rvf build/
    mkdir build/ && cd build/

    cmake -Wno-dev \
        -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
        -DCOMPIZ_PACKAGING_ENABLED=TRUE \
        -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc \
        -DUSE_GSETTINGS=TRUE \
        -DENABLE_UNIT_TESTS=FALSE \
        ..

    make

    # Make sure that the GSettings schema files were created
    gen=generated/glib-2.0/schemas
    if \
        [ ! -f ${gen}/org.compiz.networkarearegion.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitydialog.gschema.xml ] || \
        [ ! -f ${gen}/org.compiz.unitymtgrabhandles.gschema.xml ] ||
        [ ! -f ${gen}/org.compiz.unityshell.gschema.xml ]; then
        make compiz_gsettings_compile_local
    fi
}

package() {
    #cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
    cd build
    make DESTDIR="${pkgdir}/" install

    # Install profile convert files
    install -dm755 "${pkgdir}"/usr/lib/compiz/migration/
    install -m644 ../tools/convert-files/* \
        "${pkgdir}"/usr/lib/compiz/migration/

    # Taken from Ubuntu source package's debian/rules file
    find "${pkgdir}"/usr/lib -name '*.*a' -exec rm {} \;

    # Fix Python 2 scripts
    sed -i '1s/^#!.*python$/&2/g' \
        "${pkgdir}"/usr/bin/unity \
        "${pkgdir}"/usr/lib/unity/makebootchart.py

    # Arch Linux logo
    install -m644 "${srcdir}"/launcher_bfb.png \
        "${pkgdir}"/usr/share/unity/icons/launcher_bfb.png

    # Install script for running Unity with debug output (from unity-gentoo project)
    install -m755 "${srcdir}"/unity-debug "${pkgdir}"/usr/bin/

    # Use language packs
    rm -r "${pkgdir}"/usr/share/locale/
  
    # Fix insecure rpath in libunityshell.so
    chrpath --replace /usr/lib/libunity:/usr/lib/compiz \
        "${pkgdir}"/usr/lib/compiz/libunityshell.so

    # FIRE!
    install -m755 "${srcdir}"/run_this_if_unity_is_on_fire.py \
        "${pkgdir}"/usr/bin/

    # Install PAM configuration file
    rm "${pkgdir}"/etc/pam.d/unity
    install -m644 "${srcdir}"/unity.pam "${pkgdir}"/etc/pam.d/unity

    # Install session files
    install -dm755 "${pkgdir}"/usr/share/gnome-session/sessions/
    install -m644 "${srcdir}"/ubuntu.gnome-session \
        "${pkgdir}"/usr/share/gnome-session/sessions/ubuntu.session
    install -dm755 "${pkgdir}"/usr/share/xsessions/
    install -m644 "${srcdir}"/ubuntu.xsession \
        "${pkgdir}"/usr/share/xsessions/ubuntu.desktop

    # Install upstart service
    install -dm755 "${pkgdir}"/usr/share/upstart/sessions/
    sed 's/ubuntu\.session/unity.session/g' \
        < ../debian/unity7.conf \
        > "${pkgdir}"/usr/share/upstart/sessions/unity7.conf
}
