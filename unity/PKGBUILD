# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=vivid
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.3.1
_extra_ver=+15.04.20150219.2
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs gnome-desktop
         gnome-screensaver gnome-session-ubuntu hud ido libgnomeui libindicator
         libindicator3 libnotify libunique libunity libunity-misc libxfixes nux
         unity-asset-pool unity-scope-home libxi zeitgeist-ubuntu
         lightdm-unity-greeter)
makedepends=(chrpath cmake doxygen intltool patchutils pkg-config
             python2-distribute)
groups=(unity)

#options=(!ccache)
install=unity.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Disable-tests.patch
        0002-Revert-r3134-Remove-Systray-Whitelist.patch
        0003-Disable-Werror.patch
        0004-Fix-launchers.patch
        0005-Remove-Upstart-stuff.patch
        0006-Remove-social.scope.patch
        0007-Remove-xpathselect-dependency.patch
        0008-Fix-missing-include-for-shared_ptr.patch
        com.ubuntu.user-interface.gschema.xml
        launcher_bfb.png
        99unity-panel-service
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam)
sha512sums=('420edb04e5c8f50f08a8fb35b9a9c1a6d3404c3fddb7dee53a9e5ab1d07edc25b397e882f32137347cbc4c4fdbc762111a1ba9ef4d87195c777c81e5e8791e18'
            'fb1a435f1a44bcecc27cd0f05042d44b7f6be2ac2dfd3ae62ac122f3e5908f6fedb36c6549a2f222c4aa9a3fdb82a33af8002cb88ed1b3f19e7ace319ebc97c1'
            '73a097a9b10ae443cb02425c8cd1d15902db1e99e9f1f2ad601467317a1e09eec5a47d28d8472e825da6a6ededcf02fbc26da96864f6bd0b11df9fd69802da12'
            '8ebc9fa3ac3fb03ac87ab77e3d42d8e4e465c24e0a3adec68f78a10c1aaacd221f641adb40f8d909778388a2f0a0cd18decde79e1f103c5f384d8911c98929c0'
            '799ea8bca5ce8493eaf390232ec2ce6d459e886098431eff5ff072a808f5995d7401e2062bf79365dfdb141cfc044e6ae7204430f530187adfc96b13978b3d91'
            '796ac81bf0ae31c18a90ae0e30d1a927d8cc6aafc98e21bc638999e2286c34e7c65e5691d29feecd322e9cec162c1afbf5e12d83c428a3ddfd8b12bb7ea4669a'
            'd551c5df211f6871559266f9e031f5818a1b0435ef8b21d0b8bb4804ff3a43363dc1aa2773ff9d75c6988cd07b7f561c92fd2e9e901d62d14f06aea6c10a7ee8'
            'e2c93d43f71f45aecaa85b87d6ec3a3c2380be753f85fd477b0141140dfa1a47dab150f76b90a39874ac75e21aa62113eabd8672362c1a3bdb270054f8b072db'
            '7d9e21acf6cda3bd51507ecbabe5337086e23be747a5c9c5e4253b7c1ad7cb5b6280af9d32672c1e6128108093617e772bb7227426361450cba53d9daa6759ef'
            'f961b071cd0106ad96927a563356db45c782921acab4c452b546f3afbb795fa1184b1dd20e0f6dac5f1e2d5b24ff5a6f70d646757dfba001e3d5293f7806f033'
            '126ed309012d60ef51da95b115d877f65c8f8dee415be781ad9f6c0efedc5639227f72218d9c790f5d00937378ba62691d0d29fd9ac757d75dd156fd4f4b7eba'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            '5bd357e03392c6abf76b51df78e59eafb1f04e997465fe1b73c02a8e265306b020bbdb05e66d959fb44a3df1aaf24559d70289aedf6803c917ca689ec848bf66'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            '1067bcb25b6d6d01256b176b5854d1ace700ba2b7323b4af257aa95d2f47d5043ac22811f65e99f1e961772cd1e81c153ef69b162918827bd9d7d50d458908df')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Disable-tests.patch
  patch -p1 -i ../0002-Revert-r3134-Remove-Systray-Whitelist.patch
  patch -p1 -i ../0003-Disable-Werror.patch
  patch -p1 -i ../0004-Fix-launchers.patch
  patch -p1 -i ../0005-Remove-Upstart-stuff.patch
  patch -p1 -i ../0006-Remove-social.scope.patch
  patch -p1 -i ../0007-Remove-xpathselect-dependency.patch
  patch -p1 -i ../0008-Fix-missing-include-for-shared_ptr.patch

  # Apply Ubuntu patches
  patch -p1 -i "../unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CXXFLAGS+=" -lc"

  # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
  export CCACHE_SLOPPINESS=time_macros

  [[ -d build ]] && rm -rvf build/
  mkdir build/ && cd build/

  cmake -Wno-dev \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SYSCONFDIR=/etc \
    -DUSE_GSETTINGS=TRUE \
    ..

  make -j1

  # Make sure that the GSettings schema files were created
  pushd generated/glib-2.0/schemas/
  if \
    [ ! -f org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f org.compiz.unitymtgrabhandles.gschema.xml ] ||
    [ ! -f org.compiz.unityshell.gschema.xml ]; then
    make compiz_gsettings_compile_local
  fi
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install

  # Install profile convert files
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

  install -m644 "${srcdir}/com.ubuntu.user-interface.gschema.xml" \
    "${pkgdir}/usr/share/glib-2.0/schemas/"

  # Fix launching of unity-panel-service (from unity-gentoo project)
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/99unity-panel-service" \
                "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # Install script for running Unity with debug output (from unity-gentoo project)
  install -m755 "${srcdir}/unity-debug" "${pkgdir}/usr/bin/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
  
  # Fix insecure rpath in libunityshell.so
  chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib/compiz/libunityshell.so"

  # FIRE!
  install -m755 "${srcdir}/run_this_if_unity_is_on_fire.py" "${pkgdir}/usr/bin/"

  # Install PAM configuration file
  rm "${pkgdir}/etc/pam.d/unity"
  install -m644 "${srcdir}/unity.pam" "${pkgdir}/etc/pam.d/unity"
}
