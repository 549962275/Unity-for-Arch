# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=unity
_actual_ver=7.3.1
_extra_ver=+15.04.20141216
_ubuntu_rel=0ubuntu1
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
epoch=1

pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=(i686 x86_64)
url="https://launchpad.net/unity"
license=(GPL)
depends=(bamf-ubuntu boost cairo compiz-ubuntu clutter-gtk gjs gnome-desktop
         gnome-screensaver gnome-session-ubuntu hud ido libgnomeui libindicator
         libindicator3 libnotify libunique libunity libunity-misc libxfixes nux
         unity-asset-pool unity-scope-home libxi zeitgeist-ubuntu
         lightdm-unity-greeter)
makedepends=(chrpath cmake doxygen intltool patchutils pkg-config
             python2-distribute)
groups=(unity)

#options=(!ccache)
install=unity.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001-Disable-tests.patch
        0002-Revert-r3134-Remove-Systray-Whitelist.patch
        0003-Disable-Werror.patch
        0004-Fix-launchers.patch
        0005-Remove-Upstart-stuff.patch
        0006-Remove-social.scope.patch
        0007-Remove-xpathselect-dependency.patch
        0008-Fix-missing-include-for-shared_ptr.patch
        com.ubuntu.user-interface.gschema.xml
        launcher_bfb.png
        99unity-panel-service
        unity-debug
        run_this_if_unity_is_on_fire.py
        unity.pam)
sha512sums=('725fa7371361d14e923a13040ffb877622a1027c72fddd8c4e6decf8160dd9eca9508ab4dd51da02f0dbde40ee0e1a7de3eb1c58d6a5826476262e9e3b9ad5b2'
            '484231c8275d2ee2adc78efab847b805d5165962eaebda08c1fe2e8982a7222732472f4c956284f5623257d9cd2f338b7ebeed3838e844da8a605738149261b3'
            'cd6792d69cf9f55f65656c239e6f1d58fe2b4910fb52c4d7b8cf541152894203623a78e16d53d3e5bae16f1f7918b1209d1eb01275db275e353e622dcd910dcb'
            'a19b60a67581cb568d0115db67b74e8e5d8c927fa260bac6e29cd8f32857525a19a90563b36c1a0ed314dfb2bb73e067e932904d0d0633153d6a8fcae002a9bc'
            'b06bfce972e86490b5f65c003c0ecc755a86d4d4769d9470be69bec1c19d9d7f52ac91751ef9b5e64712c571853e10a3ef74b0a372db888ff2b31c1fc0d6dacc'
            '79f94574287e6437d471d08bff47c0184ce793ad686ada3280d0ac59317d7028f7e006b9b4b99ede636a6e21a5b1968a862198b17524084cb86874ba1b16ed84'
            '49de1aeb539a5dba5e755f14ce9ce9b424eceaf394e47bebeb0039ed8a8e11f9f00fdec47d66eab9248d2cdf1cabc1e69d21df41a55e2cf7cbbd0b73d2279f94'
            'e3e14e88528f1f84155e84c2fa1be60502eb7ebde856aa747faf0534fb450aecabd49ff51e0313b3bb3be5ad61bda45ca17ac678321c28aeda1928ca6d7f52ab'
            'da05c181b5c6187fc879c8965646d43b24990eb56c662d3fd36d20aacb3895ae8114918af22e23d1c0ab379a4adb403eceb6e67cf86018e88555301be7d1237b'
            'd8f4cc8c5d97ad15599fa0bd9f355daf6d22899cdf9d324b3169ed2f2da4f66ce51b3b4714f9e7cb5574c8220478218d15f38675711b0f56440cce6f4683f088'
            '126ed309012d60ef51da95b115d877f65c8f8dee415be781ad9f6c0efedc5639227f72218d9c790f5d00937378ba62691d0d29fd9ac757d75dd156fd4f4b7eba'
            'ec9ea91d79129b23aae6c4b9584fb396ecc572a0bafcac6229cc413ee441f610cb51ffce9383544c9fc62e747d5718be9be050850943eac4820095f190dc0ed0'
            '5bd357e03392c6abf76b51df78e59eafb1f04e997465fe1b73c02a8e265306b020bbdb05e66d959fb44a3df1aaf24559d70289aedf6803c917ca689ec848bf66'
            'b217490bf5eb5b0e83dbc9737b80b2ab9d7288362f370625f3c99d5a862c0e1947376b9fab4fdc9c2ddd4e8bd6e8d2ff5763a3a4391732f0781abbc60b159db5'
            'f76ce70ee8ad4a83c94a5919fcabadf7f82e90cf3c70079344e8ab4069dd41f9055f86e01ee5d96ff52819ed9979bdbe85c59e0d50aa4c511e5a20e19a9ea06d'
            '1067bcb25b6d6d01256b176b5854d1ace700ba2b7323b4af257aa95d2f47d5043ac22811f65e99f1e961772cd1e81c153ef69b162918827bd9d7d50d458908df')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i ../0001-Disable-tests.patch
  patch -p1 -i ../0002-Revert-r3134-Remove-Systray-Whitelist.patch
  patch -p1 -i ../0003-Disable-Werror.patch
  patch -p1 -i ../0004-Fix-launchers.patch
  patch -p1 -i ../0005-Remove-Upstart-stuff.patch
  patch -p1 -i ../0006-Remove-social.scope.patch
  patch -p1 -i ../0007-Remove-xpathselect-dependency.patch
  patch -p1 -i ../0008-Fix-missing-include-for-shared_ptr.patch

  # Apply Ubuntu patches
  patch -p1 -i "../unity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CXXFLAGS+=" -lc"

  # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
  export CCACHE_SLOPPINESS=time_macros

  [[ -d build ]] && rm -rvf build/
  mkdir build/ && cd build/

  cmake -Wno-dev \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SYSCONFDIR=/etc \
    -DUSE_GSETTINGS=TRUE \
    ..

  make -j1

  # Make sure that the GSettings schema files were created
  pushd generated/glib-2.0/schemas/
  if \
    [ ! -f org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f org.compiz.unitymtgrabhandles.gschema.xml ] ||
    [ ! -f org.compiz.unityshell.gschema.xml ]; then
    make compiz_gsettings_compile_local
  fi
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install

  # Install profile convert files
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

  install -m644 "${srcdir}/com.ubuntu.user-interface.gschema.xml" \
    "${pkgdir}/usr/share/glib-2.0/schemas/"

  # Fix launching of unity-panel-service (from unity-gentoo project)
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/99unity-panel-service" \
                "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # Install script for running Unity with debug output (from unity-gentoo project)
  install -m755 "${srcdir}/unity-debug" "${pkgdir}/usr/bin/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
  
  # Fix insecure rpath in libunityshell.so
  chrpath --replace /usr/lib/libunity:/usr/lib/compiz "$pkgdir/usr/lib/compiz/libunityshell.so"

  # FIRE!
  install -m755 "${srcdir}/run_this_if_unity_is_on_fire.py" "${pkgdir}/usr/bin/"

  # Install PAM configuration file
  rm "${pkgdir}/etc/pam.d/unity"
  install -m644 "${srcdir}/unity.pam" "${pkgdir}/etc/pam.d/unity"
}
