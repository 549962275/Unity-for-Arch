# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_UBUNTU=true # Using Ubuntu patches?

pkgname=unity
pkgver=5.6.0

if [ "${_UBUNTU}" == true ]; then
  _ubuntu_rel=0ubuntu4
  pkgver="${pkgver}.${_ubuntu_rel}"
  _actual_ver="${pkgver%.*}"
else
  _actual_ver="${pkgver}"
fi

pkgrel=101
pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity/"
license=('GPL')
depends=('atk' 'bamf' 'boost' 'cairo' 'ccsm-ubuntu' 'clutter' 'clutter-gtk' 'compiz-core-ubuntu' 'dbus-glib' 'dee' 'gconf-ubuntu' 'gjs' 'glib2-ubuntu' 'gnome-desktop' 'gnome-session-ubuntu' 'gobject-introspection' 'gtest' 'gtk2-ubuntu' 'gtk3-ubuntu' 'json-glib' 'libdbusmenu' 'libgee' 'libgnomeui' 'libindicator' 'libnotify' 'libunique' 'libunity' 'libunity-misc' 'libxcb' 'libxslt' 'nux' 'pango' 'python2-gconf' 'startup-notification' 'unity-asset-pool' 'utouch-geis' 'utouch-grail' 'vala')
makedepends=('cmake' 'intltool' 'pkg-config' 'doxygen' 'gmock')
groups=('unity')
provides=("unity-core=${_actual_ver}")
conflicts=('unity-core')
install=unity.install
source=("http://launchpad.net/unity/${_actual_ver%%.*}.0/${_actual_ver}/+download/${pkgname}-${_actual_ver}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}-${_ubuntu_rel}.diff.gz"
        # No longer using ompldr - too much downtime
        # kiwi6 is terrible too
        "launchpad_export.tar.gz::http://ubuntuone.com/1uOWRNegG0LvwZKnerHoMk" # Snapshot of translations from Launchpad as of 2012-03-03
        'fix_test-gtest-xless.patch'
        'fix_gtest_directory.patch'
        'launcher_bfb.png')
sha512sums=('224e73c425900007cee4c23d52c8c511a6299879b0288281b51925b0d67933f3cac00fb6cfb7ebaeec19a02980fd3086fceebcc2827be10c1e973f5fcf4a13f9'
            'b00b12432a7db2221fdf54a1c55d331f58e82264309306898a27e7b87a2ec096e9974bdcf7fd7bcef6f2a726bbab0a08b0cf4e980a0ccb5fd4c250e0d67795d7'
            'f80df915dfc13502a96a19b5b62c1c1a0bb626d91f4a3559b3c2877ce5356bd69e60f618f026aecc281edf87c6411196a1558763143e610d0f2d18b2892c74b8'
            'dab7371e5c353ea3a95bf37d768c4cd0a530280d1560f043387d037fec5b6a028c155a0c10bfa982fbe5d07ba11c229a69724206cee8f21be9aa249ed213f5a5'
            '6c8e76017ca90ed27f82066b5c75c1d1dee508c48508ffc29786a52202beb2ca16259cf9f4f2db517517bf22c67d37e1a733b1c4c6cae051c12ecebd855b70df'
            '9f5469c0300d0c33e26db670771803efd3d821e82689fbb1391a31b8fd0e280a12ea010fe6f32a702a5d1797918d416c8adabfccf6049e43b496ebb6dbe2c054')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${_actual_ver}-${_ubuntu_rel}.diff"

  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Disable tests for now; fails to build with:
  # Built target test-gtest-dbus
  # /usr/bin/ld: CMakeFiles/test-gtest-xless.dir/test_grabhandle.cpp.o: undefined reference to symbol 'pthread_setspecific@@GLIBC_2.2.5'
  # /usr/bin/ld: note: 'pthread_setspecific@@GLIBC_2.2.5' is defined in DSO /lib/libpthread.so.0 so try adding it to the linker command line
  # /lib/libpthread.so.0: could not read symbols: Invalid operation
  #sed -i '/tests/d' CMakeLists.txt

  # Fix tests
  patch -Np1 -i "${srcdir}/fix_test-gtest-xless.patch"

  # Fix gtest directory
  patch -Np1 -i "${srcdir}/fix_gtest_directory.patch"

  # Fix pkgconfig
  #sed -i -e 's/\${libdir}/@LIBDIR@/g' -e 's/\${includedir}/@INCLUDEDIR@/g' UnityCore/unity-core.pc.cmake

  # Install translations from Launchpad
  pushd po
  rm LINGUAS && touch LINGUAS # Recreate LINGUAS (list of languages) file
  for i in "${srcdir}"/po/*.po; do
    _NEWFILE=${i##*/} # Strip path
    _NEWFILE=${_NEWFILE#*-} # Strip 'unity-'
    cp "${i}" "${_NEWFILE}" # Copy new translation
    echo "${_NEWFILE%.*}" >> LINGUAS # Add translation
  done
  cp "${srcdir}/po/unity.pot" unity.pot
  popd

  # Disable '-Werror'
  sed -i 's/[ ]*-Werror[ ]*//g' CMakeLists.txt services/CMakeLists.txt

  mkdir -p build && cd build

  cmake \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}"
  cd 'build'
  make DESTDIR="${pkgdir}/" install

  # Install profile upgrade helper
  install -dm755 "${pkgdir}/etc/compizconfig/upgrades/"
  install -m644 ../debian/profile_upgrade/* \
    "${pkgdir}/etc/compizconfig/upgrades/"

  # Taken from Ubuntu source package's debian/rules file
  find "${pkgdir}/usr/lib" -name \*.*a -exec rm {} \;
  rm -vf "${pkgdir}/usr/share/compiz/networkarearegion.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libnetworkarearegion.so"
  rm -vf "${pkgdir}/usr/share/compiz/unitydialog.xml"
  rm -vf "${pkgdir}/usr/lib/compiz/libunitydialog.so"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/migrate_favorites.py" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/5/launcher_bfb.png"
}
