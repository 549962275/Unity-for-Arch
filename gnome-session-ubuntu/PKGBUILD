# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-session-ubuntu
_ubuntu_rel=0ubuntu1
_ubuntu_ver=3.6.0
_actual_ver=3.6.1
pkgver=${_actual_ver}.ubuntu${_ubuntu_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME Session Handler with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsystemd' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3' 'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome' 'upower')
makedepends=('intltool' 'mesa' 'xtrans')
groups=('gnome')
provides=("gnome-session=${pkgver}")
conflicts=('gnome-session')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz"
        '55gnome-session_gnomerc'
        'gnome-session-3.3.1-llvmpipe.patch'
        'timeout.patch')
sha512sums=('17fa465ffbe36df2b64b60528bb15c0a67837ee27adc7bfbae125d2ac456a6910465358b451fad1f803096e9e6a57394ce94776db6166eb7930e65ff1b0c62d7'
            'c79071dbb38d0b26f78fbe1567ce0efa6c6c049734423e267eaef7dff1c0f2d69fcbd167a249921b05c41a4253c95a4d2f1921f473f9bda8eeefcc99a6398e0f'
            '9246e9bcb25493d169064a11cd0609c565e7173464fb594c8feee281c196127af604a8f2b3b8d07edb82ea35bfd3bc5b586719676ec868ee25c102039cb5415a'
            'e70432410514740cc2b6f722fbc28f7b5ea984c3bbfb08c72708b9d66ea2eb038e816152e4c2352d73d0cfbd827097195a058590598a138b5ca225bd421b9e26'
            '81d28b8f345c3a16dbb443ee8f195f802b572941aeac2dafaf6ea6b231ccb89a280aa855d09744dfc7ef2ad794a5e7b0002b54b14465865441367055514bcb70')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Allow software rendering
  patch -Np1 -i "${srcdir}/gnome-session-3.3.1-llvmpipe.patch"

  # Increase timeout, for slow machines
  patch -Np1 -i "${srcdir}/timeout.patch"

  # Disable patches
    # gnome-wm uses Debian's alternatives system (Fedora has it too, but other
    # packages need to use it)
      sed -i '/01_gnome-wm.patch/d' "${srcdir}/debian/patches/series"
    # systemd should make these patches obsolete
      sed -i '/12_no_gdm_fallback.patch/d' "${srcdir}/debian/patches/series"
    # We're not Ubuntu, do not hide stuff
      sed -i '/20_hide_nodisplay.patch/d' "${srcdir}/debian/patches/series"
    # Only Ubuntu uses apport
      sed -i '/96_no_catch_sigsegv.patch/d' "${srcdir}/debian/patches/series"
    # Fedora does not have sessionmigration
      sed -i '/53_add_sessionmigration.patch/d' "${srcdir}/debian/patches/series"
    # Part of Fedora's patches
      sed -i '/97_dont_blacklist_llvmpipe.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Needed because 01_gnome-wm.patch is disabled
      sed -i 's/gnome-wm/metacity/g' "${srcdir}/debian/patches/50_ubuntu_sessions.patch"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-session \
    --disable-schemas-compile \
    --enable-ipv6 # From debian/rules

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"
  make DESTDIR="${pkgdir}" install

  # Install Ubuntu files
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -dm755 "${pkgdir}/usr/bin/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -dm755 "${pkgdir}/usr/share/gnome/applications/"
  #install -m755 "${srcdir}/debian/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  # Install my version of the script (more platform-agnostic)
  install -m755 "${srcdir}/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755 "${srcdir}/debian/scripts/gnome-session-fallback" "${pkgdir}/usr/bin/"

  # symlink session files so that KDM can find them
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu-2d.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
}

# vim:set ts=2 sw=2 et:
