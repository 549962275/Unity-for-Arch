# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libxi-ubuntu
_ubuntu_ver=3ubuntu1
pkgver=1.4.3.${_ubuntu_ver}
pkgrel=101
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
depends=('libxext' 'inputproto-ubuntu')
makedepends=('pkgconfig' 'xorg-util-macros')
provides=("libxi=${pkgver}")
conflicts=('libxi')
options=(!libtool)
license=('custom')
source=("${url}/releases/individual/lib/libXi-${pkgver%.*}.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/libx/libxi/libxi_${pkgver%.*}-${_ubuntu_ver}.diff.gz")
sha512sums=('6ec8cedd7f6ad5ec6deb7ea594893615c87823537be20b6b1d67c8595364566ac4d22292e49640cb94e0ba2216c60859fcfb8f0650089015df9dc42b10a02b87'
            'ef1ea2f8146ff9e5cc77d9ad2be5c8a1c858a6d7571221f2a698a26b6a2de44ace7636f9c1bad4e4e1cd1df0a81687312cbfcac2786b10a6df275bab4745e8fa')

build() {
  cd "${srcdir}/libXi-${pkgver%.*}"

  #Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/libxi_${pkgver%.*}-${_ubuntu_ver}.diff"
  for i in $(cat "debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  chmod +x autogen.sh
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static --with-xmlto --without-fop
  make
}

package() {
  cd "${srcdir}/libXi-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
