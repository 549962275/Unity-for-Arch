# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libxi-ubuntu
_ubuntu_rel=0ubuntu3
pkgver=1.5.99.2.${_ubuntu_rel}
pkgrel=100
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
depends=('libxext' 'inputproto-ubuntu' 'libx11-ubuntu>=1.4.99.1.0ubuntu1')
makedepends=('pkgconfig' 'xorg-util-macros' 'xmlto' 'docbook-xml' 'asciidoc')
provides=("libxi=${pkgver}")
conflicts=('libxi')
options=(!libtool)
license=('custom')
source=("${url}/releases/individual/lib/libXi-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libxi_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('7f24edae93af7e82fa05d9ff4c8b0685562b22b294f66ef4fc674a1265aed5f9ca5ac997b78aa02838e013b280b2d96f76c98e2f0f105370e383bcf0f13980c5'
            'f5d782fb9ab215c9298638eb7ed9610be8cb41146a7299440cb11093a9f23bba01c2ba34e85fca83d4b66a8523357ac1bdf662fd077cda4a2b0e6e716afb82b4')

build() {
  cd "${srcdir}/libXi-${pkgver%.*}"

  #Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/libxi_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat "debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  chmod +x autogen.sh
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static --with-xmlto --without-fop
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/libXi-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
