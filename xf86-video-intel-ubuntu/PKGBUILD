# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xf86-video-intel-ubuntu
_ubuntu_rel=1ubuntu4
pkgver=2.15.901.${_ubuntu_rel}
pkgrel=100
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util' 'libxfixes' 'udev')
makedepends=('xorg-server-devel' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'xcb-util' 'xorg-server-ubuntu')
provides=("xf86-video-intel=${pkgver}")
conflicts=('xorg-server<1.10.0' 'xf86-video-i810' 'xf86-video-intel-legacy' 'xf86-video-intel')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=("${url}/releases/individual/driver/${pkgname%-*}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xserver-xorg-video-intel_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('5aa42b6151aba800d7648a506e49a4e0b8917a4820bf34c85dae48c05ec482f589334f76193fa97c9fbf9b781a37575b288cb76c0e6c04e489692d9b209b2d33'
            '0d3d3e70a2b2ea06fdaa7a7ba4eb8ae1ff9bd646b7529d7cbebb8f5cc3974b167721063712630a7d3ff14c446b26326a910027d226eac44af31eb9209e78e43a')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  #Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/xserver-xorg-video-intel_${pkgver%.*}-${_ubuntu_rel}.diff"

  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --prefix=/usr --enable-dri
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
}
