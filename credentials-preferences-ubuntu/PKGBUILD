# This package is identical to the one in [community] except that it's built
# against libgnome-control-center in gnome-control-center-ubuntu.

# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=credentials-preferences-ubuntu
_pkgname=credentials-control-center
_realpkgname=credentials-preferences
pkgver=0.1.5
pkgrel=1
pkgdesc="Control panel for configuring online account credentials and settings"
arch=('i686' 'x86_64')
url="https://launchpad.net/gnome-control-center-signon"
license=('GPL' 'LGPL')
provides=('credentials-preferences')
conflicts=('credentials-preferences')
depends=('libaccounts-glib' 'libsignon-glib' 'gtk3' 'FEATURE-libgnome-control-center')
makedepends=('gobject-introspection' 'vala' 'intltool' 'itstool')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/gnome-control-center-signon/13.04/$pkgver/+download/$_pkgname-$pkgver.tar.xz
        http://pkgbuild.com/~bgyorgy/sources/$_pkgname-translations-20121018.tar.gz)
md5sums=('deaa290c89dffee5198f3f0f6f2e1fe1'
         '702d6b39136e10ba6dfad0afd300b2af')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Install updated language files
  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  # Fix helper script
  sed -i 's/test/test -e/' online-accounts-preferences.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
