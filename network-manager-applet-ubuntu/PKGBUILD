# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=xenial
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=1.0

pkgbase=network-manager-applet-ubuntu
pkgname=(nm-connection-editor-ubuntu network-manager-applet-ubuntu)
_ubuntu_rel=1ubuntu1
pkgver=1.0.10
pkgrel=1
epoch=1
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL2 LGPL2.1)
makedepends=(libnm-glib libsecret gtk3 mobile-broadband-provider-info iso-codes
             libnotify libmm-glib intltool gobject-introspection libgudev
             libappindicator3 python2)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgbase%-*}/${pkgver%.*}/${pkgbase%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('dc13881243e5232d0c45563442f906a93c4d196a69c52c45d335f755bbc73c4d3131ba59180a102f65ca2066d40127b9db8cf85da2b41ec18efda482a9d210aa'
            '0500eb1cb771e48ee04fa736397f0d98c5642f34a290b80d46349046bf3e1f55d42b0a0a2e3cbefca78236ec285ecf6585db69a3750c3c9366cead54220ce4b7')

prepare() {
    mkdir -p nma/etc/xdg nma/usr/bin \
        nma/usr/share/{applications,icons/hicolor/22x22/apps,man/man1,nm-applet}

    cd "${pkgbase%-*}-${pkgver}"

    # Apply Ubuntu's patches
    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

    sed -i 's/-Werror//g' m4/compiler_warnings.m4
}

build() {
    cd "${pkgbase%-*}-${pkgver}"

    autoreconf -vfi

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --disable-maintainer-mode \
        --disable-migration \
        --enable-indicator \
        --with-modem-manager-1

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j1
}

check() {
    cd "${pkgbase%-*}-${pkgver}"
    make -k check
}

package_nm-connection-editor-ubuntu() {
    pkgdesc="NetworkManager GUI connection editor"
    depends=(libnm-glib gtk3 mobile-broadband-provider-info iso-codes)
    provides=("libnm-gtk=${pkgver}-${pkgrel}" "nm-connection-editor=${pkgver}-${pkgrel}")
    conflicts=(libnm-gtk libnm-gtk-ubuntu)
    replaces=(libnm-gtk libnm-gtk-ubuntu)
    install=nm-connection-editor.install

    cd "${pkgbase%-*}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    ### Split network-manager-applet
    cd ../nma
    mv "${pkgdir}"/etc/xdg/autostart etc/xdg/
    mv "${pkgdir}"/usr/bin/nm-applet usr/bin/
    mv "${pkgdir}"/usr/share/GConf usr/share/

    cd usr/share
    mv "${pkgdir}"/usr/share/applications/nm-applet.desktop applications/
    mv "${pkgdir}"/usr/share/man/man1/nm-applet.1 man/man1/
    mv "${pkgdir}"/usr/share/nm-applet/{{8021x,gsm-unlock,info}.ui,keyring.png} nm-applet/

    cd icons/hicolor/22x22
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{adhoc,mb-roam,wwan-tower}.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{secure,vpn-active}-lock.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-{signal,tech}-*.png apps/
    mv "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/nm-*connecting*.png apps/

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}

package_network-manager-applet-ubuntu() {
    depends=(nm-connection-editor-ubuntu libmm-glib libnotify libsecret libappindicator3)
    install=network-manager-applet.install
    provides=("network-manager-applet=${pkgver}")
    conflicts=(network-manager-applet)

    mv nma/* "${pkgdir}"

    # Install Ubuntu stuff
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    install -m644 "${srcdir}"/debian/icons/22/nm-device-wired-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-00-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-00.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none.png
    ln -snf       nm-signal-00-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-25-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-25.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low.png
    ln -snf       nm-signal-25-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-50-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-50.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium.png
    ln -snf       nm-signal-50-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-75-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-75.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high.png
    ln -snf       nm-signal-75-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high-secure.png
    install -m644 "${srcdir}"/debian/icons/22/nm-signal-100-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
    ln -snf       nm-signal-100.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full.png
    ln -snf       nm-signal-100-secure.png \
                  "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full-secure.png

    # Use language packs
    rm -rf "${pkgdir}"/usr/share/locale/
}
