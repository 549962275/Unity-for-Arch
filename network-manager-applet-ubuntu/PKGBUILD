# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet-ubuntu
_ubuntu_rel=0ubuntu1
_ubuntu_ver='+git.20120126t000800.5151959'
_actual_ver=0.9.2.0
pkgver=${_actual_ver}${_ubuntu_ver/+/.}${_ubuntu_rel}
pkgrel=1
pkgdesc="GNOME frontends to NetWorkmanager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'libgnome-keyring' 'polkit-gnome' 'gtk3' 'libnotify' 'gnome-icon-theme' 'mobile-broadband-provider-info' 'gconf' 'iso-codes')
makedepends=('intltool' 'gnome-bluetooth')
optdepends=('gnome-bluetooth: for PAN/DUN support')
provides=("network-manager-applet=${pkgver}")
conflicts=('network-manager-applet')
options=('!libtool')
install=network-manager-applet.install
# I'm not going to maintain a newer version of NetworkManager...
source=("http://ftp.acc.umu.se/pub/GNOME/sources/${pkgname%-*}/${_actual_ver%.*.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('1f5509446d830d967cad7ec8ed92814ca7a0a4ab97cdc46ecab8abc82221fc93fa6316984c3dacd3bf0c1464b50060d2508c7a9dcc4439a8fe317b168b9fd4de'
            'd9ec1a7d45c881bbe52e63d324136e74e43b4af63c8ef271f2462286b9a1e4832e0eacb90161826774ab369b240386cdede2b19808409680fc29fb1ece261b12')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu patches

  # Disable patches
    # Require git version of NetworkManager
      sed -i '/lp912150_lazy_notify_init.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/lp337960_dxteam_notification_icon_names.diff/d' "${srcdir}/debian/patches/series"
      sed -i '/clear-notification-actions.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  #intltoolize -f -c
  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --disable-static \
    --disable-maintainer-mode \
    --enable-indicator

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain nm-applet "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -fv "${pkgdir}"/etc/gconf/schemas/*.schemas

  # Install Ubuntu stuff
  install -dm755 "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  install -m644 "${srcdir}/debian/icons/22/nm-device-wired-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  install -m644 "${srcdir}/debian/icons/22/nm-signal-00-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  ln -snf 'nm-signal-00.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-none.png"
  ln -snf 'nm-signal-00-secure.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-none-secure.png"
  install -m644 "${srcdir}/debian/icons/22/nm-signal-25-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  ln -snf 'nm-signal-25.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-low.png"
  ln -snf 'nm-signal-25-secure.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-low-secure.png"
  install -m644 "${srcdir}/debian/icons/22/nm-signal-50-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  ln -snf 'nm-signal-50.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium.png"
  ln -snf 'nm-signal-50-secure.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium-secure.png"
  install -m644 "${srcdir}/debian/icons/22/nm-signal-75-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  ln -snf 'nm-signal-75.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-high.png"
  ln -snf 'nm-signal-75-secure.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-high-secure.png"
  install -m644 "${srcdir}/debian/icons/22/nm-signal-100-secure.png" "${pkgdir}/usr/share/icons/hicolor/22x22/apps/"
  ln -snf 'nm-signal-100.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-full.png"
  ln -snf 'nm-signal-100-secure.png' "${pkgdir}/usr/share/icons/hicolor/22x22/apps/gsm-3g-full-secure.png"
}
