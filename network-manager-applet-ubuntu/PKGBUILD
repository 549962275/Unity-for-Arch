# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=utopic
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=0.9

pkgbase=network-manager-applet-ubuntu
pkgname=(network-manager-applet-ubuntu libnm-gtk-ubuntu)
_ubuntu_rel=0ubuntu6
_ubuntu_ver=0.9.8.8
pkgver=0.9.10.0
pkgrel=2
epoch=1
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL LGPL2.1)
makedepends=(libnm-glib libsecret gtk3 gnome-icon-theme
             mobile-broadband-provider-info iso-codes libnotify libmm-glib
             intltool gobject-introspection)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgbase%-*}/${pkgver%.*.*}/${pkgbase%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase%-*}_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.xz"
        0001-nm-applet-use-indicator.patch)
sha512sums=('d9472bb6613fa8feb7bbde15932542adcfdb99897d6f1376ea4d511a6ff785debb0b4dd630deb7dc6ba2c244ca2487a00cd8c758274a914d61864370226e2b1d'
            '68df9d224b77629fb21e6651c815b8b9d36be5f7d2010f6e91a86c67523c364ed8b1a1d798929212d3c43a92824882aa7edd64327474404b50482667d7c6024e'
            '9d876446ff9199b48702315210f94fba7216411d7d16975ef4677daa6aafd7237bd7370882db546850a9a9e3824bcd8ba6c71756f919376194b2ef5e87d1666f')

prepare() {
  cd "${pkgbase%-*}-${pkgver}"

  # Apply Ubuntu's patches

  sed -i -e '/git_use_paths_in_vpn_name_files_4867951.patch/d' \
         -e '/lp289466_always_show_tray_icon.patch/d' \
         -e '/lp328572-dxteam-connect-text.patch/d' \
         -e '/lp330571_dxteam_wired_connect_text.patch/d' \
         -e '/lp330608_dxteam_gsm_connect_text.patch/d' \
         -e '/lp341684_device_sensitive_disconnect_notify.patch/d' \
         -e '/applet-wifi-menu-before-vpn.patch/d' \
         -e '/key-certificate-extensions.patch/d' \
         -e '/lp829673_gconf_hide_applet.patch/d' \
         -e '/nm-applet-use-indicator.patch/d' \
         -e '/hide_policy_items_env_var.patch/d' \
         -e '/0001_Move_on_with_enter_in_the_country_page_of_the_new_mobile_wizard.patch/d' \
         -e '/set_atk_relations.patch/d' \
         -e '/11-user-connections.patch/d' \
         ../debian/patches/series

  patch -p1 -i ../0001-nm-applet-use-indicator.patch

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  sed -i 's/-Werror//g' m4/compiler_warnings.m4
}

build() {
  cd "${srcdir}/${pkgbase%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --disable-static \
    --disable-maintainer-mode \
    --disable-migration \
    --enable-indicator \
    --with-modem-manager-1

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -j1
}

check() {
  cd "${pkgbase%-*}-${pkgver}"
  make -k check
}

package_network-manager-applet-ubuntu() {
  depends=(libnm-gtk-ubuntu libmm-glib libnotify libsecret libappindicator3)
  install=network-manager-applet.install
  provides=("network-manager-applet=${pkgver}")
  conflicts=(network-manager-applet)

  cd "${pkgbase%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" -C src/libnm-gtk uninstall

  # Install Ubuntu stuff
  install -dm755 "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  install -m644 "${srcdir}"/debian/icons/22/nm-device-wired-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-00-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-00.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none.png
  ln -snf       nm-signal-00-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-none-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-25-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-25.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low.png
  ln -snf       nm-signal-25-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-low-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-50-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-50.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium.png
  ln -snf       nm-signal-50-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-medium-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-75-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-75.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high.png
  ln -snf       nm-signal-75-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-high-secure.png
  install -m644 "${srcdir}"/debian/icons/22/nm-signal-100-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/
  ln -snf       nm-signal-100.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full.png
  ln -snf       nm-signal-100-secure.png \
                "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/gsm-3g-full-secure.png

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

package_libnm-gtk-ubuntu() {
  pkgdesc="NetworkManager GUI library"
  depends=(libnm-glib gtk3 mobile-broadband-provider-info iso-codes)
  provides=("libnm-gtk=${pkgver}")
  conflicts=(libnm-gtk)

  cd "${pkgbase%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" -C src/libnm-gtk install
}
