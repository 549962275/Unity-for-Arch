# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=python-oauthlib
_ubuntu_rel=0ubuntu1
pkgver=0.3.4.${_ubuntu_rel}
pkgrel=100
pkgdesc="A Python implementation of the OAuth request-signing logic"
arch=('any')
url="http://pypi.python.org/pypi/oauthlib"
license=('BSD')
groups=('unity')
depends=('python' 'python-crypto')
makedepends=('python-distribute' 'python-nose')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/o/oauthlib/oauthlib-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/python-oauthlib_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('8cefd8120df5e1bc97de057c9771882d56afc0cfe2bf0025452c8f25a43723935b34edd108c5de3a54acb141577ee57e294d4621ef83a0bbf981788b7f89eaca'
            'a41a4e9b01f8d2b6ae63a1a7226d669518b0510b3c3875b2f56388e928f1def4ecc0aee8f5ede586f7a8bc9d910ba58bf6d52ad48487eec9945b58a9176a1f77')

build() {
  cd "${srcdir}/oauthlib-${pkgver%.*}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done
}

package() {
  cd "${srcdir}/oauthlib-${pkgver%.*}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
