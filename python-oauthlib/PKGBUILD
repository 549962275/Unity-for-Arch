# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=python-oauthlib
_ubuntu_rel=0ubuntu2
pkgver=0.3.3.${_ubuntu_rel}
pkgrel=100
pkgdesc="A Python implementation of the OAuth request-signing logic"
arch=('any')
url="http://pypi.python.org/pypi/oauthlib"
license=('BSD')
groups=('unity')
depends=('python' 'python-crypto')
makedepends=('python-distribute' 'python-nose')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/o/oauthlib/oauthlib-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/python-oauthlib_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('a33fd37c531ca95c3b5556248d0ad57c83187766f6a4f99ebb21f22ac325ef046f8e753253e05667892fa6ee90a719568fbf5d627450b37b6e1d00cbe321e9fc'
            '4a0e1cb26ccf72ff418bd5c6541b297f8791a270ffd1c27fbc6be2bfe7aa111bb3799aef07da6aeea6de9a85018eb05d2c8731ae5097ac8eea3819c8e755ff9d')

build() {
  cd "${srcdir}/oauthlib-${pkgver%.*}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done
}

package() {
  cd "${srcdir}/oauthlib-${pkgver%.*}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
