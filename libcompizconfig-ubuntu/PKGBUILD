# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libcompizconfig-ubuntu

_ubuntu_rel=0ubuntu1
_ubuntu_ver='~bzr428'
_actual_ver=0.9.7.0
_WTF_ver=0.9.5.94

pkgver=${_actual_ver}.bzr428.${_ubuntu_rel}

pkgrel=100
pkgdesc="Compiz configuration system library - Ubuntu version"
arch=('i686' 'x86_64')
url="http://compiz.org"
license=('GPL')
depends=('compiz-core-ubuntu' 'libxml2' 'protobuf')
makedepends=('intltool' 'pkgconfig' 'cmake')
provides=('libcompizconfig')
conflicts=('libcompizconfig')
groups=('unity')
install=${pkgname%-*}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}${_ubuntu_ver}.orig.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('13612ebcdd3016c5111a664487dd76412ef39f59fe986c0805a96d58696780d200e70dfd09e2d93a20e4c35aeb5cd2959a79c6b544d465c9232a60889b692e19'
            '9d434cc42c8ec7e5102bb2df3160de01fa8004f0b39dee98d690656c7ff1d9c3cba5ea374cab783e9e79220885745c76057ab7d5fa11b83fef5db71762d6ca26')

build() {
  cd "${srcdir}/${pkgname%-*}-${_WTF_ver}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  [[ -d build ]] || mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DUSE_GSETTINGS=OFF \
    -DCOMPIZ_DISABLE_GS_SCHEMAS_INSTALL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIZ_DESTDIR="${pkgdir}"

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_WTF_ver}"
  cd "build"
  make install
  make findcompizconfig_install

  install -dm755 "${pkgdir}/usr/share/compizconfig"
  install -dm755 "${pkgdir}/etc/compizconfig"
  install -m644 "${srcdir}/debian/normal.profile" \
    "${pkgdir}/usr/share/compizconfig/"
  install -m644 "${srcdir}/debian/extra.profile" \
    "${pkgdir}/usr/share/compizconfig/"
  install -m644 "../config/config" "${pkgdir}/etc/compizconfig/"
}
