# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libcompizconfig-ubuntu
pkgver=0.9.5.94
_ubuntu_ver=0ubuntu2
pkgrel=1
pkgdesc="Compiz configuration system library - Ubuntu version"
arch=('i686' 'x86_64')
url="http://compiz.org"
license=('GPL')
provides=('libcompizconfig')
conflicts=('libcompizconfig')
depends=('compiz-core-ubuntu' 'libxml2' 'protobuf')
makedepends=('intltool' 'pkgconfig' 'cmake')
source=(
  "http://archive.ubuntu.com/ubuntu/pool/main/libc/${pkgname%-*}/${pkgname%-*}_${pkgver}.orig.tar.gz"
  "http://archive.ubuntu.com/ubuntu/pool/main/libc/${pkgname%-*}/${pkgname%-*}_${pkgver}-${_ubuntu_ver}.debian.tar.gz"
)
sha512sums=('39bceaf3cf319254ce90e892cb27d46c96c85ed4ef8046ef4eb1ad679015f7ceb8358c9f47b8c4d3738ba24889acba6b19f9622583c094c97ec2a6f528b368b3'
            'a9d3c7277e386379812eab0d2ffa102ae94aa0eb8f3b5ca3abc4cfd41cec53ed26d405a6a9c92912a4c0087dbc2a338f5305443c431c725c51341ed36628793a')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  for i in $(cat ${srcdir}/debian/patches/series | grep -Ev '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  [[ -d build ]] || mkdir build
  cd build
  env PKG_CONFIG_PATH="/usr/lib/pkgconfig:$PKG_CONFIG_PATH" \
  cmake .. \
    -DCMAKE_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DUSE_GSETTINGS=OFF \
    -DCOMPIZ_DISABLE_GS_SCHEMAS_INSTALL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIZ_DESTDIR="${pkgdir}"
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  cd "build"
  make install
  make findcompizconfig_install

  mkdir -p "${pkgdir}/usr/share/compizconfig"
  mkdir -p "${pkgdir}/etc/compizconfig"
  install -m644 "${srcdir}/debian/normal.profile" "${pkgdir}/usr/share/compizconfig/"
  install -m644 "${srcdir}/debian/extra.profile" "${pkgdir}/usr/share/compizconfig/"
  install -m644 "../config/config" "${pkgdir}/etc/compizconfig/"
}
