# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: twa022 <twa022 at gmail dot com>
# Contributor: thn81 <root@scrat>

pkgname=xfce4-indicator-plugin
_ubuntu_rel=0ubuntu1
pkgver=0.4.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="Display indicators in the xfce4 panel"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-indicator-plugin"
groups=('xfce4-goodies' 'unity')
depends=('xfce4-panel' 'libxfce4util' 'libindicator')
makedepends=('intltool' 'xfce4-dev-tools')
groups=('unity')
options=('!libtool')
source=("http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*.*}/${pkgname}-${pkgver%.*}.tar.bz2"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('34800e56ea134f70f037bb6df6ab4fdf755d2d6171f44cc8a09c85e714e009b084e81c1f2e63293680329f8fd6d60d92e53ebff07dce1c12ee74294203628ff2'
            '4fafd7f0d8e0bbf3112319ace303ce17bff0b64ce1dbb4814d82035dd2e70f1f3b2b492b4f5d18e4be8d1d81077419f3392dc9ff743635952699ed78018ea10c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver%.*}

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  /usr/bin/xdt-autogen
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname} \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make ${MAKEFLAGS}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver%.*}
  make DESTDIR=${pkgdir} install
}

