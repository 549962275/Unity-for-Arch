# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# PKGBUILD restored from: http://www.mail-archive.com/arch-commits@archlinux.org/msg99785.html

pkgname=libgdu
pkgver=3.0.2
pkgrel=100
pkgdesc="GNOME libraries and applications for dealing with storage devices (library)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org";
depends=('dbus-glib' 'libgnome-keyring' 'udisks')
makedepends=('dbus-glib' 'libgnome-keyring' 'udisks' 'libunique3' 'libnotify'
             'avahi' 'hicolor-icon-theme' 'gtk-doc' 'intltool')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/GNOME/sources/gnome-disk-utility/${pkgver%.*}/gnome-disk-utility-${pkgver}.tar.xz"
        'fix-freeze.patch'
        'build_library_only.patch')
sha512sums=('b7e68f42d9199c74b60f32b27456b81a6351dc717e54a0dd6162b4e0af521f50deda587012a94ba2768d76dc9dda40dbf505cfea9ca94f879df5d9093fef8969'
            '00ea87c14eb2b801f568604234731d85b81ec3100d2c8afcdd851c65de2f0f7bbeff2b425d98db60ceedabefb0a111468a9171235c65cca2ab03e773c49ed551'
            '4751b97d9ea233179109342b2a1b8fdd3713ba7ace06d7c252a1ad41e7e29b1284580b8131bc886f38f5fd6bd09c0366264ba26d874629635dde4455aeac5415')

build() {
  cd "${srcdir}/gnome-disk-utility-${pkgver}"

  patch -Np1 -i "${srcdir}/fix-freeze.patch"

  # Build libgdu library only - no gnome-disk-utility
  patch -Np1 -i "${srcdir}/build_library_only.patch"

  autoreconf -vfi

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libexecdir=/usr/lib/gnome-disk-utility \
              --disable-scrollkeeper --disable-nautilus --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gnome-disk-utility-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
