# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgbase=gtk2-ubuntu
pkgname=('gtk2-ubuntu' 'gtk-update-icon-cache-ubuntu')
_ubuntu_rel=0ubuntu5
pkgver=2.24.10.${_ubuntu_rel}
pkgrel=101
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection' 'gtk-doc')
options=('!libtool')
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+2.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'xid-collision-debug.patch'
        'fix-ubuntumenuproxy-build.patch')
sha512sums=('46bd22af861c04cf7d7b704356936f282da9e7edec1e637dd578170396ce6b4a837f660b46334ceb73372a076966e31afc02aaafb5d69796539ddaac1531fedf'
            'dcc78aa10f7ca397bd636ee9fa836e63ea39e6333774717cd2d8389a0ccdd2c7f08401806da543d1c722f8af434f726b2f70c3d39c7aab4a8c3af64c0919a285'
            '89e3223c86731a76b12d39245f1ec4cf8b4860ab1d11d74a10e5deb0db302f20b4bb5d9034da1d8aeb96369dbdeddcdd2f28943270dc501476c694562b960159'
            '4b27afecb82e3517c2584665f8d2a2911193254a00b82ff8064caf4016ab029c76e99af502bb9271a66209594b50b2729797defcc9512d341aa61aff4f64ec24')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*}"

  patch -Np1 -i "${srcdir}/xid-collision-debug.patch"

  # Apply Ubuntu patches

  # Certain patches should not be used
    # Multiarch
      sed -i '/041_ia32-libs.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/098_multiarch_module_path.patch/d' "${srcdir}/debian/patches/series"
    # Static linking stuff for udebs
      sed -i '/001_static-linking-dont-query-immodules.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/002_static-linking-dont-build-perf.patch/d' "${srcdir}/debian/patches/series"
    # Breaks pkgconfig
      sed -i '/003_gdk.pc_privates.patch/d' "${srcdir}/debian/patches/series"

  # Build fix from György Balló
  patch -Np1 -i "${srcdir}/fix-ubuntumenuproxy-build.patch"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes \
    --enable-test-print-backend

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package_gtk2-ubuntu() {
  pkgdesc="GTK+ is a multi-platform toolkit (v2)"
  install=gtk2.install
  depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
  provides=("gtk2=${pkgver%.*}")
  conflicts=('gtk2')
  replaces=('gtk2-docs')
  backup=(etc/gtk-2.0/gtkrc)

  cd "${srcdir}/gtk+-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
  sed -i "s#env python#env python2#" "${pkgdir}/usr/bin/gtk-builder-convert"
  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
  # split this out to use with gtk3 too
  rm "${pkgdir}/usr/bin/gtk-update-icon-cache"
}

package_gtk-update-icon-cache-ubuntu() {
  pkgdesc="The GTK+ update icon cache tool"
  depends=('gdk-pixbuf2')
  provides=("gtk-update-icon-cache=${pkgver%.*}")
  conflicts=('gtk-update-icon-cache')

  cd "${srcdir}/gtk+-${pkgver%.*}/gtk"

  install -Dm755 gtk-update-icon-cache "${pkgdir}/usr/bin/gtk-update-icon-cache"
}

