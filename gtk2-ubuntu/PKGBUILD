# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgbase=gtk2-ubuntu
pkgname=('gtk2-ubuntu' 'gtk-update-icon-cache-ubuntu')
_ubuntu_rel=2ubuntu1
pkgver=2.24.8.${_ubuntu_rel}
pkgrel=100
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection' 'gtk-doc')
options=('!libtool' '!docs')
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/gtk+2.0/gtk+2.0_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        xid-collision-debug.patch)
sha512sums=('616e76d7879206b8c551d2e09e8406311cbaf72093cc8d4e0c18e132f366276eb1dbabf1755fe1e8d9b70df819ffb1dac5694f166014d305bf1a0bcc39a239a4'
            '29c9127dd4990afc83ad778171ef2017c81842650cfd0152ac19dcef6220c658f56fec8959b4ff86119575159942cde53bdaf8ef7a5a5ac9e640a3caae17fb27'
            '89e3223c86731a76b12d39245f1ec4cf8b4860ab1d11d74a10e5deb0db302f20b4bb5d9034da1d8aeb96369dbdeddcdd2f28943270dc501476c694562b960159')

build() {
  cd "${srcdir}/gtk+-${pkgver%.*}"

  patch -Np1 -i "${srcdir}/xid-collision-debug.patch"

  #Apply Ubuntu patches

  #Certain patches should not be used
    #Multiarch
      sed -i '/041_ia32-libs.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/098_multiarch_module_path.patch/d' "${srcdir}/debian/patches/series"
    #Static linking stuff for udebs
      sed -i '/001_static-linking-dont-query-immodules.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/002_static-linking-dont-build-perf.patch/d' "${srcdir}/debian/patches/series"
    #Breaks pkgconfig
      sed -i '/003_gdk.pc_privates.patch/d' "${srcdir}/debian/patches/series"
    #Patches accepted upstream
      sed -i '/000git_file_chooser.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/101_filechooser.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -fi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}
package_gtk2-ubuntu() {
    pkgdesc="The GTK+ Toolkit (v2) with Ubuntu's patches"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi-ubuntu' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    provides=("gtk2=${pkgver%.*}")
    conflicts=('gtk2')
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver%.*}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm "${pkgdir}/usr/bin/gtk-update-icon-cache"
}
package_gtk-update-icon-cache-ubuntu() {
    pkgdesc="The GTK+ update icon cache tool with Ubuntu's patches"
    depends=('gdk-pixbuf2')
    provides=("gtk-update-icon-cache=${pkgver%.*}")
    conflicts=('gtk-update-icon-cache')

    cd "${srcdir}/gtk+-${pkgver%.*}/gtk"

    install -D -m755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
}

