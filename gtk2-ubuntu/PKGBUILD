# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk2-ubuntu
_ubuntu_rel=0ubuntu1
_actual_ver=2.24.14
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="GTK+ is a multi-platform toolkit (v2)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'libcups' 'gtk-update-icon-cache' 'python2')
makedepends=('pkg-config' 'gobject-introspection' 'gtk-doc')
provides=("gtk2=${_actual_ver}")
conflicts=('gtk2')
replaces=('gtk2-docs')
backup=('etc/gtk-2.0/gtkrc')
options=('!libtool')
install=arch_gtk2.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+2.0_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        'arch_xid-collision-debug.patch'
        'fix-ubuntumenuproxy-build.patch'
        '0001_autoconf.patch')
sha512sums=('f262ddd728ca2d93889bb1b113866932edf7d61ab3248ca11f2c83dbb181fcc0eaab900e6ed4933e4b597cac1ed1bf622711a617e0867ec10730c934d21dc911'
            '01d22d46339156d874362f71597c23c6308c2f6325a585e7ed7afd669b22c4af5e926c8be9a577929be756064aaf0b714c851695b7a56af1c94a0b40172848fb'
            '89e3223c86731a76b12d39245f1ec4cf8b4860ab1d11d74a10e5deb0db302f20b4bb5d9034da1d8aeb96369dbdeddcdd2f28943270dc501476c694562b960159'
            '4b27afecb82e3517c2584665f8d2a2911193254a00b82ff8064caf4016ab029c76e99af502bb9271a66209594b50b2729797defcc9512d341aa61aff4f64ec24'
            'a841e4cdae3ac1bc3a671b17101a5928d444e5a3581334f602b20ca84609f57d1903bf04d1d17061af18a8ba2e24d0e5cd33a6c0a5ecb17d8dccf272ec203b76')

build() {
  cd "${srcdir}/gtk+-${_actual_ver}"

  patch -p1 -i "${srcdir}/arch_xid-collision-debug.patch"
  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Apply Ubuntu patches

  # Certain patches should not be used
    # Multiarch
      sed -i '/041_ia32-libs.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/098_multiarch_module_path.patch/d' "${srcdir}/debian/patches/series"
    # Static linking stuff for udebs
      sed -i '/001_static-linking-dont-query-immodules.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/002_static-linking-dont-build-perf.patch/d' "${srcdir}/debian/patches/series"
    # Breaks pkgconfig
      sed -i '/003_gdk.pc_privates.patch/d' "${srcdir}/debian/patches/series"
    # gtk.immodules is in /etc/gtk-2.0/ in Arch Linux, not
    # /usr/lib/gtk-2.0/2.10.0/
      sed -i '/011_immodule-cache-dir.patch/d' "${srcdir}/debian/patches/series"

  # Build fix from György Balló
  patch -p1 -i "${srcdir}/fix-ubuntumenuproxy-build.patch"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  # Cannot find gmodule-2.0
  #CFLAGS="${CFLAGS} $(pkg-config --cflags --libs gmodule-2.0)"

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-xinput=yes \
    --enable-test-print-backend

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gtk+-${_actual_ver}"

  make DESTDIR="${pkgdir}" install
  sed -i "s#env python#env python2#" "${pkgdir}/usr/bin/gtk-builder-convert"
  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
  # Use the official gtk-update-icon-cache package
  rm -v "${pkgdir}/usr/bin/gtk-update-icon-cache"
}

# vim:set ts=2 sw=2 et:
