# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=utopic
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.14
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
_ppa_rel=0ubuntu1~utopic1
pkgver=3.14.0
pkgrel=2
pkgdesc="GNOME and Unity file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf
         libtracker-sparql libnotify nautilus-sendto libunity libzeitgeist)
makedepends=(intltool gobject-introspection python python2 python2-packagekit)
url="http://www.gnome.org"
options=(!emptydirs)
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/n/nautilus/nautilus_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
sha512sums=('114e7edebd84e6c3bdc0afdc2822b80c16d361f8417973eaed7bdd3fe3af239dd2eed3f02657b02142ecee36250cec675849518c688ef40b397e5e9b115d692f'
            '100b7a190f9ce3ef2c30900478a842693861fbd761d038095618812a9ac60100e2f79c152a0e57dffe9f29321b2bf0533f8850646ef5aad1a0cbc0a80ab74a0a')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
    sed -i -e '/15_use-ubuntu-help.patch/d' \
           -e '/08_clean_session_capplet.patch/d' \
           ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator \
    --enable-unity

  make
}

package_nautilus-ubuntu() {
  depends+=(libnautilus-extension-ubuntu)
  groups=(gnome unity)
  provides=("nautilus=${pkgver}")
  conflicts=(nautilus)

  cd "nautilus-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "${pkgdir}"/usr/include n-e/usr
  mv "${pkgdir}"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "${pkgdir}"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "${pkgdir}"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"

  # Use language packs
  #rm -r "${pkgdir}/usr/share/locale/"
}

package_libnautilus-extension-ubuntu() {
  pkgdesc="Library for extending the ${pkgdesc}"
  depends=(cairo gtk3)
  provides=("libnautilus-extension=${pkgver}")
  conflicts=(libnautilus-extension)

  mv n-e/* "${pkgdir}/"
}
