# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=nautilus-ubuntu
_ubuntu_rel=0ubuntu5
_ubuntu_ver=3.2.0
pkgver=3.2.1.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME shell and file manager with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql')
makedepends=('intltool' 'gobject-introspection')
groups=('gnome')
provides=("nautilus=${pkgver}")
conflicts=('nautilus')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/n/${pkgname%-*}/${pkgname%-*}_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('1e5c4aca7b73ad55318390632bb35a97a3c6ed35e013a0d57a5071f43a5c2a35daa70720258acc8f7e6abc163934497ced8376569b1cd164342535c9639a830d'
            'b278d1bf9f767e94bcd772f5dd40ea39cf603c621fbd6e2a55c73a4f2f550cbc01bd0c6362d3014ec584fc2e0bda1c9e179b7179ac688bddc241b954c984ab24')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  #Apply Ubuntu patches

  #Disable these patches
    #Accepted upstream
      sed -i '/git_handle_missing_icon.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_gsettings_signals.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_no_typeahead_timeout_segfault.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_dont_preview_empty_selection.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_correct_signal_handling.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_no_signal_order_assert.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_slot_closing.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_close_pane_cleaning.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-nst-extension \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator --enable-introspection #From debian/rules
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  #From debian/rules
  echo "" > "${pkgdir}/usr/share/nautilus/nautilus.css"

  #Ubuntu specific stuff
  install -d -m755 "${pkgdir}/usr/share/pixmaps/"
  install -d -m755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -d -m755 "${pkgdir}/usr/share/applications/"
  install -d -m755 "${pkgdir}/usr/share/menu/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/nautilus.gsettings-override" "${pkgdir}/usr/share/glib-2.0/schemas/10_${pkgname%-*}.gschema.override"
  install -m644 "${srcdir}/debian/nautilus-home.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/menu" "${pkgdir}/usr/share/menu/${pkgname%-*}.menu"
}
