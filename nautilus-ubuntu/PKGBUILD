# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=vivid
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.16
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
_ubuntu_rel=0ubuntu4
_ubuntu_ver=3.14.2
pkgver=3.16.0
pkgrel=1
pkgdesc="GNOME and Unity file manager"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils dconf
         libtracker-sparql libnotify nautilus-sendto libunity libzeitgeist)
makedepends=(intltool gobject-introspection python python2)
url="http://www.gnome.org"
options=(!emptydirs)
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz"
        03_translations_list_update.patch)
sha512sums=('ea0ed2539fd2b77e6beb68fa9aeffc26673153fef2719b39fbd0741d96772fe969bb3624cdf0ae769d43eb1108db4c780ae0403cd84f2946ce3ec0e5b3cef8b8'
            'b48dad4ad48983ff4e39bb54db7d46e745d70579af79fd6b76ef84b7e9def0c229c7ad7d58a2f61f8bf0bcde73719d43ef70163551250758b817d11c33d5ccec'
            '6335e92c95b33f2703c4ae43ecc15f41ddd90e83e65108aab82449efafb9c907426eeaaeb1907bea6c1723109c0067958a02a6c50925c67750c1639b20dc9632')

prepare() {
  cd "${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
  sed -i -e '/15_use-ubuntu-help.patch/d' \
         -e '/08_clean_session_capplet.patch/d' \
         ../debian/patches/series

  # Not working
  sed -i '/revert_remove_unused_GtkActions.patch/d' ../debian/patches/series
  sed -i '/03_translations_list_update.patch/d' ../debian/patches/series
  sed -i '/11_copy_skipping_pager.patch/d' ../debian/patches/series
  sed -i '/12_unity_launcher_support.patch/d' ../debian/patches/series
  sed -i '/14_bring_del_instead_ctrl_del.patch/d' ../debian/patches/series
  sed -i '/zg_activity_logging.patch/d' ../debian/patches/series
  sed -i '/19_unity_open_location_xid.patch/d' ../debian/patches/series
  sed -i '/ubuntu_revert_no_wallpaper.patch/d' ../debian/patches/series
  sed -i '/interactive_search.patch/d' ../debian/patches/series
  sed -i '/ubuntu_sync_background_to_accountsservice.patch/d' ../debian/patches/series
  sed -i '/0001-Respect-gtk-dialogs-use-header-for-all-dialogs.patch/d' ../debian/patches/series
  sed -i '/0002-Only-use-a-header-bar-in-GNOME-shell.patch/d' ../debian/patches/series
  sed -i '/restore-traditional-menu-bar.patch/d' ../debian/patches/series
  sed -i '/16_unity_new_documents.patch/d' ../debian/patches/series
  sed -i '/ubuntu_backspace_behaviour.patch/d' ../debian/patches/series
  sed -i '/git_name_column.patch/d' ../debian/patches/series
  sed -i '/0001-application-use-correct-order-to-watch-for-GSettings.patch/d' ../debian/patches/series
  sed -i '/ubuntu_tracker_only_on_GNOME.patch/d' ../debian/patches/series
  sed -i '/ubuntu_infobars_color.patch/d' ../debian/patches/series
  sed -i '/dont_wrap_labels_after_dots.patch/d' ../debian/patches/series
  sed -i '/18_unity_icon_color.patch/d' ../debian/patches/series

  patch -p1 -i ../03_translations_list_update.patch

  for i in $(grep -v '#' ../debian/patches/series); do
    msg "Applying ${i} ..."
    patch -p1 -i "../debian/patches/${i}"
  done

  #sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c
}

build() {
  cd "${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator \
    --enable-unity

  make
}

package_nautilus-ubuntu() {
  depends+=(libnautilus-extension-ubuntu)
  groups=(gnome unity)
  provides=("nautilus=${pkgver}")
  conflicts=(nautilus)

  cd "nautilus-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "${pkgdir}"/usr/include n-e/usr
  mv "${pkgdir}"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "${pkgdir}"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "${pkgdir}"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"

  # Use language packs
  #rm -r "${pkgdir}/usr/share/locale/"

  # Make Unity happy
  cp "${pkgdir}"/usr/share/applications/{org.gnome.Nautilus,nautilus}.desktop
  sed -i \
      -e '/DBusActivatable/d' \
      -e '/MimeType/d' \
      "${pkgdir}"/usr/share/applications/nautilus.desktop
}

package_libnautilus-extension-ubuntu() {
  pkgdesc="Library for extending the ${pkgdesc}"
  depends=(cairo gtk3)
  provides=("libnautilus-extension=${pkgver}")
  conflicts=(libnautilus-extension)

  mv n-e/* "${pkgdir}/"
}
