# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=nautilus-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=3.3.92.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME shell and file manager with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql' 'libappindicator3' 'packagekit' 'libunity' 'glib2-ubuntu>=2.31.14.0ubuntu1' 'gtk3-ubuntu>=3.3.12.0ubuntu1')
makedepends=('intltool' 'gobject-introspection')
groups=('gnome')
provides=("nautilus=${pkgver}")
conflicts=('nautilus')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('b1ac2f582f8984cd301a4681d93d6e040fa53cc6bcfd4a5f1f94c57551f1d547fbbcbecc378cf5fa7b28105ab7eb3012993cdbcc9065b317430044ea8a0bd23a'
            '6c3fc3f7aab38679d049960522cd82d167f9b4fa6d433a88b5d10c5f8a6d961aae4eca6cb017fa61368ba6304fa4e5e9573b0ee6035e4d5d708f761fc0beadc0')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches

  # Disable patches
    # Don't use Ubuntu help
      sed -i '/15_use-ubuntu-help.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # For updating translations in desktop files
  ln -s "${srcdir}/debian" .

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-nst-extension \
    --disable-update-mimedb \
    --enable-gtk-doc \
    --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install

  # From debian/rules
  echo "" > "${pkgdir}/usr/share/nautilus/nautilus.css"

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -dm755 "${pkgdir}/usr/share/menu/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/nautilus.gsettings-override" "${pkgdir}/usr/share/glib-2.0/schemas/10_${pkgname%-*}.gschema.override"
  install -m644 "${srcdir}/debian/nautilus-home.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/menu" "${pkgdir}/usr/share/menu/${pkgname%-*}.menu"
}
