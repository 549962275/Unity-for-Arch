# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=geis
pkgver=2.2.14
pkgrel=100
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/geis"
license=('GPL' 'LGPL')
depends=('grail' 'xorg-xinput' 'python2' 'dbus-core')
provides=("utouch-geis=${pkgver}")
conflicts=('utouch-geis')
options=('!emptydirs' '!libtool')
source=("https://launchpad.net/geis/trunk/${pkgver}/+download/geis-${pkgver}.tar.xz")
sha512sums=('913ae48dc899cabe4746d8d7bcb89339b1f9ada2ed09904ae3651780a3375041aea3a1f13e3f65ccb1dd2e8b7662b709d7848cf47f460deceecad3aea311c9cb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  PYTHON2VER=$(readlink /usr/bin/python2 | sed 's/python//')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=python2 ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis} "${pkgdir}/usr/lib/python2.7/site-packages/geisview/__init__.py"
}

# vim:set ts=2 sw=2 et:
