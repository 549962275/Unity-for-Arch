# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-appmenu
pkgname=('indicator-appmenu' 'indicator-appmenu-gtk2')
_ubuntu_rel=0ubuntu3
pkgver=0.3.2.${_ubuntu_rel}
pkgrel=100
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
options=(!libtool)
source=("http://launchpad.net/${pkgbase}/${pkgver%.*.*}/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        'build_fix_with_libdbusmenu.patch')
sha512sums=('cf2d771cdde5902fca38fcaa8e442506d189cc4b8617eb596ab519b43f8ef27fd4602b093f5461560c7300048466213865cdf8a795a34688b02e16660251ac96'
            '73debad2f50c5aad15bf683e398423edaea2a8d2aaa16823255355f384e9d958ee54ec1d6058f266149b5ea9d456666b58a12a62a1679a5e749fbe20a9cd2270'
            '377937a0ba44a37e1a0cb4d6584f104ca06a4ffa39f1d9f93e60e4ce7e9c26152ea910152d8afc91cf9418ad1b9e92a61bf9e0aee8b1eaf8bc5f758994c273e6')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Build fix with new libdbusmenu
  patch -Np1 -i "${srcdir}/build_fix_with_libdbusmenu.patch"

  # Cannot find libindicator
  CFLAGS="${CFLAGS} $(pkg-config --cflags --libs indicator-0.4)"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-appmenu() {
  pkgdesc="Indicator for application menus - GTK 3 library"
  depends=('gtk3-ubuntu' 'libindicator3' 'libdbusmenu-gtk3' 'libbamf3')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make DESTDIR="${pkgdir}/" install
}

package_indicator-appmenu-gtk2() {
  pkgdesc="Indicator for application menus - GTK 2 library"
  depends=('gtk2-ubuntu' 'libindicator3' 'libdbusmenu-gtk2' 'libbamf' 'indicator-appmenu')
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make -C src DESTDIR="${pkgdir}/" install
}
