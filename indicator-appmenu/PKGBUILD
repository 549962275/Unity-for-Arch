# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=indicator-appmenu
pkgname=('indicator-appmenu' 'indicator-appmenu-gtk2' 'indicator-appmenu-tools')
_ubuntu_rel=0ubuntu2
pkgver=0.3.91.${_ubuntu_rel}
pkgrel=100
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
options=('!libtool' '!emptydirs')
source=("http://launchpad.net/${pkgbase}/0.4/${pkgver%.*}/+download/${pkgbase}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('4599fc36aaea13ca27a428561f17c66c6d44462059fddcfbfe5b288089b4092c12da3b03845c87c48fb42385d9afe5f37c10205e9537f91053fa208bb414c878'
            '0d496decfeb636b09d5ed91a3dbc36d94be5983b0fb8ce3b311f6c0f4efa8dc8f1336d55a0d0a32114451de05b4ed0b5d7472e2c4326ce98434d1380518653a1')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgbase}_${pkgver%.*}-${_ubuntu_rel}.diff"
  for i in $(cat debian/patches/series | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi
  intltoolize -f

  # Disable -Werror
  CFLAGS="${CFLAGS} -Wno-error"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=2
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --with-gtk=3
  make ${MAKEFLAGS}
  popd
}

package_indicator-appmenu() {
  pkgdesc="Indicator for application menus - GTK 3 library"
  depends=('gtk3-ubuntu' 'libindicator3' 'libdbusmenu-gtk3' 'libbamf3')
  install="${pkgbase}.install"

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make -C data DESTDIR="${pkgdir}/" install
  make -C src DESTDIR="${pkgdir}/" install

  # Remove files that go in indicator-appmenu-tools
  rm "${pkgdir}"/usr/bin/hud-{cli,dump-application,list-applications,verify-app-info}
}

package_indicator-appmenu-gtk2() {
  pkgdesc="Indicator for application menus - GTK 2 library"
  depends=('gtk2-ubuntu' 'libindicator3' 'libdbusmenu-gtk2' 'libbamf' 'indicator-appmenu')

  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk2"

  make -C data DESTDIR="${pkgdir}/" install
  make -C src DESTDIR="${pkgdir}/" install

  # Remove files that don't belong here
  rm "${pkgdir}"/usr/bin/hud-{cli,dump-application,list-applications,verify-app-info}
  rm "${pkgdir}/usr/lib/indicator-appmenu/hud-service"
  rm "${pkgdir}"/usr/share/glib-2.0/schemas/com.canonical.indicator.appmenu.{,hud.{,search.}}gschema.xml
  rm "${pkgdir}/usr/share/dbus-1/services/com.canonical.hud.service"
}

package_indicator-appmenu-tools() {
  pkgdesc="Indicator for application menus - Tools"
  depends=('indicator-appmenu')

  # Doesn't matter which one is used
  cd "${srcdir}/${pkgbase}-${pkgver%.*}/build-gtk3"

  make -C docs DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install
  make -C src DESTDIR="${pkgdir}/" install

  # Remove files that belong in the other packages
  rm "${pkgdir}/usr/lib/indicators3/7/libappmenu.so"
  rm "${pkgdir}/usr/lib/indicator-appmenu/hud-service"
}
