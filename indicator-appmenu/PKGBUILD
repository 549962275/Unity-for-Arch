# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-appmenu
_actual_ver=12.10.4
_extra_ver=daily12.12.14
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
depends=('dconf' 'libindicator3' 'libdbusmenu-gtk3' 'libbamf3')
makedepends=('vala>=0.16.0')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
provides=('indicator-appmenu-tools')
conflicts=('indicator-appmenu-tools')
options=('!libtool' '!emptydirs')
install="${pkgname}.install"
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-appmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('e861be4d1bbfa994f4699691f65a9dcb4098c038b1cc44017ce6df54cd16d843dd05a5a50d3a0a2e55b74eed9e82a79217d7fa9728c8465de9a8308168a1f3a2'
            '31b83e433824bd7acd7be93bfd60932114eff344fea77c8ea1125272d5881fd61c1609d4fbf24de0cc8838e76d8ac405e7a19162e3ce2021b6b5b6dbb61c31f8')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Autoconf fix
  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  # Disable -Werror
  CFLAGS="${CFLAGS} -Wno-error"

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
