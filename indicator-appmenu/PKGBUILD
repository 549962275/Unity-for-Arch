# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-appmenu
_actual_ver=12.10.4
_extra_ver=daily13.01.25
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
depends=('dconf' 'libappindicator3' 'libindicator3' 'libdbusmenu-gtk3' 'libbamf3')
makedepends=('gtk-doc' 'vala')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
provides=('indicator-appmenu-tools')
conflicts=('indicator-appmenu-tools')
options=('!libtool' '!emptydirs')
install="${pkgname}.install"
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-appmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch'
        'revert_r229.patch')
sha512sums=('46bbc2cbf858ff57da45d90991bd10d1e730ca74704c36c4cdb05b460e2259df4fbd42dc4d5dc67296b801d5444bc59768f4aeb719938788bb254bf1922dd105'
            '31b83e433824bd7acd7be93bfd60932114eff344fea77c8ea1125272d5881fd61c1609d4fbf24de0cc8838e76d8ac405e7a19162e3ce2021b6b5b6dbb61c31f8'
            '7752e92933f2dca8eeab4df54930e15e7550c2ba14e1e0fa6e53a2ef59dbdabf5e74ec460f65ed9c58007cf6560dc49a868f4a2047065c87c11998903348404a')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Autoconf fix
  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  patch -p0 -i "${srcdir}/revert_r229.patch"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  # Disable -Werror
  CFLAGS="${CFLAGS} -Wno-error"

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
