# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: thn81 <root@scrat>

pkgname=evemu
pkgver=1.2.0
pkgrel=100
pkgdesc="Linux Evdev Event Emulation"
arch=('i686' 'x86_64')
url="https://launchpad.net/evemu"
license=('GPL')
depends=('glibc')
makedepends=('xmlto' 'asciidoc')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/evemu_${pkgver}.orig.tar.xz")
options=('!libtool')
sha512sums=('41bef7b2e0f0769e4affb5633f41e5352224f5acfb5a001a27548f2236a1dc23d444bbfc3e93ea8eb518a7f4e628185eef9ca5ecefa4d87b201224bfeee28044')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1s/python/python2/' src/make-event-names.py
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
