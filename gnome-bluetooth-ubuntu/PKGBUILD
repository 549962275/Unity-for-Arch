# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth-ubuntu
_ubuntu_rel=0ubuntu1
_actual_ver=3.6.1
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libappindicator3' 'libnotify')
makedepends=('intltool' 'nautilus-sendto' 'gobject-introspection' 'itstool' 'docbook-xsl' 'gtk-doc')
provides=("gnome-bluetooth=${pkgver}")
conflicts=('gnome-bluetooth')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz"
        '61-gnome-bluetooth-rfkill.rules')
sha512sums=('9ae64b7d2edc308244a2ece81b1700524da89730a04df9dfbf81999af601ab23f7ed1bf6056bf39787a153f8c59fd354ac4713b54ef31bd4ec530d60fd229a62'
            '39e9eea012e6f88e828f3cd93f84d9ecfffe97f60b74738d720827f97985e189ebb4a58af2589410ce616ed57e80488776b2c65f161db7c09d5c95e41e287970'
            '80b7fd0bc3e0f82c8d2d609ffed634200e63f61d9a134445df8e8511756b7fb423d338a1d12e42b91c57243097beaa3d4662856ec0507fb19a29b6de7ee9e4de')

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  intltoolize --force

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 "${srcdir}/61-gnome-bluetooth-rfkill.rules" "${pkgdir}/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/debian/bluetooth-applet-unity.desktop" "${pkgdir}/etc/xdg/autostart/"
}

# vim:set ts=2 sw=2 et:
