# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth-ubuntu
_ubuntu_rel=1ubuntu1
pkgver=3.2.1.${_ubuntu_rel}
pkgrel=2
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection')
provides=("gnome-bluetooth=${pkgver}")
conflicts=('gnome-bluetooth')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/g/${pkgname%-*}/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        '61-gnome-bluetooth-rfkill.rules')
sha512sums=('96df759b63aa6e056828ba8f668567c72ce217a3091bbad618cd2a575eb3881a384f4cbd2bf6b1fdd0440ed9b274fc932377aaf36ef8d134038320ed35d1b241'
            '431862a9388046ac8142d37d2220ffa12f1c4068e37b4df834c2239a780f3388fd6db6fe94e6abd3958a899ccde0b41903a3fde31e8c438de2ae8c42ed5ba261'
            '80b7fd0bc3e0f82c8d2d609ffed634200e63f61d9a134445df8e8511756b7fb423d338a1d12e42b91c57243097beaa3d4662856ec0507fb19a29b6de7ee9e4de')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  #Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install

  install -Dm644 "${srcdir}/61-gnome-bluetooth-rfkill.rules" "${pkgdir}/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
  install -dm755 "${pkgdir}/etc/ld.so.conf.d/"
  echo "/usr/lib/gnome-bluetooth" > "${pkgdir}/etc/ld.so.conf.d/${pkgname%-*}.conf"
  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/debian/bluetooth-applet-unity.desktop" "${pkgdir}/etc/xdg/autostart/"
}
