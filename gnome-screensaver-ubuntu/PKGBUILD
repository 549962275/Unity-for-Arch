# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-screensaver-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=3.4.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=('etc/pam.d/gnome-screensaver')
depends=('dbus-glib' 'libgnomekbd' 'gnome-desktop' 'gsettings-desktop-schemas' 'libsystemd')
makedepends=('pkgconfig' 'intltool' 'libxss')
provides=("gnome-screensaver=${pkgver}")
conflicts=('gnome-screensaver')
groups=('gnome')
options=('!emptydirs')
install=gnome-screensaver.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'gnome-screensaver.pam')
sha512sums=('441e38de4b119019a502ded401bb95ddd55e5cab7134bc72cca550e510a37c22e1c97b89db6ee71e84551ac2f131260b66ae0ba1fde736416b0d27c9492f6fd7'
            '32a1e5978f6c7f3cb53741a4f620c322b2041d51702ee1c332c4e1d092114ac32628112852c6c22abe4d3e33ddb40b18b5d04465afc0dbeb5ba0876e806117da'
            'aed0933feed70a9b16da80d6e4716641cd45d2fd3fadbace3dfeb02b1a07124ee659f470277f8ce93492c6f7cf3cc9996bc60598724eea1955975604ff8f324d')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname%-*} \
    --localstatedir=/var \
    --with-mit-ext \
    --with-systemd

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 "${srcdir}/gnome-screensaver.pam" "${pkgdir}/etc/pam.d/gnome-screensaver"

  # From debian/rules
  install -dm755 "${pkgdir}/usr/share/gnome/autostart/"
  mv "${pkgdir}/etc/xdg/autostart/gnome-screensaver.desktop" "${pkgdir}/usr/share/gnome/autostart/"
}
