# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

# NOTE: Change the following to true to compile in systemd support
USE_SYSTEMD=false

pkgname=gnome-screensaver-ubuntu
_ubuntu_rel=0ubuntu1
pkgver=3.2.1.${_ubuntu_rel}
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=('etc/pam.d/gnome-screensaver')
depends=('dbus-glib' 'libgnomekbd' 'gnome-desktop' 'gsettings-desktop-schemas')
if [[ "${USE_SYSTEMD}" == "true" ]]; then
  depends=(${depends[@]} 'libsystemd')
else
  CONFIGURE_OPTS='--without-systemd'
fi
makedepends=('pkgconfig' 'intltool' 'libxss')
provides=("gnome-screensaver=${pkgver}")
conflicts=('gnome-screensaver')
groups=('gnome')
options=(!emptydirs)
install=gnome-screensaver.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'gnome-screensaver.pam')
sha512sums=('a23ea94ae7fb4b16e2b8ea3755df02b8242c2390caf62241934cc8bb2b3fa1eafa087c442b2ece4a1f9bef928561b8308f5950be5dfaeb68fb4f26753fdd056a'
            '9f6368fa74bae80d0e2367ac29b9a761c79bd94ec9292b59af220ab0d9e9439207e40980e93ff36f63e4ea577e9d5544ee3cd8e44c7b73c350cfa6b2c8a17d41'
            '5eaf5f9982d949efd34595761d1c96e9f5d71fc892b0fdd0e1877d8aada3d97dac4bf789c241bfbf4cd9e9c867a3e2365fa5e855580542b96af76fc1d5bff92d')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gnome-screensaver \
    --localstatedir=/var \
    --with-mit-ext \
    --enable-locking \
    ${CONFIGURE_OPTS}

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 "${srcdir}/gnome-screensaver.pam" "${pkgdir}/etc/pam.d/gnome-screensaver"

  # From debian/rules
  install -dm755 "${pkgdir}/usr/share/gnome/autostart/"
  mv "${pkgdir}/etc/xdg/autostart/gnome-screensaver.desktop" "${pkgdir}/usr/share/gnome/autostart/"
}
