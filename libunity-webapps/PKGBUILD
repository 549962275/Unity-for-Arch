# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libunity-webapps
_actual_ver=2.4.3
_extra_ver=daily13.02.12
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="WebApps: Library for the integration with the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-webapps"
license=('LGPL')
groups=('unity')
depends=('geoclue' 'glib2' 'hicolor-icon-theme' 'indicator-messages' 'json-glib' 'libdbusmenu-gtk3' 'libindicate' 'libindicate-gtk3' 'libnotify' 'libsoup' 'libunity' 'libwnck3' 'packagekit' 'polkit' 'sqlite' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool')
options=('!libtool')
install=libunity-webapps.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('90bf5ea6726f963cfc034a3a7f9feee4669248126bd65934926cadf54bb11f91a9866d30405026360c0c091596e09f24c146acbe55a3207b1b7c7e10e980412f')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/libunity-webapps --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
