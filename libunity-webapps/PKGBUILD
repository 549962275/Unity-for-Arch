# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libunity-webapps
_ubuntu_rel=0ubuntu1
_actual_ver=2.4.3
_extra_ver=daily13.01.10
pkgver=${_actual_ver}${_extra_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="WebApps: Library for the integration with the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-webapps"
license=('LGPL')
groups=('unity')
depends=('geoclue' 'glib2' 'hicolor-icon-theme' 'indicator-messages' 'json-glib' 'libdbusmenu-gtk3' 'libindicate' 'libindicate-gtk3' 'libnotify' 'libsoup' 'libunity' 'libwnck3' 'packagekit' 'polkit' 'sqlite' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool')
options=('!libtool')
install=libunity-webapps.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('adaa0b7760d8144fc72a2f9ba38353b56e004607a2898661bcf76994e36f8acfd54f98aa91896e085057c2340b9a6b657ce7a8e78cedd5c99dec2ec2267aa443'
            '376bee0979ddb5f678da48f1b9367681244de4ec0aa0d2b0ff9389e5619f75c31d3d48acee84ab8e35f9ac0f0a8e79cf04bbd0853be167e5abe8eb1ede4ced7c')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
  #for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
  #  patch -p1 -i "${srcdir}/debian/patches/${i}"
  #done

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/libunity-webapps --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
