# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libunity-webapps
_actual_ver=2.5.0
# ~+ ??
_extra_ver=~+14.04.20140409
pkgver=${_actual_ver}.daily14.04.09
pkgrel=1
pkgdesc="WebApps: Library for the integration with the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-webapps"
license=('LGPL')
groups=('unity')
depends=('geoclue' 'hicolor-icon-theme' 'indicator-messages' 'json-glib' 'libdbusmenu-glib' 'libnotify' 'libsoup' 'libunity' 'libwnck3' 'packagekit' 'polkit' 'sqlite')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool')
# configure.ac probably needs to be updated
makedepends+=('telepathy-glib' 'libdbusmenu-gtk3')
install=libunity-webapps.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('fc1c9ad160a0b3536973838b3966caa5756af169df9a2e514f57d6b40a0a535c519ab1dae60a0429587368ac9cea1b2bccec120806e9e0eb73529271e362bc95')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/libunity-webapps --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

# vim:set ts=2 sw=2 et:
