# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-lens-video
_actual_ver=0.3.14
_extra_ver=daily13.03.04
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=102
pkgdesc="Unity Video lens"
arch=('any')
url="https://launchpad.net/unity-lens-videos"
license=('GPL')
depends=('dee' 'json-glib' 'libgee' 'libsoup-gnome' 'libunity' 'libzeitgeist' 'unity-lens-music')
makedepends=('gnome-common' 'intltool' 'vala')
checkdepends=('xorg-server-xvfb')
provides=("unity-scope-video-remote=${pkgver}")
conflicts=('unity-scope-video-remote')
replaces=('unity-scope-video-remote')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-video_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('afa125498664a652f88d2ae26cf310c5f81868b78f86c3ac4c6de3426db820a8b238bd7259850451bac69a6b68b39fa27f0ef9fb20ab244435c4367530c2fdd1')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --enable-headless-tests
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
