# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee1
pkgver=0.6.8
pkgrel=1
pkgdesc="GObject collection library"
url="http://live.gnome.org/Libgee"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('gobject-introspection')
conflicts=("libgee<=${pkgver}")
options=('!libtool')
source=("http://ftp.gnome.org/pub/GNOME/sources/libgee/${pkgver%.*}/libgee-${pkgver}.tar.xz")
sha512sums=('b26d36ee9fc0c5c214521ad97bc20f11f1191a3dab72a5f1af2f6435f51ff2f28e81ccb26fac399939c1a7eaad519db98032d3bc593a4052463acd91bb12902a')

build() {
  cd "${srcdir}/libgee-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/libgee-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/libgee-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
