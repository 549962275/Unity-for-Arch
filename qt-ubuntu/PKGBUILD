# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# AUR fix
pkgname=qt-ubuntu
pkgdesc='A cross-platform application and UI framework'

pkgbase=qt-ubuntu
true && pkgname=('qt-ubuntu' 'qt-private-headers-ubuntu')
_ubuntu_rel=1ubuntu10
pkgver=4.8.0.${_ubuntu_rel}
pkgrel=101
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus' 'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs' 'mysql' 'unixodbc' 'cups' 'gtk2' 'freetds' 'libfbclient')
options=('!libtool')
_pkgfqn="${pkgbase%-*}-everywhere-opensource-src-${pkgver%.*}"
source=("http://get.qt.nokia.com/qt/source/${_pkgfqn}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/qt4-x11_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        'fix-qgraphicsscene-regression.patch'
        'improved-filter-event.patch'
        'qurl-backward-compatibility.patch')
sha512sums=('b37f2d5fe3cebdcc7ebd5b6bc68ea03f4cabeadc492c77d7c18d00ede5c254adb57fcbe5cec6326072304db49e69e59c14af7e90875138c3b2f255237a99a78f'
            '17518a447cb31b0da4ab65921e880e579e73ff3d7de63f8b9d386a234451bda971272cea8e409b48146b8ef9ca72e535929754610e3e4d974e08ec5a94daea48'
            '5b5bdf98eaba6cbbd5e9245fcd34bdccdc0dbf4dc84e311fb3967c82a586426b62e8f616d5b84e02a434fdee185fe30ea1b3949061f7b805112adbe132822068'
            '9d3675b4773c8979268ab02e81779804ebc83c128d6be3df17321e251e11caa2b4ea8f5b98743435557cdd6fecf80e5542dd7c54fa7cc544409ecf7da8e95037'
            'c9d5861a486d09d004cc32f49a9c981876835ab7e4994ac21bd89b0a9236d77a04a7490306a44f6fff06d3be8ce4b344fb63603e500cabf4b9f8e2ef1e4d8c58')

build() {
  cd "${srcdir}/${_pkgfqn}"

  # Apply Arch Linux patches
    # (FS#28707)
      patch -p1 -i "${srcdir}"/fix-qgraphicsscene-regression.patch
    # (FS#27757) (KDEBUG#275469)
      patch -p1 -i "${srcdir}"/improved-filter-event.patch
      patch -p1 -i "${srcdir}"/qurl-backward-compatibility.patch

  # Apply Ubuntu patches
    # Disable patches (longest command you've ever seen? :D) (can't use
    # punctuation for comments or editors will completely screw up the
    # syntax highlighting)
      sed -i \
      `# Patch for adding '-qt4' to the end of the filenames of binaries` \
        -e '/01_debian_append_qt4_suffix.diff/d'                          \
      `# Multiarch patch for Debian/Ubuntu`                               \
        -e '/qt-multiarch-plugin-path-compat.diff/d'                      \
      `# Debian architecture detection {fails with:`                      \
      `# "Qt has not been ported to this architecture"}`                  \
        -e '/07_trust_dpkg-arch_over_uname-m.diff/d'                      \
      `# ARM v6 entry for previous patch`                                 \
        -e '/94_armv6_uname_entry.diff/d'                                 \
      `# Disable Qt webkit {exists in separate package}`                  \
        -e '/16_hide_std_symbols_on_qtwebkit.diff/d'                      \
        -e '/18_enable_qt3support_qtwebkit_debug_info.diff/d'             \
        -e '/30_webkit_unaligned_access.diff/d'                           \
        -e '/96_webkit_no_gc_sections.diff/d'                             \
        -e '/kubuntu_17_enable_qtwebkit_for_qtassistant.diff/d'           \
        -e '/kubuntu_01_fix_build_glib_231.diff/d'                        \
        -e '/11_build_translations.diff/d'                                \
      `# Disable enabling qvfb {does that make sense? :D} - it requires`  \
      `# the built-in Qt webkit`                                          \
        -e '/20_install_qvfb.diff/d'                                      \
        -e '/fix_qvfb_build.patch/d'                                      \
      `# Debug packages arent used in Arch Linux - remove patch to`       \
      `# default to keeping debug symbols`                                \
        -e '/12_add_nostrip_for_debug_packages.diff/d'                    \
      `# Breaks build`                                                    \
        -e '/kubuntu_97_a11y_qt_and_qml_backport.diff/d'                  \
      `# Unsupported CPU architectures`                                   \
        -e '/add_missing_method_for_QBasicAtomicPointer_on_s390.patch/d'  \
        -e '/powerpc_designer_gstabs.diff/d'                              \
        -e '/kfreebsd_monotonic_clock.diff/d'                             \
        -e '/sh.diff/d'                                                   \
        -e '/powerpcspe.diff/d'                                           \
        -e '/99_hppa_bug561203_decrease_failure_rate.diff/d'              \
        -e '/92_armel_gcc43_valist_compat.diff/d'                         \
        -e '/80_hurd_max_path.diff/d'                                     \
        -e '/71_hppa_unaligned_access_fix_458133.diff/d'                  \
        -e '/70_hppa_ldcw_fix.diff/d'                                     \
        -e '/51_kfreebsd_strnstr_build_fix.diff/d'                        \
        -e '/50_kfreebsd_Q_OS.diff/d'                                     \
        -e '/41_disable_opengl_visibility.diff/d'                         \
        -e '/40_alpha_ice.diff/d'                                         \
      `# Demos arent built on Arch Linux`                                 \
        -e '/buildable_appchooser_states_demos.patch/d'                   \
      `# This is not Debian or Ubuntu`                                    \
        -e '/08_configure_quilt_compat.diff/d'                            \
        -e '/fix_ftbfs_format-security.patch/d'                           \
      `# Do not default to ibus`                                          \
        -e '/kubuntu_10_ibus_input_method.diff/d'                         \
      `# Tests arent built`                                               \
        -e '/10_config_tests_fixes.diff/d'                                \
      `# Not needed for rolling release distros`                          \
        -e '/23_permit_plugins_built_with_future_qt.diff/d'               \
      \
      "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Workaround the error:
  # error: ‘struct’ tag used in naming ‘union _GMutex’
  CFLAGS="${CFLAGS} -fpermissive"
  CXXFLAGS="${CXXFLAGS} -fpermissive"

  # Workaround error with new glib
  #CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"
  #CXXFLAGS="${CXXFLAGS} -Wno-error=deprecated-declarations"

  # More workarounds
  CFLAGS="${CFLAGS} -Wno-error"
  CXXFLAGS="${CXXFLAGS} -Wno-error"

  export QT4DIR="${srcdir}/${_pkgfqn}"
  export LD_LIBRARY_PATH="${QT4DIR}/lib:${LD_LIBRARY_PATH}"

  sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/{g++,gcc}-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/gcc-base.conf

  # From debian/rules:
  # Remove include directory. Then ./configure will take care of calling
  # syncqt and regenerating it.
  rm -rvf include

  ./configure \
    `# License` \
    -confirm-license \
    -opensource \
    `# Paths and prefixes` \
    -datadir /usr/share/qt \
    -demosdir /usr/share/doc/qt/demos \
    -docdir /usr/share/doc/qt \
    -examplesdir /usr/share/doc/qt/examples \
    -importdir /usr/lib/qt/imports \
    -plugindir /usr/lib/qt/plugins \
    -prefix /usr \
    -sysconfdir /etc/xdg \
    -translationdir /usr/share/qt/translations \
    `# Enable features` \
    -plugin-sql-mysql \
    -plugin-sql-odbc \
    -plugin-sql-psql \
    -plugin-sql-sqlite \
    `# Disable features` \
    -no-openvg \
    -no-phonon \
    -no-phonon-backend \
    -no-webkit \
    `# Configuration options` \
    -graphicssystem raster \
    -optimized-qmake \
    -reduce-relocations \
    -system-sqlite \
    `# Build options` \
    -dbus-linked \
    -no-rpath \
    -nomake demos \
    -nomake docs \
    -nomake examples \
    -openssl-linked \
    -verbose

  make ${MAKEFLAGS}
}

package_qt-ubuntu() {
  pkgdesc='A cross-platform application and UI framework'
  depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus' 'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib' 'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'libxi')
  conflicts=('qt')
  provides=("qt=${pkgver}")
  optdepends=('postgresql-libs: PostgreSQL driver'
              'libmysqlclient: MySQL driver'
              'unixodbc: ODBC driver'
              'freetds: Sybase and MS SQL driver'
              'libfbclient: Firebird driver'
              'libxinerama: Xinerama support'
              'libxcursor: Xcursor support'
              'libxfixes: Xfixes support')
  install='qt.install'

  cd "${srcdir}/${_pkgfqn}"
  make INSTALL_ROOT="${pkgdir}" install

  ### Tnstall desktop files and icons ###
  install -d -m755 "${pkgdir}/usr/share/applications/"

  # Qt Designer
  sed 's/-qt4//g' < "${srcdir}/debian/desktop/designer-qt4.desktop" > "${pkgdir}/usr/share/applications/designer.desktop"
  install -p -D -m644 tools/designer/src/designer/images/designer.png "${pkgdir}/usr/share/pixmaps/designer.png"
  # Qt Assistant
  sed 's/-qt4//g' < "${srcdir}/debian/desktop/assistant-qt4.desktop" > "${pkgdir}/usr/share/applications/assistant.desktop"
  install -p -D -m644 tools/assistant/tools/assistant/images/assistant.png "${pkgdir}/usr/share/pixmaps/assistant.png"
  # Qt Linguist
  sed 's/-qt4//g' < "${srcdir}/debian/desktop/linguist-qt4.desktop" > "${pkgdir}/usr/share/applications/linguist.desktop"
  install -p -D -m644 tools/linguist/linguist/images/icons/linguist-128-32.png "${pkgdir}/usr/share/pixmaps/linguist.png"
  # Qt Config
  sed 's/-qt4//g' < "${srcdir}/debian/desktop/qtconfig-qt4.desktop" > "${pkgdir}/usr/share/applications/qtconfig.desktop"
  install -p -D -m644 src/gui/dialogs/images/qtlogo-64.png "${pkgdir}/usr/share/pixmaps/qtconfig.png"

  ### Install manpages from Debian ###
  install -d -m755 "${pkgdir}/usr/share/man/man1/"
  for i in "${srcdir}"/debian/manpages/*.1; do
    FILENAME="${i##*/}"
    FILENAME="${FILENAME/-qt4/}"
    sed -e 's/-qt4//g' -e 's/-QT4//g' < "${i}" > "${pkgdir}/usr/share/man/man1/${FILENAME}"
  done

  ### Install license files ###
  install -D -m644 LGPL_EXCEPTION.txt "${pkgdir}/usr/share/licenses/qt/LGPL_EXCEPTION.txt"

  ### Fix path in pkgconfig files ###
  find "${pkgdir}/usr/lib/pkgconfig" -type f -name '*.pc' -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;

  ### Fix path in prl files ###
  find "${pkgdir}/usr/lib" -type f -name '*.prl' -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
}

package_qt-private-headers-ubuntu(){
  pkgdesc="Qt private headers for development"
  depends=("qt-ubuntu=${pkgver}")
  conflicts=('qt-private-headers')
  provides=("qt-private-headers=${pkgver}")

  install -d -m755 "${pkgdir}"/usr/include/{QtCore,QtDeclarative,QtGui,QtScript}
  install -d -m755 "${pkgdir}"/usr/src/{corelib,declarative,gui,script}
    
  for i in QtCore QtDeclarative QtGui QtScript; do
    cp -r "${srcdir}/${_pkgfqn}/include/${i}/private/" "${pkgdir}/usr/include/${i}/"
  done

  for i in corelib declarative gui script; do
    cp -r "${srcdir}/${_pkgfqn}/src/${i}" "${pkgdir}/usr/src/"
  done
}
