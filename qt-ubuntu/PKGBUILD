# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: thn81 <root@scrat>

pkgbase=qt-ubuntu
pkgname=('qt-ubuntu' 'qt-private-headers-ubuntu')
_ubuntu_ver=0ubuntu8
pkgver=4.7.4.${_ubuntu_ver}
pkgrel=3
arch=('i686' 'x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
makedepends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils' 'mesa' 'postgresql-libs'
    'mysql' 'unixodbc' 'cups' 'gtk2')
options=('!libtool')
_pkgfqn="qt-everywhere-opensource-src-${pkgver%.*}"
source=("ftp://ftp.qt.nokia.com/qt/source/${_pkgfqn}.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/q/qt4-x11/qt4-x11_${pkgver%.*}-${_ubuntu_ver}.debian.tar.gz"
        'assistant.desktop' 'designer.desktop' 'linguist.desktop'
        'qtconfig.desktop'
        'blacklist-diginotar-certs.diff')
sha512sums=('8dcb7d2e9069205bd80eeecbeb62a6ab727f9e327cbfc03003fe4f1caea364771d3c7e3a12347644aef1c51898680c08693cc76e2950ea8f7487223404b6581c'
            '4344c51d1e28d718728a90fb04c1732c4d717e1ca3503fb8b86bbecc4605ab2d91374d4474885e02a49207b612d2fd6ce4a02f10774d0dbb271bfa0dc18311e3'
            'c74f192385cf39c6b243972fad80b33e8f1dde15e20619b900ee38618a5145677ec2d13857dba1a1ae75d38e05946197210fa3a664ff77ec5be4ae7e880d3ca1'
            '12aa3b9ca924e3d366ab41b8b3e86d92a4aa5f0bf83662211df27991bb45b7c30e10c3ea5b868b849c3bff4b54f7fe61f124dc2cbfd3dc30f4e6415d5c6b2869'
            '210a0d1c57ce9cb939750adfcafb43b719d1d2502f1a65797ac3b5a567d42f7233fc58a23ca78087c2b5c108abb00f48854be954dff53d3f8361e364de694eaa'
            'bac68d61c2356100bc13a6d4b3a4c3330f6263b9701fc176158618de6eff97cbaed322cc52d6f44b7cbda628969d671b38d7b919fa63116171629b47451f051c'
            'b98076753100e41015b4472cd4d5a97e631f73504affaea985e6bf042339a05eba182200d522a57ed08568ef047418a60062416df69872d7fd3b1acbd77eedfd')

build() {
  unset QMAKESPEC
  export QT4DIR=${srcdir}/${_pkgfqn}
  export PATH=${QT4DIR}/bin:${PATH}
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

  # FS#24601
  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"

  cd "${srcdir}/${_pkgfqn}"

  #Apply Ubuntu patches

  #Patches that don't apply to Arch Linux
    #This patch determines the CPU architecture with dpkg (Debian's package manager)
      #It will fail with an unknown architecture error in Arch Linux.
      sed -i '/07_trust_dpkg-arch_over_uname-m.diff/d' "${srcdir}/debian/patches/series"
    #ARM v6 entry for previous patch (dependent on it)
      sed -i '/94_armv6_uname_entry.diff/d' "${srcdir}/debian/patches/series"
    #Multiarch
      sed -i '/qt-multiarch-plugin-path-compat.diff/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  #Already done by Ubuntu
  #patch -Np1 -i "${srcdir}"/blacklist-diginotar-certs.diff

  #Build fix
  cp src/declarative/accessible/qdeclarativeaccessible_p.h include/QtDeclarative/private/

  sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/g++.conf
  sed -i "/^QMAKE_RPATH/s| -Wl,-rpath,||g" mkspecs/common/g++.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/g++.conf

  ./configure \
    -confirm-license \
    -opensource \
    -prefix /usr \
    -docdir /usr/share/doc/qt \
    -plugindir /usr/lib/qt/plugins \
    -importdir /usr/lib/qt/imports \
    -datadir /usr/share/qt \
    -translationdir /usr/share/qt/translations \
    -sysconfdir /etc \
    -examplesdir /usr/share/doc/qt/examples \
    -demosdir /usr/share/doc/qt/demos \
    -largefile \
    -plugin-sql-{psql,mysql,sqlite,odbc} \
    -system-sqlite \
    -xmlpatterns \
    -no-phonon \
    -no-phonon-backend \
    -svg \
    -webkit \
    -script \
    -scripttools \
    -system-zlib \
    -system-libtiff \
    -system-libpng \
    -system-libmng \
    -system-libjpeg \
    -nomake demos \
    -nomake examples \
    -nomake docs \
    -no-rpath \
    -openssl-linked \
    -v \
    -optimized-qmake \
    -dbus \
    -reduce-relocations \
    -no-separate-debug-info \
    -gtkstyle \
    -opengl \
    -no-openvg \
    -glib
  make -j8
}

package_qt-ubuntu() {
  pkgdesc="A cross-platform application and UI framework with Ubuntu's patches"
  depends=('libtiff' 'libpng' 'libmng' 'sqlite3' 'ca-certificates' 'glib2' 'dbus'
    'fontconfig' 'libgl' 'libsm' 'libxrandr' 'libxv' 'libxi' 'alsa-lib'
    'xdg-utils' 'hicolor-icon-theme' 'desktop-file-utils')
  optdepends=('postgresql-libs: PostgreSQL driver'
              'libmysqlclient: MySQL driver'
              'unixodbc: ODBC driver'
              'libxinerama: Xinerama support'
              'libxcursor: Xcursor support'
              'libxfixes: Xfixes support')
  provides=("qt=${pkgver%.*}")
  conflicts=('qt')
  install='qt.install'
	
  cd "${srcdir}/${_pkgfqn}"
    make INSTALL_ROOT="${pkgdir}" install

    # install missing icons and desktop files
    for icon in tools/linguist/linguist/images/icons/linguist-*-32.png ; do
      size=$(echo $(basename ${icon}) | cut -d- -f2)
      install -p -D -m644 ${icon} "${pkgdir}"/usr/share/icons/hicolor/${size}x${size}/apps/linguist.png
    done
    install -p -D -m644 src/gui/dialogs/images/qtlogo-64.png "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/qtlogo.png
    install -p -D -m644 tools/assistant/tools/assistant/images/assistant.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/assistant.png
    install -p -D -m644 tools/designer/src/designer/images/designer.png "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/designer.png
    install -d "${pkgdir}"/usr/share/applications
    install -m644 ${srcdir}/{linguist,designer,assistant,qtconfig}.desktop "${pkgdir}"/usr/share/applications/

    # install license addition
    install -D -m644 LGPL_EXCEPTION.txt "${pkgdir}"/usr/share/licenses/qt/LGPL_EXCEPTION.txt

    # Fix wrong path in pkgconfig files
    find "${pkgdir}"/usr/lib/pkgconfig -type f -name '*.pc' \
      -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
    # Fix wrong path in prl files
    find ${pkgdir}/usr/lib -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
}

package_qt-private-headers-ubuntu(){
  pkgdesc="Qt private headers for development with Ubuntu's patches"
  depends=("qt-ubuntu=${pkgver%.*}")
  provides=("qt-private-headers=${pkgver%.*}")
  conflicts=('qt-private-headers')

  install -d "${pkgdir}"/usr/include/{QtCore,QtDeclarative,QtGui,QtScript}
  install -d "${pkgdir}"/usr/src/{corelib,declarative,gui,script}
    
  for i in QtCore QtDeclarative QtGui QtScript; do
    cp -r "${srcdir}"/$_pkgfqn/include/${i}/private/ \
      "${pkgdir}"/usr/include/${i}/
  done

  for i in corelib declarative gui script; do
    cp -r "${srcdir}"/$_pkgfqn/src/${i} "${pkgdir}"/usr/src/
  done
}
