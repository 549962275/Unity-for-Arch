# Maintainer: Andrew Gunnerson <andrewgunnerson@gmail.com>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty

pkgname=appmenu-qt
_actual_ver=0.2.7
_extra_ver=+14.04.20140305
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Adds support for application menus to Qt4"
arch=(i686 x86_64)
url="https://launchpad.net/appmenu-qt"
license=(LGPL)
groups=(unity)
depends=(libdbusmenu-qt4)
makedepends=(cmake)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('3f846e8a3a1b6a976ad5d05f539d56d20a44b41381f63da205f20729dc95f02eea8efc557aa1d8356093c544d41a7c27a960e3669fc387563b3c501b0568d51e')

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}" install
}
